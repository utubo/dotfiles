vim9script noclear
set encoding=utf-8
scriptencoding utf-8

# ----------------------------------------------------------
# åŸºæœ¬è¨­å®š {{{
set fileencodings=ucs-bom,utf-8,iso-2022-jp,cp932,euc-jp
set noexpandtab
set tabstop=3 # æ„å¤–ã¨ã‚ã‚Šãªæ°—ãŒã—ã¦ããŸâ€¦
set shiftwidth=0
set softtabstop=0
set autoindent
set smartindent
set breakindent
set backspace=indent,start,eol
set nf=alpha,hex
set virtualedit=block
set list
set listchars=tab:\|\ ,trail:-,extends:>,precedes:<,nbsp:%
set fillchars=
set cmdheight=1
set noshowcmd
set noshowmode
set display=lastline
set ambiwidth=double
set belloff=all
set ttimeoutlen=50
set wildmenu
set autochdir
set backupskip=/var/tmp/*
set undodir=~/.vim/undo
set undofile
set updatetime=2000
set incsearch
set hlsearch

augroup vimrc
	# æ–°ã—ã„è‡ªç”±
	au!
augroup End
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ {{{
const rtproot = has('win32') ? '~/vimfiles' : '~/.vim'
const has_deno = executable('deno')

# ã“ã‚“ãªæ„Ÿã˜
# MultiCmd nmap,vmap xxx yyy<if-nmap>NNN<if-vmap>VVV<if-*>zzz
# â†“
# nmap xxx yyyNNNzzz | vmap xxx yyyVVVzzz
def MultiCmd(qargs: string)
	const [cmds, args] = qargs->split('^\S*\zs')
	for cmd in cmds->split(',')
		const a = args
			->substitute($'<if-{cmd}>', '<if-*>', 'g')
			->substitute('<if-[^*>]\+>.\{-1,}\(<if-\*>\|$\)', '', 'g')
			->substitute('<if-\*>', '', 'g')
		execute cmd a
	endfor
enddef
command! -nargs=* MultiCmd MultiCmd(<q-args>)

# ãã®ä»–
command! -nargs=1 -complete=var Enable  <args> = 1
command! -nargs=1 -complete=var Disable <args> = 0

def RemoveEmptyLine(line: number)
	silent! execute ':' line 's/\s\+$//'
	silent! execute ':' line 's/^\s*\n//'
enddef

def BufIsSmth(): bool
	return &modified || ! empty(bufname())
enddef

def IndentStr(expr: any): string
	return matchstr(getline(expr), '^\s*')
enddef

# æŒ‡å®šå¹…ä»¥ä¸Šãªã‚‰'>'ã§çœç•¥ã™ã‚‹
def TruncToDisplayWidth(str: string, width: number): string
	return strdisplaywidth(str) <= width ? str : $'{str->matchstr($'.*\%<{width + 1}v')}>'
enddef

# MoveCursorã¯å‘¼ã³å‡ºã—å›æ•°ãŒå¤šã„ã®ã§ã€ç§»å‹•é€”ä¸­ã¯ãƒ¦ãƒ¼ã‚¶ãƒ¼ã‚¤ãƒ™ãƒ³ãƒˆã§300ãƒŸãƒªç§’ã«1å›ã ã‘å®Ÿè¡Œã™ã‚‹ã‚ˆã†ã«ã™ã‚‹
const CM_DELAY_MSEC = 300
var cm_delay_timer = 0
var cm_delay_cueue = 0
def CursorMovedDelayExec(timer: any)
	cm_delay_timer = 0
	if cm_delay_cueue !=# 0
		cm_delay_cueue = 0
		doautocmd User CursorMovedDelay
	endif
enddef
def CursorMovedDelay()
	if cm_delay_timer !=# 0
		cm_delay_cueue += 1
		return
	endif
	# æœ€åˆã®1å›ã¯å³æ™‚å®Ÿè¡Œã™ã‚‹
	cm_delay_cueue = 0
	doautocmd User CursorMovedDelay
	cm_delay_timer = timer_start(CM_DELAY_MSEC, CursorMovedDelayExec)
enddef
au vimrc CursorMoved * CursorMovedDelay()

# <Cmd>ã§defã‚’å®Ÿè¡Œã—ãŸã¨ãã®ãƒ“ã‚¸ãƒ¥ã‚¢ãƒ«ãƒ¢ãƒ¼ãƒ‰ã®ç¯„å›²(è¡Œ)
def VFirstLast(): list<number>
	return mode() ==? 'V' ? sort([line('.'), line('v')]) : [line('.'), line('.')]
enddef
def VRange(): list<number>
	const a = VFirstLast()
	return range(a[0], a[1])
enddef

def GetVisualSelectionLines(): list<string>
	var v = getpos('v')[1 : 2]
	var c = getpos('.')[1 : 2]
	if c[0] < v[0]
		[v, c] = [c, v]
	endif
	var lines = getline(v[0], c[0])
	if mode() ==# 'V'
		# nop
	elseif mode() ==# 'v'
		lines[0] = lines[0][v[1] : ]
		lines[-1] = lines[-1][1 : c[1]]
	else
		var [s, e] = sort([c[1], v[1]])
		for i in range(0, len(lines) - 1)
			lines[i] = lines[i][s : e]
		endfor
	endif
	return lines
enddef

#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ {{{

# jetpack {{{
const jetpackfile = expand( $'{rtproot}/pack/jetpack/opt/vim-jetpack/plugin/jetpack.vim')
const has_jetpack = filereadable(jetpackfile)
if ! has_jetpack
  const jetpackurl = 'https://raw.githubusercontent.com/tani/vim-jetpack/master/plugin/jetpack.vim'
  system($'curl -fsSLo {jetpackfile} --create-dirs {jetpackurl}')
endif

packadd vim-jetpack
jetpack#begin()
Jetpack 'tani/vim-jetpack', { 'opt': 1 }
Jetpack 'airblade/vim-gitgutter'
Jetpack 'alvan/vim-closetag'
Jetpack 'ctrlpvim/ctrlp.vim'
Jetpack 'cohama/lexima.vim'      # æ‹¬å¼§è£œå®Œ
Jetpack 'delphinus/vim-auto-cursorline'
Jetpack 'dense-analysis/ale'
Jetpack 'easymotion/vim-easymotion'
Jetpack 'hrsh7th/vim-vsnip'
Jetpack 'hrsh7th/vim-vsnip-integ'
Jetpack 'itchyny/calendar.vim'
Jetpack 'itchyny/vim-parenmatch'
Jetpack 'kana/vim-textobj-user'
Jetpack 'LeafCage/vimhelpgenerator'
Jetpack 'luochen1990/rainbow'    # è™¹è‰²æ‹¬å¼§
Jetpack 'machakann/vim-sandwich'
Jetpack 'mattn/ctrlp-matchfuzzy'
Jetpack 'mattn/vim-notification'
Jetpack 'matze/vim-move'         # è¡Œç§»å‹•
Jetpack 'mechatroner/rainbow_csv'
Jetpack 'michaeljsmith/vim-indent-object'
Jetpack 'MTDL9/vim-log-highlighting'
Jetpack 'obcat/vim-hitspop'
Jetpack 'obcat/vim-sclow'
Jetpack 'osyo-manga/vim-textobj-multiblock'
Jetpack 'othree/html5.vim'
Jetpack 'othree/yajs.vim'
Jetpack 'prabirshrestha/asyncomplete-buffer.vim'
Jetpack 'prabirshrestha/asyncomplete.vim'
Jetpack 'rafamadriz/friendly-snippets'
Jetpack 'thinca/vim-portal'
Jetpack 'tpope/vim-fugitive'      # Gdiffã¨ã‹
Jetpack 'tyru/caw.vim'            # ã‚³ãƒ¡ãƒ³ãƒˆåŒ–
Jetpack 'yami-beta/asyncomplete-omni.vim'
Jetpack 'yegappan/mru'
Jetpack 'vim-jp/vital.vim'
Jetpack 'utubo/jumpcuorsor.vim'   # vimã«å¯¾å¿œã•ã›ãŸã‚„ã¤(æ§˜å­è¦‹)vim-jetpackã ã¨ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«å‡ºæ¥ãªã„ã‹ã‚‚ï¼Ÿ
Jetpack 'utubo/vim-colorscheme-girly'
Jetpack 'utubo/vim-minviml'
Jetpack 'utubo/vim-cmdheight0'
Jetpack 'utubo/vim-portal-aim'
Jetpack 'utubo/vim-registers-lite'
Jetpack 'utubo/vim-reformatdate'
Jetpack 'utubo/vim-tabtoslash'
# ã‚ã¾ã‚Šä½¿ã£ã¦ãªã„ã‘ã©ä½œã£ãŸã®ã§â€¦
Jetpack 'utubo/vim-shrink'
Jetpack 'utubo/vim-tablist'
Jetpack 'utubo/vim-tabpopupmenu'
Jetpack 'utubo/vim-textobj-twochars'

if has_deno
	Jetpack 'vim-denops/denops.vim'
	Jetpack 'vim-skk/skkeleton'
endif
jetpack#end()
if ! has_jetpack
	jetpack#sync()
endif
#}}}

# easymotion {{{
Enable  g:EasyMotion_smartcase
Enable  g:EasyMotion_use_migemo
Enable  g:EasyMotion_enter_jump_first
Disable g:EasyMotion_verbose
Disable g:EasyMotion_do_mapping
g:EasyMotion_keys = 'asdghklqwertyuiopzxcvbnmfjASDGHKLQWERTYUIOPZXCVBNMFJ;'
g:EasyMotion_prompt = 'EasyMotion: '
noremap s <Plug>(easymotion-s)
#}}}

# sandwich {{{
g:sandwich#recipes = deepcopy(g:sandwich#default_recipes)
g:sandwich#recipes += [
	{ buns: ["\r", ''  ], input: ["\r"], command: ["normal! a\r"] },
	{ buns: ['',   ''  ], input: ['q'] },
	{ buns: ['ã€Œ', 'ã€'], input: ['k'] },
	{ buns: ['{ ', ' }'], input: ['{'] },
	{ buns: ['${', '}' ], input: ['${'] },
	{ buns: ['%{', '}' ], input: ['%{'] },
	{ buns: ['CommentString(0)', 'CommentString(1)'], expr: 1, input: ['c'] },
]
def! g:CommentString(index: number): string
	return &commentstring->split('%s')->get(index, '')
enddef
Enable g:sandwich_no_default_key_mappings
Enable g:operator_sandwich_no_default_key_mappings
MultiCmd nnoremap,xnoremap Sd <Plug>(operator-sandwich-delete)<if-nnoremap>ab
MultiCmd nnoremap,xnoremap Sr <Plug>(operator-sandwich-replace)<if-nnoremap>ab
MultiCmd nnoremap,xnoremap S  <Plug>(operator-sandwich-add)<if-nnoremap>iw
nmap <expr> SS (matchstr(getline('.'), '[''"]', col('.')) ==# '"') ? 'Sr''' : 'Sr"'

# æ”¹è¡Œã§æŒŸã‚“ã ã‚ã¨ã‚¿ãƒ–ã§ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã•ã‚Œã‚‹ã¨å…·åˆãŒæ‚ªããªã‚‹ã®ã§â€¦
def FixSandwichPos()
	var c = g:operator#sandwich#object.cursor
	if g:fix_sandwich_pos[1] !=# c.inner_head[1]
		c.inner_head[2] = getline(c.inner_head[1])->match('\S') + 1
		c.inner_tail[2] = getline(c.inner_tail[1])->match('$') + 1
	endif
enddef
au vimrc User OperatorSandwichAddPre g:fix_sandwich_pos = getpos('.')
au vimrc User OperatorSandwichAddPost FixSandwichPos()

# å†…å´ã«é€£ç¶šã§æŒŸã‚€ã‚„ã¤
var big_mac_crown = []
def BigMac(first: bool = true)
	const c = first ? [] : g:operator#sandwich#object.cursor.inner_head[1 : 2]
	if first || big_mac_crown !=# c
		big_mac_crown = c
		au vimrc User OperatorSandwichAddPost ++once BigMac(false)
		if first
			feedkeys('S')
		else
			setpos("'<", g:operator#sandwich#object.cursor.inner_head)
			setpos("'>", g:operator#sandwich#object.cursor.inner_tail)
			feedkeys('gvS')
		endif
	endif
enddef
nmap Sm viwSm
vnoremap Sm <ScriptCmd>BigMac()<CR>

# å›²ã¿ã‚’å‰Šé™¤ã—ãŸã‚‰è¡Œæœ«ç©ºç™½ã¨ç©ºè¡Œã‚‚å‰Šé™¤
def RemoveAirBuns()
	const c = g:operator#sandwich#object.cursor
	RemoveEmptyLine(c.tail[1])
	RemoveEmptyLine(c.head[1])
enddef
au vimrc User OperatorSandwichDeletePost RemoveAirBuns()
#}}}

# MRU {{{
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆè¨­å®š(æ‹¬å¼§å†…ã«ãƒ•ãƒ«ãƒ‘ã‚¹)ã ã¨ãƒ‘ã‚¹ã«æ‹¬å¼§ãŒå«ã¾ã‚Œã¦ã„ã‚‹ãƒ•ã‚¡ã‚¤ãƒ«ãŒé–‹ã‘ãªã„ã®ã§ã€ãƒ‘ã‚¹ã«ä½¿ç”¨ã•ã‚Œãªã„">"ã‚’åŒºåˆ‡ã‚Šã«ã™ã‚‹
g:MRU_Filename_Format = {
	formatter: 'fnamemodify(v:val, ":t") . " > " . v:val',
	parser: '> \zs.*',
	syntax: '^.\{-}\ze >'
}
# æ•°å­—ã‚­ãƒ¼ã§é–‹ã
def MRUwithNumKey(use_tab: bool)
	b:use_tab = use_tab
	setlocal number
	redraw
	echoh Question
	echo $'[1]..[9] => open with a {use_tab ? 'tab' : 'window'}.'
	echoh None
	const key = use_tab ? 't' : '<CR>'
	for i in range(1, 9)
		execute $'nmap <buffer> <silent> {i} :<C-u>{i}<CR>{key}'
	endfor
enddef
def MyMRU()
	Enable b:auto_cursorline_disabled
	setlocal cursorline
	nnoremap <buffer> w <ScriptCmd>MRUwithNumKey(!b:use_tab)<CR>
	nnoremap <buffer> R <Cmd>MruRefresh<CR><Cmd>MRU<CR>
	nnoremap <buffer> <Esc> <Cmd>q!<CR>
	MRUwithNumKey(BufIsSmth())
enddef
au vimrc FileType mru MyMRU()
au vimrc ColorScheme * hi link MruFileName Directory
nnoremap <F2> <Cmd>MRUToggle<CR>
g:MRU_Exclude_Files = has('win32') ? $'{$TEMP}\\.*' : '^/tmp/.*\|^/var/tmp/.*'
#}}}

# è£œå®Œ {{{
def RegisterAsyncompSource(name: string, white: list<string>, black: list<string>)
	execute printf("asyncomplete#register_source(asyncomplete#sources#%s#get_source_options({ name: '%s', whitelist: %s, blacklist: %s, completor: asyncomplete#sources#%s#completor }))", name, name, white, black, name)
enddef
RegisterAsyncompSource('omni', ['*'], ['c', 'cpp', 'html'])
RegisterAsyncompSource('buffer', ['*'], ['go'])
MultiCmd inoremap,snoremap <expr> JJ      vsnip#expandable() ? '<Plug>(vsnip-expand)' : 'JJ'
MultiCmd inoremap,snoremap <expr> <C-l>   vsnip#available(1) ? '<Plug>(vsnip-expand-or-jump)' : '<C-l>'
MultiCmd inoremap,snoremap <expr> <Tab>   vsnip#jumpable(1)  ? '<Plug>(vsnip-jump-next)' : pumvisible() ? '<C-n>' : '<Tab>'
MultiCmd inoremap,snoremap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)' : pumvisible() ? '<C-p>' : '<S-Tab>'
#inoremap <expr> <CR> pumvisible() ? '<C-y>' : '<CR>'
Enable g:lexima_accept_pum_with_enter
#}}}

# ALE {{{
Enable  g:ale_set_quickfix
Enable  g:ale_fix_on_save
Disable g:ale_lint_on_insert_leave
Disable g:ale_set_loclist
g:ale_sign_error = 'ğŸ'
g:ale_sign_warning = 'ğŸ'
g:ale_linters = { javascript: ['eslint'] }
g:ale_fixers = { typescript: ['deno'] }
g:ale_lint_delay = &updatetime
nnoremap <silent> [a <Plug>(ale_previous_wrap)
nnoremap <silent> ]a <Plug>(ale_next_wrap)

# cmdheight=0ã ã¨ALEã®ãƒ›ãƒãƒ¼ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ãŒã¡ã‚‰ã¤ãã®ã§g:ll_aleã«ä»£å…¥ã—ã¦lightlineã§è¡¨ç¤ºã™ã‚‹
g:ale_echo_cursor = 0
#}}}

# cmdheight0 {{{
# ãƒ¤ãƒ³ã‚¯ã—ãŸã‚„ã¤ã‚’è¡¨ç¤ºã™ã‚‹ã‚„ã¤
g:ruler_reg = ''
def LLYankPost()
	var reg = v:event.regcontents
		->join('â†µ')
		->substitute('\t', 'â€º', 'g')
		->TruncToDisplayWidth(20)
	g:ruler_reg = $'ğŸ“‹:{reg}'
enddef
au vimrc TextYankPost * LLYankPost()

# æ¯æ™‚vimèµ·å‹•å¾Œ45åˆ†ã‹ã‚‰15åˆ†é–“ä¼‘æ†©ã—ã‚ˆã†ã­
g:ruler_worktime = 'ğŸ•›'
g:ruler_worktime_open_at = get(g:, 'ruler_worktime_open_at', localtime()) # .vimrcã‚’å†å®Ÿè¡Œã—ã¦ã‚‚(1è¡Œç›®ã®noclearã§)æŒã¡è¶Šã—
def! g:VimrcTimer60s(timer: any)
	const hhmm = (localtime() - g:ruler_worktime_open_at) / 60
	const mm = hhmm % 60
	#:ruler_worktime = 'ğŸ•›ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ•˜ğŸ•™ğŸ•š'[mm / 5]
	g:ruler_worktime = 'ğŸ•›ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ°ğŸ°ğŸ°'[mm / 5]
	if (mm ==# 45)
		notification#show("       â˜•ğŸ´ğŸ°\nHave a break time !")
	endif
enddef
timer_stop(get(g:, 'vimrc_timer_60s', 0))
g:vimrc_timer_60s = timer_start(60000, 'VimrcTimer60s', { repeat: -1 })

# markdownã®ãƒã‚§ãƒƒã‚¯ãƒœãƒƒã‚¯ã‚¹ã®æ•°ã‚’ã‚«ã‚¦ãƒ³ãƒˆ
g:ruler_mdcb = ''
def CountCheckBoxs(): string
	var [firstline, lastline] = VFirstLast()
	if mode() ==? 'V'
		# OK
	elseif &ft !=# 'markdown'
		return ''
	else
		const indent = indent(firstline)
		for l in range(firstline + 1, line('$'))
			if indent(l) <= indent
				break
			endif
			lastline = l
		endfor
	endif
	# å¿µã®ãŸã‚max99è¡Œ
	const MAX_LINES = 99 - 1
	var andmore = ''
	if firstline + MAX_LINES < lastline
		andmore = '+'
		lastline = firstline + MAX_LINES
	endif
	if firstline > lastline # TODO: ãªã‚“ã§ï¼Ÿ
		return ''
	endif
	var chkd = 0
	var empty = 0
	for l in range(firstline, lastline)
		const line = getline(l)
		if line->match('^\s*- \[x\]') !=# -1
			chkd += 1
		elseif line->match('^\s*- \[ \]') !=# -1
			empty += 1
		endif
	endfor
	if chkd ==# 0 && empty ==# 0
		return ''
	else
		return  $'[x]:{chkd}/{chkd + empty}{andmore}'
	endif
enddef

def CountCheckBoxsDelay()
	if mode()[0] !=# 'n'
		return
	endif
	const count = CountCheckBoxs()
	if count !=# g:ruler_mdcb
		g:ruler_mdcb = count
		# silent! cmdheight0#Invalidate()
	endif
enddef

au vimrc User CursorMovedDelay CountCheckBoxsDelay()

# &ff
if has('win32')
	def RulerFF(): string
		return &ff !=# 'dos' ? $' {&ff}' : ''
	enddef
else
	def RulerFF(): string
		return &ff ==# 'dos' ? $' {&ff}' : ''
	enddef
endif

def! g:RulerBufInfo(): string
	if winwidth(winnr()) < 60
		return ''
	else
		var info = &fenc ==# 'utf-8' ? '' : &fenc
		info ..= RulerFF()
		return info
	endif
enddef

# cmdheight0è¨­å®š
g:cmdheight0 = get(g:, 'cmdheight0', {})
g:cmdheight0.delay = -1
g:cmdheight0.tail = "\ue0c6"
g:cmdheight0.sep  = "\ue0c6"
g:cmdheight0.sub  = [" \ue0b5", "\ue0b7 "]
g:cmdheight0.horiz = "â”€"
g:cmdheight0.format = '%t %m%r%|%=%|%{ruler_reg|}%{ruler_mdcb|}%3l:%-2c:%L%|%{RulerBufInfo()|}%{ruler_worktime} '
g:cmdheight0.laststatus = 0
nnoremap ZZ <ScriptCmd>cmdheight0#ToggleZen()<CR>
#}}}

# skk {{{
if has_deno
	if ! empty($SKK_JISYO_DIR)
		skkeleton#config({
		globalJisyo: expand($'{$SKK_JISYO_DIR}SKK-JISYO.L'),
			userJisyo: expand($'{$SKK_JISYO_DIR}.skkeleton'),
		})
	endif
	skkeleton#config({
		eggLikeNewline: true,
		keepState: true,
		showCandidatesCount: 1,
	})
	map! <C-j> <Plug>(skkeleton-toggle)
endif
#}}}

# textobj-multiblock  {{{
MultiCmd onoremap,xnoremap ab <Plug>(textobj-multiblock-a)
MultiCmd onoremap,xnoremap ib <Plug>(textobj-multiblock-i)
g:textobj_multiblock_blocks = [
	\ [ "(", ")" ],
	\ [ "[", "]" ],
	\ [ "{", "}" ],
	\ [ '<', '>' ],
	\ [ '"', '"', 1 ],
	\ [ "'", "'", 1 ],
	\ [ ">", "<", 1 ],
	\ [ "ã€Œ", "ã€", 1 ],
]
#}}}

# Portal {{{
nnoremap <Leader>a <Cmd>PortalAim<CR>
nnoremap <Leader>b <Cmd>PortalAim blue<CR>
nnoremap <Leader>o <Cmd>PortalAim orange<CR>
nnoremap <Leader>r <Cmd>PortalReset<CR>
#}}}

# ãƒ˜ãƒ«ãƒ—ä½œæˆ {{{
g:vimhelpgenerator_version = ''
g:vimhelpgenerator_author = 'Author  : utubo'
g:vimhelpgenerator_defaultlanguage = 'en'
#}}}

# ã‚«ãƒ¬ãƒ³ãƒ€ãƒ¼ {{{
g:calendar_first_day = 'sunday'
def MyCalender()
	nnoremap <buffer> k <Plug>(calendar_up)
	nnoremap <buffer> j <Plug>(calendar_down)
	nnoremap <buffer> h <Plug>(calendar_prev)
	nnoremap <buffer> l <Plug>(calendar_next)
	nnoremap <buffer> gh <Plug>(calendar_left)
	nnoremap <buffer> gl <Plug>(calendar_right)
	nmap <buffer> <CR> >
	nmap <buffer> <BS> <
enddef
au vimrc FileType calendar MyCalender()
# }}}

# cmdline statusline åˆ‡ã‚Šæ›¿ãˆ {{{
MultiCmd nnoremap,xnoremap / <Cmd>noh<CR>/
MultiCmd nnoremap,xnoremap ? <Cmd>noh<CR>?
MultiCmd nmap,vmap ; :
nnoremap <Space>; ;
nnoremap <Space>: :
# è‡ªä½œãƒ—ãƒ©ã‚°ã‚¤ãƒ³(vim-registerslite)ã¨è¢«ã£ã¦ã—ã¾ã£ãŸâ€¦
# inoremap <C-r>= <C-o><C-r>=
#}}}

# ãã®ä»– {{{
Enable g:rainbow_active
g:loaded_matchparen = 1
g:auto_cursorline_wait_ms = &updatetime
g:ctrlp_match_func = {'match': 'ctrlp_matchfuzzy#matcher'}
g:ctrlp_cmd = 'CtrlPMixed'
nnoremap [c <Plug>(GitGutterPrevHunk)
nnoremap ]c <Plug>(GitGutterNextHunk)
nmap <Space>ga :<C-u>Git add %
nmap <Space>gc :<C-u>Git commit -m ''<Left>
nmap <Space>gp :<C-u>Git push
nnoremap <Space>gv <Cmd>Gvdiffsplit<CR>
nnoremap <Space>gd <Cmd>Gdiffsplit<CR>
nnoremap <Space>gl <Cmd>Git pull<CR>
nnoremap <Space>t <ScriptCmd>tabpopupmenu#popup()<CR>
nnoremap <Space>T <ScriptCmd>tablist#Show()<CR>
MultiCmd nnoremap,xnoremap <Space>c <Plug>(caw:hatpos:toggle)
MultiCmd nnoremap,tnoremap <silent> <C-w><C-s> <Plug>(shrink-height)<C-w>w
MultiCmd nnoremap,tnoremap <silent> <C-w><C-h> <Plug>(shrink-width)<C-w>w
# EasyMotionã¨ã©ã£ã¡ã‚’ä½¿ã†ã‹æ§˜å­è¦‹ä¸­
noremap <Space>s <Plug>(jumpcursor-jump)
#}}}

# é–‹ç™ºç”¨ {{{
const localplugins = expand($'{rtproot}/pack/local/opt/*')
if localplugins !=# ''
	&runtimepath = $'{substitute(localplugins, '\n', ',', 'g')},{&runtimepath}'
endif
def DevColorScheme()
	if expand('%:p') !~# '/colors/'
		return
	endif
	nnoremap <buffer> <expr> ZX $"<Cmd>update<CR><Cmd>colorscheme {expand('%:r')}<CR>"
	nnoremap <buffer> <expr> ZB $"<Cmd>set background={&background ==# 'dark' ? 'light' : 'dark'}<CR>"
enddef
au vimrc FileType vim DevColorScheme()
#}}}

filetype plugin indent on
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ã‚³ãƒ”ãƒšå¯„ã›é›†ã‚è‰²ã€… {{{
au vimrc InsertLeave * set nopaste
au vimrc BufReadPost *.log* normal! G
xnoremap * "vy/\V<C-r>=substitute(escape(@v,'\/'),"\n",'\\n','g')<CR><CR>
inoremap kj <Esc>`^
inoremap kk <Esc>`^
inoremap <CR> <CR><C-g>u
# https://github.com/astrorobot110/myvimrc/blob/master/vimrc
set matchpairs+=ï¼ˆ:ï¼‰,ã€Œ:ã€,ã€:ã€,ã€:ã€‘,ï¼»:ï¼½,ï¼œ:ï¼
# https://github.com/Omochice/dotfiles
nnoremap <expr> i !empty(getline('.')) ? 'i' : '"_cc'
nnoremap <expr> a !empty(getline('.')) ? 'a' : '"_cc'
nnoremap <expr> A !empty(getline('.')) ? 'A' : '"_cc'
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ã‚¿ãƒ–å¹…ã‚„ã‚¿ãƒ–å±•é–‹ã‚’è‡ªå‹•è¨­å®š {{{
def SetupTabstop()
	const limit = 100
	const org = getpos('.')
	cursor(1, 1)
	if !!search('^\t', 'nc', limit)
		setlocal noexpandtab
		setlocal tabstop=3 # æ„å¤–ã¨ã‚ã‚Šãªæ°—ãŒã—ã¦ããŸâ€¦
	elseif !!search('^  \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=2
	elseif !!search('^    \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=4
	endif
	setpos('.', org)
enddef
au vimrc BufReadPost * SetupTabstop()
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# vimgrep {{{
def VimGrep(keyword: string, ...targets: list<string>)
	var path = join(targets, ' ')
	# ãƒ‘ã‚¹ã‚’çœç•¥ã—ãŸå ´åˆã¯ã€åŒã˜æ‹¡å¼µå­ã®ãƒ•ã‚¡ã‚¤ãƒ«ã‹ã‚‰æ¢ã™
	if empty(path)
		path = expand('%:e') ==# '' ? '*' : ($'*.{expand('%:e')}')
	endif
	# é©å®œã‚¿ãƒ–ã§é–‹ã(ãŸã ã—æ˜ç¤ºçš„ã«ã€Œ%ã€ã‚’æŒ‡å®šã—ãŸã‚‰ã‚«ãƒ¬ãƒ³ãƒˆã§é–‹ã)
	const use_tab = BufIsSmth() && path !=# '%'
	if use_tab
		tabnew
	endif
	# lvimgrepã—ã¦ãªã‚“ã‚„ã‹ã‚“ã‚„ã—ã¦çµ‚ã‚ã‚Š
	execute $'silent! lvimgrep {keyword} {path}'
	if ! empty(getloclist(0))
		lwindow
	else
		echoh ErrorMsg
		echomsg $'Not found.: {keyword}'
		echoh None
		if use_tab
			tabnext -
			tabclose +
		endif
	endif
enddef
command! -nargs=+ VimGrep VimGrep(<f-args>)
nmap <Space>/ :<C-u>VimGrep<Space>

def SetupQF()
	nnoremap <buffer> <silent> ; <CR>:silent! normal! zv<CR><C-W>w
	nnoremap <buffer> <silent> w <C-W><CR>:silent! normal! zv<CR><C-W>w
	nnoremap <buffer> <silent> t <C-W><CR>:silent! normal! zv<CR><C-W>T
	nnoremap <buffer> <nowait> q <Cmd>lexpr ''<CR>:q<CR>
	nnoremap <buffer> f <C-f>
	nnoremap <buffer> b <C-b>
	# æ§˜å­è¦‹ä¸­(ä½¿ã‚ãªãã†ãªã‚‰å‰Šé™¤ã™ã‚‹)
	execute $'nnoremap <buffer> T <C-W><CR><C-W>T{tabpagenr()}gt'
enddef
au vimrc FileType qf SetupQF()
au vimrc WinEnter * if winnr('$') ==# 1 && &buftype ==# 'quickfix' | q | endif
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# diff {{{
set splitright
set fillchars+=diff:\ # å‰Šé™¤è¡Œã¯ç©ºç™½æ–‡å­—ã§åŸ‹ã‚ã‚‹
# diffãƒ¢ãƒ¼ãƒ‰ã‚’è‡ªå‹•ã§off https://hail2u.net/blog/software/vim-turn-off-diff-mode-automatically.html
au vimrc WinEnter * if (winnr('$') ==# 1) && !!getbufvar(winbufnr(0), '&diff') | diffoff | endif
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# æ—¥ä»˜é–¢ä¿‚ {{{
g:reformatdate_extend_names = [{
	a: ['æ—¥', 'æœˆ', 'ç«', 'æ°´', 'æœ¨', 'é‡‘', 'åœŸ'],
	A: ['æ—¥æ›œæ—¥', 'æœˆæ›œæ—¥', 'ç«æ›œæ—¥', 'æ°´æ›œæ—¥', 'æœ¨æ›œæ—¥', 'é‡‘æ›œæ—¥', 'åœŸæ›œæ—¥'],
}]
g:reformatdate_extend_formats = ['%m/%d(%a)']
reformatdate#init()
inoremap <expr> <F5> strftime('%Y/%m/%d')
cnoremap <expr> <F5> strftime('%Y%m%d')
nnoremap <F5> <ScriptCmd>reformatdate#reformat(localtime())<CR>
nnoremap <C-a> <ScriptCmd>reformatdate#inc(v:count)<CR>
nnoremap <C-x> <ScriptCmd>reformatdate#dec(v:count)<CR>
nnoremap <Space><F5> /\d\{4\}\/\d\d\/\d\d<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ã‚¹ãƒãƒ›ç”¨ {{{
# - ã‚­ãƒ¼ãŒå°ã•ã„ã®ã§æŠ¼ã—ã«ãã„ã‚‚ã®ã¯Spaceã¸ãƒãƒƒãƒ”ãƒ³ã‚°
# - ã‚¹ãƒãƒ›ã§ã®ã‚³ãƒ¼ãƒ‡ã‚£ãƒ³ã‚°ã¯åŸºæœ¬çš„ã«ãƒã‚°å–ã‚Š
nnoremap <Space>zz <Cmd>q!<CR>
# ã‚¹ã‚¿ãƒƒã‚¯ãƒˆãƒ¬ãƒ¼ã‚¹ã‹ã‚‰yankã—ã¦ã‚½ãƒ¼ã‚¹ã®è©²å½“ç®‡æ‰€ã‚’æ¢ã™ã®ã‚’è£œåŠ©
nnoremap <Space>e G?\cErr\\|Exception<CR>
nnoremap <Space>y yiw
nnoremap <expr> <Space>f $'{(getreg('"') =~ '^\d\+$' ? ':' : '/')}{getreg('"')}<CR>'
# ã‚¹ãƒãƒ›ã ã¨:ã¨/ã¨ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ãŒé ã„ã®ã§â€¦
nmap <Space>. :
nmap <Space>, /
for i in range(1, 10)
	execute $'nmap <Space>{i % 10} <F{i}>'
endfor
nmap <Space><Space>1 <F11>
nmap <Space><Space>2 <F12>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ã‚«ãƒ¼ã‚½ãƒ«ã‚’è¡Œé ­ã«æ²¿ã‚ã›ã¦ç§»å‹• {{{
def PutHat(): string
	const x = getline('.')->match('\S') + 1
	if x !=# 0 || !exists('w:my_hat')
		w:my_hat = col('.') ==# x ? '^' : ''
	endif
	return w:my_hat
enddef
nnoremap <expr> j $'j{<SID>PutHat()}'
nnoremap <expr> k $'k{<SID>PutHat()}'
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# æŠ˜ã‚Šç•³ã¿ {{{
# ã“ã‚“ãªã‹ã‚“ã˜ã§ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã«åˆã‚ã›ã¦è¡¨ç¤ºğŸ“ {{{
def! g:MyFoldText(): string
	const src = getline(v:foldstart)
	const indent = repeat(' ', indent(v:foldstart))
	const text = &foldmethod ==# 'indent' ? '' : src->substitute(matchstr(&foldmarker, '^[^,]*'), '', '')->trim()
	return $'{indent}{text} ğŸ“'
enddef
set foldtext=g:MyFoldText()
set fillchars+=fold:\ # æŠ˜ã‚Šç•³ã¿æ™‚ã®ã€Œ-ã€ã¯åŠè§’ç©ºç™½
au vimrc ColorScheme * hi! link Folded Delimiter
au vimrc ColorScheme * hi! link ALEVirtualTextWarning ALEWarningSign
au vimrc ColorScheme * hi! link ALEVirtualTextError ALEErrorSign
#}}}
# ãƒ›ãƒ¼ãƒ«ãƒ‰ãƒãƒ¼ã‚«ãƒ¼ã®å‰ã«ã‚¹ãƒšãƒ¼ã‚¹ã€å¾Œã‚ã«æ”¹è¡Œã‚’å…¥ã‚Œã‚‹ {{{
def Zf()
	var [firstline, lastline] = VFirstLast()
	execute ':' firstline 's/\v(\S)?$/\1 /'
	append(lastline, IndentStr(firstline))
	cursor([firstline, 1])
	cursor([lastline + 1, 1])
	normal! zf
enddef
xnoremap zf <ScriptCmd>Zf()<CR>
#}}}
# ãƒ›ãƒ¼ãƒ«ãƒ‰ãƒãƒ¼ã‚«ãƒ¼ã‚’å‰Šé™¤ã—ãŸã‚‰è¡Œæœ«ã‚’ãƒˆãƒªãƒ ã™ã‚‹ {{{
def Zd()
	if foldclosed(line('.')) ==# -1
		normal! zc
	endif
	const head = foldclosed(line('.'))
	const tail = foldclosedend(line('.'))
	if head ==# -1
		return
	endif
	const org = getpos('.')
	normal! zd
	RemoveEmptyLine(tail)
	RemoveEmptyLine(head)
	setpos('.', org)
enddef
nnoremap zd <ScriptCmd>Zd()<CR>
#}}}
# ãã®ä»–æŠ˜ã‚ŠãŸãŸã¿é–¢ä¿‚ {{{
set foldmethod=marker
au vimrc FileType markdown,yaml setlocal foldlevelstart=99 foldmethod=indent
au vimrc BufReadPost * :silent! normal! zO
nnoremap <expr> h (col('.') ==# 1 && 0 < foldlevel('.') ? 'zc' : 'h')
nnoremap Z<Tab> <Cmd>set foldmethod=indent<CR>
nnoremap Z{ <Cmd>set foldmethod=marker<CR>
nnoremap Zy <Cmd>set foldmethod=syntax<CR>
#}}}
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ“ã‚¸ãƒ¥ã‚¢ãƒ«ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
def KeepingCurPos(expr: string)
	const cur = getcurpos()
	execute expr
	setpos('.', cur)
enddef
xnoremap u <ScriptCmd>KeepingCurPos('undo')<CR>
xnoremap <Space>u u
xnoremap <C-R> <ScriptCmd>KeepingCurPos('redo')<CR>
xnoremap <Tab> <Cmd>normal! >gv<CR>
xnoremap <S-Tab> <Cmd>normal! <gv<CR>
#}}}

# ----------------------------------------------------------
# ã‚³ãƒãƒ³ãƒ‰ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
cnoremap <C-h> <Left>
cnoremap <C-l> <Right>
cnoremap <C-j> <Down>
cnoremap <C-k> <Up>
cnoremap <expr> <C-r><C-r> trim(@")->substitute('\n', ' \| ', 'g')
cnoremap <expr> <C-r><C-e> escape(@", '~^$.*?/\[]')->substitute('\n', '\\n', 'g')
cnoreabbrev cs colorscheme
# ã€Œjjã€ã§<CR>ã€ã€Œkkã€ã¯ã‚­ãƒ£ãƒ³ã‚»ãƒ«
# ãŸã ã—ä¿å­˜ã¯ç‰‡æ‰‹ã§ã€Œ;jjã€ã§ã‚‚OK(ã€Œ;wjjã€ã˜ã‚ƒãªãã¦ã„ã„)
cnoremap kk <C-c>
cnoremap <expr> jj (empty(getcmdline()) && getcmdtype() ==# ':' ? 'update<CR>' : '<CR>')
inoremap ;jj <Esc>`^<Cmd>update<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# terminalã¨ã‹ {{{
if has('win32')
	command! Powershell :bo terminal ++close pwsh
	nnoremap SH <Cmd>Powershell<CR>
	nnoremap <S-F1> <Cmd>silent !start explorer %:p:h<CR>
else
	nnoremap SH <Cmd>bo terminal<CR>
endif
tnoremap <C-w>; <C-w>:
tnoremap <C-w><C-w> <C-w>w
tnoremap <C-w><C-q> exit<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# markdownã®ãƒã‚§ãƒƒã‚¯ãƒœãƒƒã‚¯ã‚¹ {{{
def ToggleCheckBox()
	for l in VRange()
		const a = getline(l)
		var b = substitute(a, '^\(\s*\)- \[ \]', '\1- [x]', '') # check on
		if a ==# b
			b = substitute(a, '^\(\s*\)- \[x\]', '\1- [ ]', '') # check off
		endif
		if a ==# b
			b = substitute(a, '^\(\s*\)\(- \)*', '\1- [ ] ', '') # a new check box
		endif
		setline(l, b)
		if l ==# line('.')
			var c = getpos('.')
			c[2] += len(b) - len(a)
			setpos('.', c)
		endif
	endfor
enddef
noremap <Space>x <ScriptCmd>ToggleCheckBox()<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒãƒƒãƒ•ã‚¡ã®æƒ…å ±ã‚’è‰²ä»˜ãã§è¡¨ç¤º {{{
def ShowBufInfo(event: string = '')
	if &ft ==# 'qf'
		return
	endif

	var isReadPost = event ==# 'BufReadPost'
	if isReadPost && !filereadable(expand('%'))
		# ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã¨ã‹ãŒä¸€æ™‚çš„ãªbufnameã‚’ä»˜ä¸ã—ã¦é–‹ã„ãŸå ´åˆã¯ç„¡è¦–ã™ã‚‹
		return
	endif

	var msg = []
	add(msg, ['Title', $'"{bufname()}"'])
	add(msg, ['Normal', ' '])
	if &modified
		add(msg, ['Delimiter', '[+]'])
		add(msg, ['Normal', ' '])
	endif
	if !isReadPost && !filereadable(expand('%'))
		add(msg, ['Tag', '[New]'])
		add(msg, ['Normal', ' '])
	endif
	if &readonly
		add(msg, ['WarningMsg', '[RO]'])
		add(msg, ['Normal', ' '])
	endif
	const w = wordcount()
	if isReadPost || w.bytes !=# 0
		add(msg, ['Constant', printf('%dL, %dB', w.bytes ==# 0 ? 0 : line('$'), w.bytes)])
		add(msg, ['Normal', ' '])
	endif
	add(msg, ['MoreMsg', $'{&ff} {empty(&fenc) ? &encoding : &fenc} {&ft}'])
	var msglen = 0
	const maxlen = &columns - 2
	for i in reverse(range(0, len(msg) - 1))
		var s = msg[i][1]
		var d = strdisplaywidth(s)
		msglen += d
		if maxlen < msglen
			const l = maxlen - msglen + d
			while !empty(s) && l < strdisplaywidth(s)
				s = s[1 :]
			endwhile
			msg[i][1] = s
			msg = msg[i : ]
			insert(msg, ['NonText', '<'], 0)
			break
		endif
	endfor
	redraw
	echo ''
	for m in msg
		execute 'echohl' m[0]
		echon m[1]
	endfor
	echohl Normal
enddef

# Zenãƒ¢ãƒ¼ãƒ‰ã§ä½ç½®ãŒåˆ†ã‹ã‚‰ãªããªã‚‹ã®ã§POPUPã§ç¾åœ¨ä½ç½®ã‚’è¡¨ç¤º
def PopupCursorPos()
	popup_create($' {line(".")}:{col(".")} ', {
		pos: 'botleft',
		line: 'cursor-1',
		col: 'cursor',
		moved: 'any',
		padding: [1, 1, 1, 1],
	})
enddef

# TODO: â†“`call <SID>`ã‚’å‰Šã£ãŸã‚‰"not an editor command"ã«ãªã£ãŸè¦èª¿æŸ»
nnoremap <C-g> <ScriptCmd>call <SID>ShowBufInfo()<CR><scriptCmd>call <SID>PopupCursorPos()<CR>
au vimrc BufNewFile,BufReadPost,BufWritePost * ShowBufInfo('BufNewFile')
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# é–‰ã˜ã‚‹ {{{
def Quit(expr: string = '')
	if !!expr
		if winnr() ==# winnr(expr)
			return
		endif
		execute 'wincmd' expr
	endif
	if mode() ==# 't'
		quit!
	else
		confirm quit
	endif
enddef
nnoremap q <Nop>
nnoremap Q q
nnoremap qh <ScriptCmd>Quit('h')<CR>
nnoremap qj <ScriptCmd>Quit('j')<CR>
nnoremap qk <ScriptCmd>Quit('k')<CR>
nnoremap ql <ScriptCmd>Quit('l')<CR>
nnoremap qq <ScriptCmd>Quit()<CR>
nnoremap q<CR> <ScriptCmd>Quit()<CR>
nnoremap qn <Cmd>confirm tabclose +<CR>
nnoremap qp <Cmd>confirm tabclose -<CR>
nnoremap q# <Cmd>confirm tabclose #<CR>
nnoremap qo <Cmd>confirm tabonly<CR>
nnoremap q: q:
nnoremap q/ q/
nnoremap q? q?
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ç§»å‹•ã—ã¦ä¿å­˜ {{{
def MoveFile(newname: string)
	const oldpath = expand('%')
	const newpath = expand(newname)
	if ! empty(oldpath) && filereadable(oldpath)
		if filereadable(newpath)
			echoh Error
			echo $'file "{newname}" already exists.'
			echoh None
			return
		endif
		rename(oldpath, newpath)
	endif
	execute 'saveas!' newpath
	# é–‹ãç›´ã—ã¦MRUã«ç™»éŒ²
	edit
enddef
command! -nargs=1 -complete=file MoveFile MoveFile(<f-args>)
cnoreabbrev mv MoveFile
#}}}

# ----------------------------------------------------------
# vimrcä½œæˆç”¨ {{{
# ã‚«ãƒ¼ã‚½ãƒ«è¡Œã‚’å®Ÿè¡Œã™ã‚‹ã‚„ã¤
cnoremap <expr> <SID>(exec_line) $'{getline('.')->substitute('^[ \t"#:]\+', '', '')}<CR>'
nmap g: :<C-u><SID>(exec_line)
nmap g9 :<C-u>vim9cmd <SID>(exec_line)
xnoremap g: "vy:<C-u><C-r>=@v<CR><CR>
xnoremap g9 "vy:<C-u>vim9cmd <C-r>=@v<CR><CR>
# ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã®ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’ç¢ºèªã™ã‚‹ã‚„ã¤
nnoremap <expr> <Space>gh $'<Cmd>hi {synID(line('.'), col('.'), 1)->synIDattr('name')->substitute('^$', 'Normal', '')}<CR>'
# ä¿å­˜ã—ã¦å®Ÿè¡Œ TODO: `g!`ã¯å¾®å¦™ã‹ãªâ€¦
au vimrc FileType vim nnoremap g! <Cmd>update<CR><Cmd>source %<CR>
#}}}

# ----------------------------------------------------------
# ãã®ä»–ç´°ã€…ã—ãŸã® {{{
if has('clipboard')
	au vimrc FocusGained * @" = @+
	au vimrc FocusLost   * @+ = @"
endif

def ToggleNumber()
	if &number
		set nonumber
	elseif &relativenumber
		set number norelativenumber
	else
		set relativenumber
	endif
enddef

nnoremap <F11> <ScriptCmd>ToggleNumber()<CR>
nnoremap <F12> <Cmd>set wrap!<CR>

cnoremap <expr> <SID>(rpl) $'s///g \| noh{repeat('<Left>', 9)}'
nmap gs :<C-u>%<SID>(rpl)
nmap gS :<C-u>%<SID>(rpl)<ScriptCmd>feedkeys(expand('<cword>')->escape('^$.*?/\[]'), 'ni')<CR><Right>
vmap gs :<SID>(rpl)

nnoremap Y y$
nnoremap <Space>p $p
nnoremap <Space>P ^P
nnoremap <Space><Space>p o<Esc>P
nnoremap <Space><Space>P O<Esc>p

# åˆ†å‰²ã‚­ãƒ¼ãƒœãƒ¼ãƒ‰ã§å³æ‰‹è¦ªæŒ‡ãŒ<CR>ã«ãªã£ãŸã®ã§
nmap <CR> <Space>

# `T`å¤šå°‘æ½°ã—ã¦ã‚‚å¤§ä¸ˆå¤«ã ã‚ã†â€¦
nnoremap TE :<C-u>tabe<Space>
nnoremap TN <Cmd>tabnew<CR>
nnoremap TD <Cmd>tabe ./<CR>
nnoremap TT <Cmd>silent! tabnext #<CR>

onoremap <expr> } $"\<Esc>m`0{v:count1}{v:operator}\}"
onoremap <expr> { $"\<Esc>m`V{v:count1}\{{v:operator}"

xnoremap <expr> h mode() ==# 'V' ? '<Esc>h' : 'h'
xnoremap <expr> l mode() ==# 'V' ? '<Esc>l' : 'l'
xnoremap J j
xnoremap K k

inoremap ï½‹ï½Š <Esc>`^
inoremap ã€Œ ã€Œã€<Left>
inoremap ã€Œã€ ã€Œã€<Left>
inoremap ï¼ˆ ()<Left>
inoremap ï¼ˆï¼‰ ()<Left>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# æ§˜å­è¦‹ä¸­ {{{
# ä½¿ã‚ãªãã†ãªã‚‰å‰Šé™¤ã™ã‚‹
xnoremap <expr> p $'"_s<C-R>{v:register}<ESC>'
xnoremap P p
nnoremap <Space>h ^
nnoremap <Space>l $
nnoremap <Space>d "_d
nnoremap <Space>n <Cmd>nohlsearch<CR>
au vimrc CursorHold * feedkeys(' n') # nohã¯auã§å‹•ã‹ãªã„(:help noh)

# ã©ã£ã¡ã‚‚<C-w>wã€‚å·¦æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã¨å³æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã®ãƒãƒƒãƒ”ãƒ³ã‚°
nnoremap <Space>w <C-w>w
nnoremap <Space>o <C-w>w

# CSVã¨ã‹ã®ãƒ˜ãƒƒãƒ€ã‚’å›ºå®šè¡¨ç¤ºã™ã‚‹ã€‚ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ã˜ã‚ƒãªãã¦ã‚³ãƒãƒ³ãƒ‰å®šç¾©ã™ã‚Œã°ã„ã„ã‹ãªâ€¦
nnoremap <silent> <F10> <ESC>1<C-w>s:1<CR><C-w>w
xnoremap <F10> <ESC>1<C-w>s<C-w>w

# USâ†’ã€Œ"ã€æŠ¼ã—ã«ãã„ã€JISâ†’ã€Œ'ã€æŠ¼ã—ã«ãã„
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®Mã¯ã‚ã¾ã‚Šä½¿ã‚ãªã„ã‹ãªãâ€¦
nnoremap ' "
nnoremap m '
nnoremap M m

# ã†ãƒ¼ã‚“â€¦
inoremap jj <C-o>
inoremap jjh <C-o>^
inoremap jjl <C-o>$
inoremap jje <C-o>e<C-o>a
inoremap jj; <C-o>$;
inoremap jj, <C-o>$,
inoremap jj{ <C-o>$ {
inoremap jj} <C-o>$ }
inoremap jj<CR> <C-o>$<CR>
inoremap jjk ã€Œã€<Left>
inoremap jjx <ScriptCmd>ToggleCheckBox()<CR>
# ã“ã‚Œã¯ã¡ã‚‡ã£ã¨æŠ¼ã—ã«ãã„(è‡ªåˆ†ã®ã‚­ãƒ¼ãƒœãƒ¼ãƒ‰ã ã¨)
inoremap <M-x> <ScriptCmd>ToggleCheckBox()<CR>
# è‹±å˜èªã¯`q`ã®ã‚ã¨ã¯å¿…ãš`u`ã ã‹ã‚‰`q`ã‚’ãƒ—ãƒ¬ãƒ•ã‚£ãƒƒã‚¯ã‚¹ã«ã™ã‚‹æ‰‹ã‚‚ã‚ã‚Šã‹ï¼Ÿ
# ãã‚‚ãã‚‚`q`ãŒæŠ¼ã—ã«ãã„ã‹â€¦
cnoremap qq <C-f>

# syntaxå›ºæœ‰ã®è¿½åŠ å¼·èª¿
def ClearMySyntax()
	for id in get(w:, 'my_syntax', [])
		matchdelete(id)
	endfor
	w:my_syntax = []
enddef
def AddMySyntax(group: string, pattern: string)
	w:my_syntax->add(matchadd(group, pattern))
enddef
au vimrc Syntax * ClearMySyntax()
# ã€Œ==#ã€ã¨ã‹ã®å­˜åœ¨ã‚’å¿˜ã‚Œãªã„ã‚ˆã†ã«
au vimrc Syntax javascript,vim AddMySyntax('SpellRare', '\s[=!]=\s')
# åŸºæœ¬çš„ã«normalã¯å†ãƒãƒƒãƒ”ãƒ³ã‚°ã•ã›ãªã„ã‚ˆã†ã«ã€Œ!ã€ã‚’ä»˜ã‘ã‚‹
au vimrc Syntax vim AddMySyntax('SpellRare', '\<normal!\@!')

# è‡ªåˆ†ã§ä½œã£ãŸã®ã«ä½¿ã‚ãªã™ãã‚‹ã®ã§å•“ç™º
textobj#user#map('twochars', {'-': {'select-a': 'aa', 'select-i': 'ii'}})

# 'itchyny/vim-cursorword'ã®ç°¡æ˜“CursorHoldç‰ˆ
def HiCursorWord()
	var cword = expand('<cword>')
	if cword !=# '' && cword !=# get(w:, 'cword_match', '')
		if exists('w:cword_match_id')
			matchdelete(w:cword_match_id)
			unlet w:cword_match_id
		endif
		if cword =~ "^[a-zA-Z0-9]"
			w:cword_match_id = matchadd('CWordMatch', cword)
			w:cword_match = cword
		endif
	endif
enddef
au vimrc CursorHold * HiCursorWord()
au vimrc ColorScheme * hi CWordMatch cterm=underline gui=underline

# é¸æŠä¸­ã®æ–‡å­—æ•°ã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—
def PopupVisualLength()
	var text = GetVisualSelectionLines()->join('')
	popup_create($'{strlen(text)}chars', {
		pos: 'botleft',
		line: 'cursor-1',
		col: 'cursor',
		moved: 'any',
		padding: [1, 1, 1, 1],
	})
enddef
vnoremap <C-g> <ScriptCmd>PopupVisualLength()<CR>

#noremap <F1> <Cmd>smile<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# â€  ã‚ã¨ã‚‚ã†1å›ã€Œã“ã‚Œä½¿ã£ã¦ãªã„ãªâ€¦ã€ã¨æ€ã£ãŸã¨ãã«æ¶ˆã™ {{{

nnoremap <Space>a A

# sandwich
MultiCmd nnoremap,xnoremap Sa <Plug>(operator-sandwich-add)<if-nnoremap>iw
nmap S^ v^S
nmap S$ vg_S

# æœ€å¾Œã®é¸æŠç¯„å›²ã‚’ç¾åœ¨è¡Œã®ä¸‹ã«ç§»å‹•ã™ã‚‹
nnoremap <expr> <Space>m $'<Cmd>{getpos("'<")[1]},{getpos("'>")[1]}move {getpos('.')[1]}<CR>'

#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆãƒãƒƒãƒ”ãƒ³ã‚°ãƒ‡ãƒ¼ {{{
if strftime('%d') ==# '01'
	def DMD()
		notification#show("âœ¨ Today, Let's enjoy the default key mapping ! âœ¨")
		imapclear
		mapclear
	enddef
	au vimrc VimEnter * DMD()
endif
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# è‰² {{{
def DefaultColors()
	g:rainbow_conf = {
		guifgs: ['#9999ee', '#99ccee', '#99ee99', '#eeee99', '#ee99cc', '#cc99ee'],
		ctermfgs: ['105', '117', '120', '228', '212', '177']
	}
	g:rcsv_colorpairs = [
		['105', '#9999ee'], ['117', '#99ccee'], ['120', '#99ee99'],
		['228', '#eeee99'], ['212', '#ee99cc'], ['177', '#cc99ee']
	]
enddef
au vimrc ColorSchemePre * DefaultColors()
au vimrc ColorScheme * hi! link CmdHeight0Horiz TabLineFill
au vimrc ColorScheme * hi! link ALEVirtualTextWarning ALEStyleWarningSign
au vimrc ColorScheme * hi! link ALEVirtualTextError ALEStyleErrorSign

# å¥½ã¿ã§ãƒã‚¤ãƒ©ã‚¤ãƒˆ
# vimrcå†èª­ã¿è¾¼ã¿ã§ã‚¯ãƒªã‚¢&å†è¨­å®šã•ã‚Œãªã„ã‘ã©é¢å€’ã ã‹ã‚‰ãƒ¨ã‚·
def MyMatches()
	if exists('w:my_matches') && !empty(getmatches())
		return
	endif
	w:my_matches = 1
	matchadd('String', 'ã€Œ[^ã€]*ã€')
	matchadd('Label', '^\s*â– .*$')
	matchadd('Delimiter', 'WARN\|æ³¨æ„\|æ³¨:\|[â˜…â€»][^\s()ï¼ˆï¼‰]*')
	matchadd('Todo', 'TODO')
	matchadd('Error', 'ERROR')
	matchadd('Delimiter', '- \[ \]')
	matchadd('SpellRare', '[ï½-ï½šï¼¡-ï¼ºï¼-ï¼™ï¼ˆï¼‰ï½›ï½]')
	# å…¨è§’ç©ºç™½ã¨åŠè§’å¹…ã®å††è¨˜å·
	matchadd('SpellBad', '[ã€€Â¥]')
	# ç¨€ã«ã‚ˆãtypoã™ã‚‹å˜èª(æ°—ã¥ã„ãŸã‚‰è¿½åŠ ã™ã‚‹)
	matchadd('SpellBad', 'stlye')
enddef
au vimrc VimEnter,WinEnter * MyMatches()

# æ–‡æœ«ç©ºç™½(&listãŒæœ‰åŠ¹ã®ã¨ãã ã‘SpellBadã§ç›®ç«‹ãŸã›ã‚‹)
def HiTail()
	if &list && !exists('w:hi_tail')
		w:hi_tail = matchadd('SpellBad', '\s\+$')
	elseif !&list && exists('w:hi_tail')
		# calendar.vimç­‰ã§è¦‹ã¥ã‚‰ããªã‚‹ã®ã§ãã®å¯¾å¿œ
		matchdelete(w:hi_tail)
		unlet w:hi_tail
	endif
enddef
au vimrc OptionSet list silent! HiTail()
# matchaddã¯ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦å˜ä½ã ãŒã€`setlocal list`ã‚’è€ƒæ…®ã—ã¦Buf...ã‚¤ãƒ™ãƒ³ãƒˆã§å®Ÿè¡Œã™ã‚‹
au vimrc BufNew,BufReadPost * silent! HiTail()

set t_Co=256
syntax on
set background=dark
silent! colorscheme girly
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ¡ãƒ¢ {{{
# <F1> <S-F1>ã§ãƒ•ã‚©ãƒ«ãƒ€ã‚’é–‹ã(win32)
# <F2> MRU
# <F3>
# <F4>
# <F5> æ—¥ä»˜é–¢ä¿‚
# <F6>
# <F7>
# <F8>
# <F9>
# <F10> ãƒ˜ãƒƒãƒ€è¡Œã‚’è¡¨ç¤º(ã‚ã‚“ã¾ã‚Šä½¿ã‚ãªã„)
# <F11> è¡Œç•ªå·è¡¨ç¤ºåˆ‡æ›¿
# <F12> æŠ˜ã‚Šè¿”ã—è¡¨ç¤ºåˆ‡æ›¿
#}}} -------------------------------------------------------

if '~/.vimrc_local'->expand()->filereadable()
	source ~/.vimrc_local
endif

def OpenLastfile()
	var lastfile = get(v:oldfiles, 0, '')->expand()
	if lastfile->filereadable()
		execute 'edit' lastfile
	endif
enddef
au vimrc VimEnter * ++nested if !BufIsSmth() | OpenLastfile() | endif

