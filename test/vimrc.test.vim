vim9script

v:errors = []
def! g:EchoErrors()
	# v:errorsè¦‹ã¥ã‚‰ã„â€¦
	for e in v:errors
		var m = matchlist(e, '\(line \d\+\:.*\): Expected \(.*\) but got \(.*\)')
		echo m[1]
		echo '  Expected: ' .. m[2]
		echo '    Actual: ' .. m[3]
	endfor
enddef

var progress = 0
var progress_char = 'ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ•˜ğŸ•™ğŸ•šğŸ•›'
def ShowProgress()
	progress += 1
	echon progress_char[progress % 12] .. progress
	redraw
enddef

# ãƒãƒƒãƒ”ãƒ³ã‚°ãŒæƒ³å®šå¤–ã«è¢«ã£ã¦ãªã„ã‹ç¢ºèªã™ã‚‹ {{{
var lst = []
def TestMapping()
	# ã‚ã–ã¨ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã‚‰ã›ã¦ã‚‹ã‚„ã¤
	# ä»¥ä¸‹ã¯vimrcå¤–ã§ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã£ã¦ã‚‹
	# n  <C-A> vim-reformatdate
	# n  <C-U> default.vim
	# n  <C-W><C-H> vim-shrink
	# n  <C-X> vim-reformatdate
	# n  Q     default.vim
	# n  gc    vim-caw
	var default_ignore = '\C' ..
		'n  \([hjklqsQSTY;'']\|gc\|gs\|zd\|zf\|<C-[AUWX]>\|<Esc>\)\|' ..
		'v  \([*]\)'

	# ã‚ã–ã¨è¢«ã‚‰ã›ã¦ã‚‹ã‚„ã¤(ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©)
	# sandwitch
	var user_ignore = '\C' ..
		'n  S\|' ..
		'v  S'

	# ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©ã®ãƒãƒƒãƒ”ãƒ³ã‚°
	var user_map = execute('map')

	# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®ãƒãƒƒãƒ”ãƒ³ã‚°
	var default_map = []
	# ãƒãƒ¼ãƒãƒ«1æ–‡å­—
	add(default_map, 'n  [-!#$%&()+,./:;=>?*:^|_~abcdefhijklmnopqrstuvwxABCDEFGHIJKLMOPQRSTUVWXY]')
	# ãƒãƒ¼ãƒãƒ«è‹±å­—ã‚¹ã‚¿ãƒ¼ãƒˆ
	add(default_map, 'n  g[-#$&''`*+,@~08;?DEHIJNPQRTUV^_abcdefFghijknmMopqrstuvw\]]')
	add(default_map, 'n  g<C-[AGH\]]>')
	add(default_map, 'n  g<\(CR\|Left\|Right\|Down\|End\|Home\|LeftMouse\|RightMouse\|Up\)>')
	add(default_map, 'n  y[yjk0-9]') # ã¦ãã¨ã†
	add(default_map, 'n  z[-+.=ACDEFGHLMNORWX^abcdefghijklmnopestvwxz]')
	add(default_map, 'n  zu[wgWG]')
	add(default_map, 'n  Z[ZQ]')
	# ãƒãƒ¼ãƒãƒ«è¨˜å·ã‚¹ã‚¿ãƒ¼ãƒˆ
	add(default_map, 'n  @[0-9a-zA-Z+.:%_"#@:]')
	add(default_map, 'n  "[0-9a-zA-Z+.:%_"#]')
	add(default_map, 'n  `[()<>\[\]`{}]')
	add(default_map, 'n  ''[a-z]')
	add(default_map, 'n  [\[\]]\([''#(*/DIP[]`cdfimpsz{]\|<C-D>\|<C-I>\|<MiddleMouse>\)')
	add(default_map, 'n  <[<jk0-9]') # ã¦ãã¨ã†
	# ãƒãƒ¼ãƒãƒ«CTRL
	add(default_map, 'n  <C-[ABCDEFGHIJLMNOPQRTUVWXYZ^@\]]>')
	add(default_map, 'n  <C-W>[-+:=>FHJKLPRSTW\^_bcdfhijklnopqrstvwxz}|\]]')
	add(default_map, 'n  <C-W><C-[-+:=>FHJKLPRSTW\^_bcdfhijklnopqrstvwxz}|\]]>')
	add(default_map, 'n  <C-W>\(g[FTft}\]]\|g<C-]>\|<Down>\|<Left>\|<Right>\|<Up>\)')
	add(default_map, 'n  <C-W><[^ >]* ')
	# ãƒãƒ¼ãƒãƒ«ãã®ä»–ã‚­ãƒ¼
	# add(default_map, 'n  <Esc>â€¦è‰²ã€…') ã¾ãã‚„ã‚‰ãªãã¦ã„ã„ã‹
	# ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰ä»¥å¤–ã®ç¢ºèªã¯ãã®ã†ã¡
	add(default_map, 'v  [y.@"''/?:>]')
	add(default_map, 'v  <[^ >]* ')
	add(default_map, 'v  <Esc>')

	# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã‚ŠãŒãªã„ã‹ã‚’ç¢ºèªã™ã‚‹
	for i in default_map
		ShowProgress()
		lst = []
		substitute(user_map, '\C' .. i .. '[^\n]*', '\=add(lst, submatch(0))', 'g')
		filter(lst, (k, v) => v !~ default_ignore)
		assert_equal([], lst, 'ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã£ã¦ã‚‹ã‹ã‚‚ /' .. i .. '/')
	endfor

	# ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©å†…ã§è¢«ã‚ŠãŒãªã„ã‹ã‚’ç¢ºèªã™ã‚‹
	var user_map_lines = split(user_map, "\n")
	for i in user_map_lines
		ShowProgress()
		var head = matchstr(i, '^.\s\+\S\+')
		if empty(head) || match(head, user_ignore) == 0
			continue
		endif
		head = escape(head,  '^$.*?/\[]')
		head = substitute(head, '^ ', '.', '')
		head = substitute(head, '^[xv]', '[xv]', '')
		head = substitute(head, '^[il]', '[il]', '')
		head = '^\C' .. head
		var dups = []
		for j in user_map_lines
			if match(j, head) == 0
			add(dups, j)
			endif
		endfor
		assert_equal([dups[0]], dups, 'ãƒãƒƒãƒ”ãƒ³ã‚°ãŒè¢«ã£ã¦ã‚‹ã‹ã‚‚')
	endfor
enddef
TestMapping()
# }}}

g:EchoErrors()
echo 'Ran all test.'

