vim9script

v:errors = []
const vimrc_lines = readfile('../.vimrc')
const vimrc_str = vimrc_lines->join("\n")

# ãƒ†ã‚¹ãƒˆç”¨ãƒ¡ã‚½ãƒƒãƒ‰ {{{
def! g:EchoErrors()
	# v:errorsè¦‹ã¥ã‚‰ã„â€¦
	for msg in v:errors
		var m = matchlist(msg, '\(line \d\+\:.*\): Expected \(.*\) but got \(.*\)')
		if len(m) == 0
			echo msg
		else
			echo m[1]
			echo '  Expected: ' .. m[2]
			echo '    Actual: ' .. m[3]
		endif
	endfor
enddef

var progress = 0
const progress_char = 'ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ•˜ğŸ•™ğŸ•šğŸ•›'
def ShowProgress()
	progress += 1
	echon progress_char[progress % 12] .. progress
	redraw
enddef

# æ­£è¦è¡¨ç¾ã§ãƒãƒƒãƒã™ã‚‹æ–‡å­—åˆ—ã‚’å…¨ã¦æŠ½å‡ºã™ã‚‹
def Scan(expr: any, pat: string, index: number = 0): list<string>
  var scanResult = []
  substitute(expr, pat, (m) => add(scanResult, m[index])[0], 'g')
  return scanResult
enddef
#}}}

# setãŒé‡è¤‡ã—ã¦ãªã„ã“ã¨ {{{
def TestSets()
	var sets = []
	const ignore_names = 'fcs\|foldmethod' # æƒ³å®šå†…ãªã®ã§ç„¡è¦–ã™ã‚‹åå‰s
	for line in vimrc_lines
		ShowProgress()
		var m = matchlist(line, '\<set\s\+\(\w\+\)')
		if len(m) == 0
			m = matchlist(line, '\<&\(\w\+\)\s*=')
		endif
		if len(m) == 0
			continue
		endif
		const name = m[1]
		if name =~ ignore_names
			continue
		endif
		if index(sets, name) != -1
			assert_report('set ' .. name .. 'ãŒè¤‡æ•°ç®‡æ‰€ã«ã‚ã‚‹ï¼')
		endif
		sets->add(name)
	endfor
enddef
#}}}

# ãƒãƒƒãƒ”ãƒ³ã‚°ãŒæƒ³å®šå¤–ã«è¢«ã£ã¦ãªã„ã“ã¨ {{{
def TestMapping()
	# ã‚ã–ã¨ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆ(`map`ã‚³ãƒãƒ³ãƒ‰ã§å–å¾—ã§ããªã„ã‚„ã¤)ã¨è¢«ã‚‰ã›ã¦ã‚‹ã‚„ã¤
	# n  Q  defaults.vimã§gqã«ã—ã¦ã‚‹ã‘ã©.vimrcã§qã¸å†ãƒãƒƒãƒ—
	# n  gc vim-caw
	# n  a  _cc
	# n  i  _cc
	# n  m  '
	# n  M  m
	# n  A  _cc
	# n  S  Sandwich
	# n  T  ã‚¿ãƒ–é–¢ä¿‚
	# n  Y  y$ãƒ˜ãƒ«ãƒ—ã«ã‚‚ãã†æ›¸ã„ã¦ã‚ã‚‹
	# n  :  <Plug><ahc-switch>:
	# v  :  <Plug><ahc-switch>:
	# v  /  <Plug><ahc-switch>/
	# v  ?  <Plug><ahc-switch>?
	# i     <C-U> defaults.vim
	var default_ignore = '\C' ..
		'n  \([ahijklmqsAMQSTY;''/?:]\|gc\|gs\|zd\|zf\|<C-[AWX]>\|<Esc>\)\|' ..
		'v  \([*/?:]\)\|' ..
		'i  \(<C-U>\)'

	# ã‚ã–ã¨è¢«ã‚‰ã›ã¦ã‚‹ã‚„ã¤(`map`ã‚³ãƒãƒ³ãƒ‰ã§å–å¾—ã§ãã‚‹ã‚„ã¤)
	# æ¦‚ã­ãƒ—ãƒ©ã‚°ã‚¤ãƒ³å†…ã§è¢«ã£ã¦ã‚‹
	# n  <Plug>fugitive:
	# i  {     vim-laxima
	# i  [     vim-laxima
	# i  <Esc> vim-laxima
	#    <SNR>XX_(save-cursor-pos) vim-textobj
	var user_ignore = '\C' ..
		'n  \([qS:]\|<Plug>fugitive:\)\|' ..
		'v  \([JS]\)\|' ..
		'i  \(<Esc>\|[ã€Œï¼ˆ\[{]\|jj\)\|' ..
		'   <SNR>\d\+_(save-cursor-pos)'

	# ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©ã®ãƒãƒƒãƒ”ãƒ³ã‚°
	var user_map = join([execute('map'), execute('map!')], "\n")

	# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®ãƒãƒƒãƒ”ãƒ³ã‚°
	var default_map = [
		# ãƒãƒ¼ãƒãƒ«1æ–‡å­—
		'n  [-!#$%&()+,./:;=>?*:^|_~abcdefhijklmnopqrstuvwxABCDEFGHIJKLMOPQRSTUVWXY]',
		# ãƒãƒ¼ãƒãƒ«è‹±å­—ã‚¹ã‚¿ãƒ¼ãƒˆ
		'n  g[-#$&''`*+,@~08;?DEHIJNPQRTUV^_abcdefFghijknmMopqrstuvw\]]',
		'n  g<C-[AGH\]]>',
		'n  g<\(CR\|Left\|Right\|Down\|End\|Home\|LeftMouse\|RightMouse\|Up\)>',
		'n  y[yjk0-9]', # ã¦ãã¨ã†
		'n  z[-+.=ACDEFGHLMNORWX^abcdefghijklmnopestvwxz]',
		'n  zu[wgWG]',
		'n  Z[ZQ]',
		# ãƒãƒ¼ãƒãƒ«è¨˜å·ã‚¹ã‚¿ãƒ¼ãƒˆ
		'n  @[0-9a-zA-Z+.:%_"#@:]',
		'n  "[0-9a-zA-Z+.:%_"#]',
		'n  `[()<>\[\]`{}]',
		'n  ''[a-z]',
		'n  [\[\]]\([''#(*/DIP[]`cdfimpsz{]\|<C-D>\|<C-I>\|<MiddleMouse>\)',
		'n  <[<jk0-9]', # ã¦ãã¨ã†
		# ãƒãƒ¼ãƒãƒ«CTRL
		'n  <C-[ABCDEFGHIJLMNOPQRTUVWXYZ^@\]]>',
		'n  <C-W>[-+:=>FHJKLPRSTW\^_bcdfhijklnopqrstvwxz}|\]]',
		'n  <C-W><C-[-+:=>FHJKLPRSTW\^_bcdfhijklnopqrstvwxz}|\]]>',
		'n  <C-W>\(g[FTft}\]]\|g<C-]>\|<Down>\|<Left>\|<Right>\|<Up>\)',
		'n  <C-W><[^ >]* ',
		# ãƒãƒ¼ãƒãƒ«ãã®ä»–ã‚­ãƒ¼
		# 	'n  <Esc>â€¦è‰²ã€…') ã¾ãã‚„ã‚‰ãªãã¦ã„ã„ã‹
		# ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰ä»¥å¤–ã®ç¢ºèªã¯ãã®ã†ã¡
		'v  [y.@"''/?:>]',
		'v  <[^ >]* ',
		'v  <Esc>',
	]

	# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã‚ŠãŒãªã„ã‹ã‚’ç¢ºèªã™ã‚‹
	for i in default_map
		ShowProgress()
		var dups = Scan(user_map, '\C' .. i .. '[^\n]*')
		dups->filter((k, v) => v !~ default_ignore)
		assert_equal([], dups, 'ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã£ã¦ã‚‹ã‹ã‚‚ /' .. i .. '/')
	endfor

	# ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©å†…ã§è¢«ã‚ŠãŒãªã„ã‹ã‚’ç¢ºèªã™ã‚‹
	var user_map_lines = split(user_map, "\n")
	for i in user_map_lines
		ShowProgress()
		var head = matchstr(i, '^.\s\+\S\+')
		if empty(head) || match(head, user_ignore) == 0
			continue
		endif
		var head_regex = head
			->escape('^$.*/\[]')
			->substitute('^ ', '[ nvxso]', '')
			->substitute('^n', '[ n]', '')
			->substitute('^v', '[ vxs]', '')
			->substitute('^s', '[ s]', '')
			->substitute('^x', '[ x]', '')
			->substitute('^o', '[ o]', '')
			->substitute('^!', '[!ilc]', '')
			->substitute('^i', '[!i]', '')
			->substitute('^l', '[!il]', '')
			->substitute('^c', '[!c]', '')
			->substitute('^', '^\\C', '')
		var dups = []
		for j in user_map_lines
			if match(j, head_regex) == 0
				add(dups, j)
			endif
		endfor
		dups->uniq() # imapã¨cmapã§ã€Œ!  ...ã€ ãŒé‡è¤‡ã™ã‚‹ã®ã§
		dups->filter((k, v) => v !~ user_ignore)
		assert_equal([dups[0]], dups, 'ãƒãƒƒãƒ”ãƒ³ã‚°ãŒè¢«ã£ã¦ã‚‹ã‹ã‚‚')
	endfor
enddef
# }}}

# ãã®ä»–ã‹ã‚“ãŸã‚“ãªãƒ†ã‚¹ãƒˆ {{{
def TestAutocmd()
	assert_equal([], Scan(vimrc_str, 'au\(tocmd\)\{0,1\} \%(vimrc\)\@!'), 'autocmdã¯ã™ã¹ã¦vimrcã‚°ãƒ«ãƒ¼ãƒ—ã«å±ã™ã‚‹ã“ã¨')
enddef
#}}}

# ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ã®ãƒ†ã‚¹ãƒˆ {{{
def TestMultiCmd()
	MultiCmd nmap,vmap xxx yyy<if-nmap>NNN<if-vmap>VVV<>zzz
	assert_equal("\n\nn  xxx           yyyNNNzzz", execute('nmap xxx'))
	assert_equal("\n\nv  xxx           yyyVVVzzz", execute('vmap xxx'))
	nunmap xxx
	vunmap xxx
enddef

def TestEnableDisable()
	Enable g:test_vimrc_enable
	Disable g:test_vimrc_disable
	assert_equal(1, g:test_vimrc_enable)
	assert_equal(0, g:test_vimrc_disable)
	unlet g:test_vimrc_enable
	unlet g:test_vimrc_disable
enddef

def TestTruncToDisplayWidth()
	# minifyã—ãŸã‹ã‚‰ãƒ†ã‚¹ãƒˆã—ã¥ã‚‰ã„ï¼ã¡ãã—ã‚‡ã†èª°ãŒã“ã‚“ãªã“ã¨ã‚’â€¦
	# var F = function('<SNR>1_TruncToDisplayWidth')
	var F = function('<SNR>1_E')
	assert_equal('123', F('123', 3))
	assert_equal('12>', F('1234', 3))
	assert_equal('ã‚ã„ã†', F('ã‚ã„ã†', 6))
	assert_equal('ã‚ã„>',  F('ã‚ã„ã†1', 6))
	assert_equal('ã‚ã„>',  F('ã‚ã„ã†', 5))
enddef
#}}}

# ãƒ†ã‚¹ãƒˆå®Ÿè¡Œ {{{
var testDefs = []
def SetupTestDefs()
	for i in range(line('$'))
		var m = getline(i)->matchlist('^def \(Test.*\)()')
		if !empty(m)
			testDefs->add(m[1])
		endif
	endfor
enddef
SetupTestDefs()

def CompTestDefs(A: any, L: any, P: any): list<string>
	return testDefs
enddef

def RunTestAtCursor()
	var m = getline('.')->matchlist('^def \(Test.*\)()')
	if !empty(m)
		echo 'Run' m[1]
		RunTest(m[1])
	endif
enddef

def RunTest(qargs: string = '')
	v:errors = []
	progress = 0
	var targets = empty(qargs) ? testDefs : qargs->split(' ')
	for target in targets
		execute target .. '()'
	endfor
	g:EchoErrors()
	if empty(v:errors)
		echo 'Success!'
	endif
enddef
command! -nargs=* -complete=customlist,CompTestDefs RunTest RunTest(<q-args>)
nnoremap <buffer> <Leader>T <Cmd>call <SID>RunTest()<CR>
nnoremap <buffer> <Leader>t <Cmd>call <SID>RunTestAtCursor()<CR>
#}}}

