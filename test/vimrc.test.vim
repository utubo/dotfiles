vim9script

v:errors = []
def! g:EchoErrors()
	# v:errorsè¦‹ã¥ã‚‰ã„â€¦
	for e in v:errors
		var m = matchlist(e, '\(line \d\+\:.*\): Expected \(.*\) but got \(.*\)')
		echo m[1]
		echo '  Expected: ' .. m[2]
		echo '    Actual: ' .. m[3]
	endfor
enddef

var progress = 0
var progress_char = 'ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ•˜ğŸ•™ğŸ•šğŸ•›'
def ShowProgress()
	progress += 1
	echon progress_char[progress % 12] .. progress
	redraw
enddef

# ãƒãƒƒãƒ”ãƒ³ã‚°ãŒæƒ³å®šå¤–ã«è¢«ã£ã¦ãªã„ã‹ç¢ºèªã™ã‚‹ {{{
var lst = []
def TestMapping()
	# ã‚ã–ã¨ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã‚‰ã›ã¦ã‚‹ã‚„ã¤
	# ä»¥ä¸‹ã¯vimrcå¤–ã§ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã£ã¦ã‚‹
	# n  <C-U> default.vim
	# n  Q     default.vim
	# n  gc    vim-caw
	var default_ignore = '\C' ..
		'n  \([hjklqsQSTY;'']\|gc\|gs\|zd\|zf\|<C-[AUWX]>\|<Esc>\)\|' ..
		'v  \([*]\)'

	# ã‚ã–ã¨è¢«ã‚‰ã›ã¦ã‚‹ã‚„ã¤(ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©)
	# ä»¥ä¸‹ã¯vimrcå¤–ã§ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã£ã¦ã‚‹
	# i  { vim-lexma
	# i  [ vim-lexma
	#    <SNR>XX_(save-cursor-pos) vim-textobj
	var user_ignore = '\C' ..
		'n  S\|' ..
		'v  \([JS]\)\|' ..
		'i  \([ã€Œï¼ˆ\[{]\|jj\)\|' ..
		'   .*(save-cursor-pos)'

	# ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©ã®ãƒãƒƒãƒ”ãƒ³ã‚°
	var user_map = join([execute('map'), execute('imap'), execute('cmap')], "\n")

	# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®ãƒãƒƒãƒ”ãƒ³ã‚°
	var default_map = [
		# ãƒãƒ¼ãƒãƒ«1æ–‡å­—
		'n  [-!#$%&()+,./:;=>?*:^|_~abcdefhijklmnopqrstuvwxABCDEFGHIJKLMOPQRSTUVWXY]',
		# ãƒãƒ¼ãƒãƒ«è‹±å­—ã‚¹ã‚¿ãƒ¼ãƒˆ
		'n  g[-#$&''`*+,@~08;?DEHIJNPQRTUV^_abcdefFghijknmMopqrstuvw\]]',
		'n  g<C-[AGH\]]>',
		'n  g<\(CR\|Left\|Right\|Down\|End\|Home\|LeftMouse\|RightMouse\|Up\)>',
		'n  y[yjk0-9]', # ã¦ãã¨ã†
		'n  z[-+.=ACDEFGHLMNORWX^abcdefghijklmnopestvwxz]',
		'n  zu[wgWG]',
		'n  Z[ZQ]',
		# ãƒãƒ¼ãƒãƒ«è¨˜å·ã‚¹ã‚¿ãƒ¼ãƒˆ
		'n  @[0-9a-zA-Z+.:%_"#@:]',
		'n  "[0-9a-zA-Z+.:%_"#]',
		'n  `[()<>\[\]`{}]',
		'n  ''[a-z]',
		'n  [\[\]]\([''#(*/DIP[]`cdfimpsz{]\|<C-D>\|<C-I>\|<MiddleMouse>\)',
		'n  <[<jk0-9]', # ã¦ãã¨ã†
		# ãƒãƒ¼ãƒãƒ«CTRL
		'n  <C-[ABCDEFGHIJLMNOPQRTUVWXYZ^@\]]>',
		'n  <C-W>[-+:=>FHJKLPRSTW\^_bcdfhijklnopqrstvwxz}|\]]',
		'n  <C-W><C-[-+:=>FHJKLPRSTW\^_bcdfhijklnopqrstvwxz}|\]]>',
		'n  <C-W>\(g[FTft}\]]\|g<C-]>\|<Down>\|<Left>\|<Right>\|<Up>\)',
		'n  <C-W><[^ >]* ',
		# ãƒãƒ¼ãƒãƒ«ãã®ä»–ã‚­ãƒ¼
		# 	'n  <Esc>â€¦è‰²ã€…') ã¾ãã‚„ã‚‰ãªãã¦ã„ã„ã‹
		# ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰ä»¥å¤–ã®ç¢ºèªã¯ãã®ã†ã¡
		'v  [y.@"''/?:>]',
		'v  <[^ >]* ',
		'v  <Esc>',
	]

	# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã‚ŠãŒãªã„ã‹ã‚’ç¢ºèªã™ã‚‹
	for i in default_map
		ShowProgress()
		lst = []
		substitute(user_map, '\C' .. i .. '[^\n]*', '\=add(lst, submatch(0))', 'g')
		filter(lst, (k, v) => v !~ default_ignore)
		assert_equal([], lst, 'ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¨è¢«ã£ã¦ã‚‹ã‹ã‚‚ /' .. i .. '/')
	endfor

	# ãƒ¦ãƒ¼ã‚¶ãƒ¼å®šç¾©å†…ã§è¢«ã‚ŠãŒãªã„ã‹ã‚’ç¢ºèªã™ã‚‹
	var user_map_lines = split(user_map, "\n")
	for i in user_map_lines
		ShowProgress()
		var head = matchstr(i, '^.\s\+\S\+')
		if empty(head) || match(head, user_ignore) == 0
			continue
		endif
		var head_regex = head
			->escape('^$.*?/\[]')
			->substitute('^ ', '[ nvxso]', '')
			->substitute('^n', '[ n]', '')
			->substitute('^v', '[ vxs]', '')
			->substitute('^s', '[ s]', '')
			->substitute('^x', '[ x]', '')
			->substitute('^o', '[ o]', '')
			->substitute('^!', '[!ilc]', '')
			->substitute('^i', '[!i]', '')
			->substitute('^l', '[!il]', '')
			->substitute('^c', '[!c]', '')
			->substitute('^', '^\\C', '')
		var dups = []
		for j in user_map_lines
			if match(j, head_regex) == 0
			add(dups, j)
			endif
		endfor
		dups->uniq() # imapã¨cmapã§ã€Œ!  ...ã€ ãŒé‡è¤‡ã™ã‚‹ã®ã§
		dups->filter((k, v) => v !~ user_ignore)
		assert_equal([dups[0]], dups, 'ãƒãƒƒãƒ”ãƒ³ã‚°ãŒè¢«ã£ã¦ã‚‹ã‹ã‚‚')
	endfor
enddef
TestMapping()
# }}}

g:EchoErrors()
echo 'Ran all test.'

