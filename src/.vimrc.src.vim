vim9script noclear
set encoding=utf-8
scriptencoding utf-8

# ----------------------------------------------------------
# åŸºæœ¬è¨­å®š {{{
set fileencodings=ucs-bom,utf-8,iso-2022-jp,cp932,euc-jp
set noexpandtab
set tabstop=3 # æ„å¤–ã¨ã‚ã‚Šãªæ°—ãŒã—ã¦ããŸâ€¦
set shiftwidth=0
set softtabstop=0
set autoindent
set smartindent
set breakindent
set backspace=indent,start,eol
set nf=alpha,hex
set virtualedit=block
set list
set listchars=tab:\|\ ,trail:-,extends:>,precedes:<,nbsp:%
set fillchars=
set cmdheight=1
set noshowcmd
set noshowmode
set display=lastline
set ambiwidth=double
set belloff=all
set ttimeoutlen=50
set wildmenu
set wildcharm=<Tab>
set autochdir
set backupskip=/var/tmp/*
set undodir=~/.vim/undo
set undofile
set updatetime=2000
set incsearch
set hlsearch

augroup vimrc
	# æ–°ã—ã„è‡ªç”±
	au!
augroup End
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ {{{
const rtproot = has('win32') ? '~/vimfiles' : '~/.vim'
const has_deno = executable('deno')

# ã“ã‚“ãªæ„Ÿã˜
# CmdEach nmap,xmap xxx yyy<if-nmap>NNN<if-xmap>VVV<endif>zzz
# â†“
# nmap xxx yyyNNNzzz | xmap xxx yyyVVVzzz
def CmdEach(qargs: string)
	const [cmds, args] = qargs->split('^\S*\zs')
	for cmd in cmds->split(',')
		const a = args
			->substitute($'<if-{cmd}>', '<endif>', 'g')
			->substitute('<if-[^>]\+>.\{-1,}\(<endif>\|$\)', '', 'g')
			->substitute('<endif>', '', 'g')
		execute cmd a
	endfor
enddef
command! -nargs=* CmdEach CmdEach(<q-args>)

# ã“ã‚“ãªæ„Ÿã˜
# Each j,k nnoremap {} g{}
# â†“
# nnoremap j gj
# nnoremap k gk
# â€»ã“ã‚Œä½¿ã†ã‚ˆã‚Šã¹ãŸã§æ›¸ã„ãŸã»ã†ãŒèµ·å‹•ã¯é€Ÿã„
# â€»CmdEachã‚’çµ±åˆã§ããã†
# â€»ã‚„ã‚Šã™ãæ„Ÿã¯å¦ã‚ãªã„
# â€»`{}`ã‚’å…¨ã¦ç½®æ›ã—ã¦ã—ã¾ã†ã®ã§ã“ã®ã‚³ãƒãƒ³ãƒ‰ã¯é‡ã­ã‚‰ã‚Œãªã„
def Each(qargs: string)
	const [items, args] = qargs->split('^\S*\zs')
	for i in items->split(',')
		execute args->substitute('{}', i, 'g')
	endfor
enddef
command! -nargs=* Each Each(<q-args>)

# ãã®ä»–
command! -nargs=1 -complete=var Enable  <args> = 1
command! -nargs=1 -complete=var Disable <args> = 0

def RemoveEmptyLine(line: number)
	silent! execute ':' line 's/\s\+$//'
	silent! execute ':' line 's/^\s*\n//'
enddef

def BufIsSmth(): bool
	return &modified || ! empty(bufname())
enddef

def IndentStr(expr: any): string
	return matchstr(getline(expr), '^\s*')
enddef

def StayCurPos(expr: string)
	const len = getline('.')->len()
	var cur = getcurpos()
	execute expr
	cur[2] += getline('.')->len() - len
	setpos('.', cur)
enddef

# æŒ‡å®šå¹…ä»¥ä¸Šãªã‚‰'>'ã§çœç•¥ã™ã‚‹
def TruncToDisplayWidth(str: string, width: number): string
	return strdisplaywidth(str) <= width ? str : $'{str->matchstr($'.*\%<{width + 1}v')}>'
enddef

# <Cmd>ã§defã‚’å®Ÿè¡Œã—ãŸã¨ãã®ãƒ“ã‚¸ãƒ¥ã‚¢ãƒ«ãƒ¢ãƒ¼ãƒ‰ã®ç¯„å›²(è¡Œ)
def! g:VFirstLast(): list<number>
	return [line('.'), line('v')]->sort('n')
enddef

def! g:VRange(): list<number>
	const a = g:VFirstLast()
	return range(a[0], a[1])
enddef
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ {{{

# jetpack {{{
const jetpackfile = expand( $'{rtproot}/pack/jetpack/opt/vim-jetpack/plugin/jetpack.vim')
const has_jetpack = filereadable(jetpackfile)
if ! has_jetpack
  const jetpackurl = 'https://raw.githubusercontent.com/tani/vim-jetpack/master/plugin/jetpack.vim'
  system($'curl -fsSLo {jetpackfile} --create-dirs {jetpackurl}')
endif

packadd vim-jetpack
jetpack#begin()
Jetpack 'tani/vim-jetpack', { 'opt': 1 }
Jetpack 'airblade/vim-gitgutter'
Jetpack 'cohama/lexima.vim' # æ‹¬å¼§è£œå®Œ
Jetpack 'delphinus/vim-auto-cursorline'
Jetpack 'dense-analysis/ale'
Jetpack 'easymotion/vim-easymotion'
Jetpack 'hrsh7th/vim-vsnip'
Jetpack 'hrsh7th/vim-vsnip-integ'
Jetpack 'itchyny/calendar.vim'
Jetpack 'kana/vim-textobj-user'
Jetpack 'kana/vim-smartword'
Jetpack 'LeafCage/vimhelpgenerator'
Jetpack 'luochen1990/rainbow' # è™¹è‰²æ‹¬å¼§
Jetpack 'machakann/vim-sandwich'
Jetpack 'mattn/vim-notification'
Jetpack 'matze/vim-move' # è¡Œç§»å‹•
Jetpack 'michaeljsmith/vim-indent-object'
Jetpack 'MTDL9/vim-log-highlighting'
Jetpack 'obcat/vim-hitspop'
Jetpack 'obcat/vim-sclow' # ã‚¹ã‚¯ãƒ­ãƒ¼ãƒ«ãƒãƒ¼
Jetpack 'osyo-manga/vim-textobj-multiblock'
Jetpack 'othree/html5.vim'
Jetpack 'othree/yajs.vim'
Jetpack 'prabirshrestha/asyncomplete-buffer.vim'
Jetpack 'prabirshrestha/asyncomplete.vim'
Jetpack 'thinca/vim-portal'
Jetpack 'thinca/vim-themis'
Jetpack 'tpope/vim-fugitive' # Gdiffã¨ã‹
Jetpack 'tyru/capture.vim' # å®Ÿè¡Œçµæœã‚’ãƒãƒƒãƒ•ã‚¡ã«ã‚­ãƒ£ãƒ—ãƒãƒ£
Jetpack 'tyru/caw.vim' # ã‚³ãƒ¡ãƒ³ãƒˆåŒ–
Jetpack 'yami-beta/asyncomplete-omni.vim'
Jetpack 'yegappan/mru'
Jetpack 'yuki-yano/dedent-yank.vim' # yankã™ã‚‹ã¨ãã«ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã‚’é™¤å»
Jetpack 'vim-jp/vital.vim'
# Fern
Jetpack 'lambdalisue/fern.vim'
Jetpack 'lambdalisue/fern-git-status.vim'
Jetpack 'lambdalisue/fern-renderer-nerdfont.vim'
Jetpack 'lambdalisue/fern-hijack.vim'
Jetpack 'lambdalisue/nerdfont.vim'
# ğŸ‘€æ§˜å­è¦‹ä¸­
Jetpack 'ctrlpvim/ctrlp.vim'
Jetpack 'mattn/ctrlp-matchfuzzy'
Jetpack 'sheerun/vim-polyglot' # ã„ã‚ã‚“ãªã‚·ãƒ³ã‚¿ãƒƒã‚¯ã‚¹ãƒã‚¤ãƒ©ã‚¤ãƒˆ
Jetpack 'tani/vim-typo'
# ğŸ¶ğŸš
Jetpack 'utubo/vim-colorscheme-girly'
Jetpack 'utubo/vim-hlpairs'
Jetpack 'utubo/vim-minviml'
Jetpack 'utubo/vim-cmdheight0'
Jetpack 'utubo/vim-registers-lite'
Jetpack 'utubo/vim-reformatdate'
Jetpack 'utubo/vim-tabtoslash'
Jetpack 'utubo/vim-yomigana'
# ğŸ¶ğŸšæ§˜å­è¦‹ä¸­
Jetpack 'utubo/jumpcursor.vim'
Jetpack 'utubo/vim-portal-aim'
Jetpack 'utubo/vim-shrink'
Jetpack 'utubo/vim-tablist'
Jetpack 'utubo/vim-tabpopupmenu'
Jetpack 'utubo/vim-textobj-twochars'

if has_deno
	Jetpack 'vim-denops/denops.vim'
	Jetpack 'vim-skk/skkeleton'
endif
jetpack#end()
if ! has_jetpack
	jetpack#sync()
endif
#}}}

# ALE {{{
Enable  g:ale_fix_on_save
Enable  g:ale_set_quickfix
Disable g:ale_echo_cursor
Disable g:ale_lint_on_insert_leave
Disable g:ale_set_loclist
g:ale_sign_error = 'ğŸ'
g:ale_sign_warning = 'ğŸ'
g:ale_linters = { javascript: ['eslint'] }
g:ale_fixers = { typescript: ['deno'] }
g:ale_lint_delay = &updatetime
nnoremap <silent> [a <Plug>(ale_previous_wrap)
nnoremap <silent> ]a <Plug>(ale_next_wrap)
#}}}

# cmdheight0, statusline {{{
# ã‚¢ã‚¤ã‚³ãƒ³
au vimrc WinNew,FileType * b:stl_icon = nerdfont#find()

# æ–‡å­—ã‚³ãƒ¼ãƒ‰ã¨æ”¹è¡Œã‚³ãƒ¼ãƒ‰
b:stl_bufinfo = ''
def UpdateStlBufInfo()
	var info = []
	if &fenc !=# 'utf-8' && !!&fenc
		info += [&fenc->toupper()]
	endif
	# ãªã‚“ã‹ `&ff !=# ...` ã‚’æ‹¬å¼§ã§ããã‚‰ãªã„ã¨Type mismatchã«ãªã‚‹â€¦
	if &ff !=# '' && (has('win32') && (&ff !=# 'dos') || !has('win32') && (&ff !=# 'unix'))
		info += [&ff ==# 'dos' ? 'CRLF' : &ff ==# 'unix' ? 'LF' : 'CR']
	endif
	if !info
		b:stl_bufinfo = ''
	else
		b:stl_bufinfo = '%#Cmdheight0Warn#' .. info->join(',') .. '%*'
	endif
enddef
au vimrc BufNew,BufRead,OptionSet * UpdateStlBufInfo()

# ã‚«ãƒ¼ã‚½ãƒ«ä»¥ä¸‹ã®markdownã®ãƒã‚§ãƒƒã‚¯ãƒœãƒƒã‚¯ã‚¹ã®æ•°
# æœ¬ä½“ã¯.vim/after/ftplugin/markdown.vim
w:ruler_mdcb = ''
au vimrc VimEnter,WinNew * w:ruler_mdcb = ''

# ãƒ¤ãƒ³ã‚¯ã—ãŸã‚„ã¤ã‚’è¡¨ç¤ºã™ã‚‹ã‚„ã¤
g:stl_reg = ''
def UpdateStlRegister()
	var reg = v:event.regcontents
		->join('â†µ')
		->substitute('\t', 'â€º', 'g')
		->TruncToDisplayWidth(20)
		->substitute('%', '%%', 'g')
	g:stl_reg = $'ğŸ“‹%#Cmdheight0Info#{reg}%*'
enddef
au vimrc TextYankPost * UpdateStlRegister()

# æ¯æ™‚vimèµ·å‹•å¾Œ45åˆ†ã‹ã‚‰15åˆ†é–“ä¼‘æ†©ã—ã‚ˆã†ã­
g:stl_worktime = 'ğŸ•›'
g:stl_worktime_open_at = get(g:, 'ruler_worktime_open_at', localtime())
def! g:VimrcTimer60s(timer: any)
	const hhmm = (localtime() - g:stl_worktime_open_at) / 60
	const mm = hhmm % 60
	#:stl_worktime = 'ğŸ•›ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ•˜ğŸ•™ğŸ•š'[mm / 5]
	g:stl_worktime = 'ğŸ•›ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ°ğŸ°ğŸ°'[mm / 5]
	if (mm ==# 45)
		notification#show("       â˜•ğŸ´ğŸ°\nHave a break time !")
	endif
	if g:stl_worktime ==# 'ğŸ°'
		g:stl_worktime = '%#Cmdheight0Warn#' .. g:stl_worktime .. '%*'
	endif
enddef
timer_stop(get(g:, 'vimrc_timer_60s', 0)) # .vimrcå†å®Ÿè¡Œã‚’è€ƒæ…®ã—ã¦ã‚¿ã‚¤ãƒãƒ¼ã‚’ã‚¹ãƒˆãƒƒãƒ—
g:vimrc_timer_60s = timer_start(60000, 'VimrcTimer60s', { repeat: -1 })

# cmdheight0è¨­å®š
g:cmdheight0 = {}
g:cmdheight0.delay = -1
g:cmdheight0.tail = "\ue0c6"
g:cmdheight0.sep  = "\ue0c6"
g:cmdheight0.sub  = ["\ue0b9", "\ue0bb"]
g:cmdheight0.horiznr = 'â”€'
g:cmdheight0.format = ' ' ..   # ãƒ‘ãƒ‡ã‚£ãƒ³ã‚°
	'%{b:stl_icon}%t' ..        # ã‚¢ã‚¤ã‚³ãƒ³ã¨ãƒ•ã‚¡ã‚¤ãƒ«å
	'%#CmdHeight0Error#%m%*' .. # ç·¨é›†æ¸ˆã¿ã‹
	'%|%=%|' ..                 # ä¸­å¤®
	'%{w:ruler_mdcb|}' ..       # markdownã®ãƒã‚§ãƒƒã‚¯ãƒœãƒƒã‚¯ã‚¹ã®æ•°
	'%{%g:stl_reg|%}' ..        # ãƒ¬ã‚¸ã‚¹ã‚¿
	'%3l:%-2c:%L%|' ..          # ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®
	'%{%b:stl_bufinfo|%}' ..    # æ–‡å­—ã‚³ãƒ¼ãƒ‰ã¨æ”¹è¡Œã‚³ãƒ¼ãƒ‰
	'%{%g:stl_worktime%}' ..    # ä½œæ¥­æ™‚é–“
	' '                         # ãƒ‘ãƒ‡ã‚£ãƒ³ã‚°
g:cmdheight0.laststatus = 0
nnoremap ZZ <ScriptCmd>cmdheight0#ToggleZen()<CR>

# Zenãƒ¢ãƒ¼ãƒ‰ã§terminalã ã‘ã«ãªã‚‹ã¨æ··ä¹±ã™ã‚‹ã®ã§
au vimrc WinEnter * {
	if winnr('$') ==# 1 && tabpagenr('$') ==# 1 && &buftype ==# 'terminal'
		cmdheight0#ToggleZen(0)
	endif
}
#}}}

# easymotion {{{
Enable  g:EasyMotion_smartcase
Enable  g:EasyMotion_use_migemo
Enable  g:EasyMotion_enter_jump_first
Disable g:EasyMotion_verbose
Disable g:EasyMotion_do_mapping
g:EasyMotion_keys = 'asdghklqwertyuiopzxcvbnmfjASDGHKLQWERTYUIOPZXCVBNMFJ;'
g:EasyMotion_prompt = 'EasyMotion: '
noremap s <Plug>(easymotion-s)
#}}}

# fern {{{
Enable g:fern#default_hidden
g:fern#renderer = "nerdfont"
au vimrc FileType fern {
	Enable b:auto_cursorline_disabled
	setlocal cursorline
	nnoremap <buffer> <F1> <Cmd>:q!<CR>
	nnoremap <buffer> p <Plug>(fern-action-leave)
}
nnoremap <F1> <Cmd>Fern . -reveal=% -opener=split<CR>
#}}}

# Git {{{
def GitAddAll()
	echoh MoreMsg
	echo 'git add -A -n'
	const list = system('git add -A -n')
	if !list
		echo 'none.'
	elseif !!v:shell_error
		echoh ErrorMsg
		echo list
		echoh Normal
		return
	else
		echoh DiffAdd
		echo list
		echoh Question
		if input("execute ? (y/n) > ", 'y') ==# 'y'
			system("git add -A")
		endif
	endif
	echoh Normal
enddef
def! g:ConventionalCommits(a: any, l: string, p: number): list<string>
	return ['âœ¨feat:', 'ğŸfix:', 'ğŸ“docs:', 'ğŸ”¨refactor:', 'ğŸ¨style:', 'âœ…test:', 'ğŸ”§chore', 'ğŸ‰release:']
enddef
command! -nargs=1 -complete=customlist,g:ConventionalCommits GitCommit Git commit -m <q-args>
def GitTagPush(tagname: string)
	echo system($"git tag '{tagname}'")
	echo system($"git push origin '{tagname}'")
enddef
command! -nargs=1 GitTagPush GitTagPush(<q-args>)
nnoremap <Space>ga <ScriptCmd>GitAddAll()<CR>
nnoremap <Space>gA :<C-u>Git add %
nnoremap <Space>gc :<C-u>GitCommit<Space><Tab>
nnoremap <Space>gp :<C-u>Git push
nnoremap <Space>gs <Cmd>Git status -sb<CR>
nnoremap <Space>gv <Cmd>Gvdiffsplit<CR>
nnoremap <Space>gd <Cmd>Gdiffsplit<CR>
nnoremap <Space>gl <Cmd>Git pull<CR>
nnoremap <Space>gt :<C-u>GitTagPush<Space>
nnoremap <Space>gC :<C-u>Git checkout %
#}}}

# lexima {{{
Enable g:lexima_accept_pum_with_enter
# æ­£è¦è¡¨ç¾ã®æ‹¬å¼§ `\(\)`ã¨`\{\}`
def g:SetupLexima(timer: number)
	lexima#add_rule({ char: '(', at: '\\\%#', input_after: '\)', mode: 'ic' })
	lexima#add_rule({ char: '{', at: '\\\%#', input_after: '\}', mode: 'ic' })
	lexima#add_rule({ char: ')', at: '\%#\\)', leave: 2, mode: 'ic' })
	lexima#add_rule({ char: '}', at: '\%#\\}', leave: 2, mode: 'ic' })
	lexima#add_rule({ char: '\', at: '\%#\\[)}]', leave: 1, mode: 'ic' })
	# cmdlineã§ã®æ‹¬å¼§
	au vimrc ModeChanged *:c* ++once {
		for pair in ['()', '{}', '""', "''", '``']
			lexima#add_rule({ char: pair[0], input_after: pair[1], mode: 'c' })
			lexima#add_rule({ char: pair[1], at: '\%#' .. pair[1], leave: 1, mode: 'c' })
		endfor
		# `I'm`ã‚’å…¥åŠ›ã§ãã‚‹ã‚ˆã†ã«ã™ã‚‹ãƒ«ãƒ¼ãƒ«
		lexima#add_rule({ char: "'", at: '[a-zA-Z]\%#''\@!', mode: 'c' })
	}
enddef
timer_start(1000, g:SetupLexima)
# }}}

# MRU {{{
nnoremap <F2> <Cmd>MRUToggle<CR>
g:MRU_Exclude_Files = has('win32') ? $'{$TEMP}\\.*' : '^/tmp/.*\|^/var/tmp/.*'
#}}}

# Portal {{{
nnoremap <Leader>a <Cmd>PortalAim<CR>
nnoremap <Leader>b <Cmd>PortalAim blue<CR>
nnoremap <Leader>o <Cmd>PortalAim orange<CR>
nnoremap <Leader>r <Cmd>PortalReset<CR>
#}}}

# sandwich {{{
g:sandwich#recipes = deepcopy(g:sandwich#default_recipes)
g:sandwich#recipes += [
	{ buns: ["\r", ''  ], input: ["\r"], command: ["normal! a\r"] },
	{ buns: ['',   ''  ], input: ['q'] },
	{ buns: ['ã€Œ', 'ã€'], input: ['k'] },
	{ buns: ['ã€', 'ã€‘'], input: ['K'] },
	{ buns: ['{ ', ' }'], input: ['{'] },
	{ buns: ['${', '}' ], input: ['${'] },
	{ buns: ['%{', '}' ], input: ['%{'] },
	{ buns: ['CommentString(0)', 'CommentString(1)'], expr: 1, input: ['c'] },
]
def! g:CommentString(index: number): string
	return &commentstring->split('%s')->get(index, '')
enddef
Enable g:sandwich_no_default_key_mappings
Enable g:operator_sandwich_no_default_key_mappings
CmdEach nmap,xmap Sd <Plug>(operator-sandwich-delete)<if-nmap>ab
CmdEach nmap,xmap Sr <Plug>(operator-sandwich-replace)<if-nmap>ab
CmdEach nnoremap,xnoremap S <Plug>(operator-sandwich-add)<if-nnoremap>iw
nmap <expr> Srr (matchstr(getline('.'), '[''"]', col('.')) ==# '"') ? "Sr'" : 'Sr"'
# `S${`ã¨è¢«ã£ã¦ã—ã¾ã†ã‘ã©ã¾ãã„ã„ã‹
nmap S$ vg_S
# å¾®èª¿æ•´
au vimrc User OperatorSandwichAddPre g:fix_sandwich_pos = getpos('.')
au vimrc User OperatorSandwichAddPost myutil#FixSandwichPos()
au vimrc User OperatorSandwichDeletePost myutil#RemoveAirBuns()
# å†…å´ã«é€£ç¶šã§æŒŸã‚€ã‚„ã¤
xnoremap Sm <ScriptCmd>myutil#BigMac()<CR>
nmap Sm viwSm
#}}}

# skk {{{
if has_deno
	if ! empty($SKK_JISYO_DIR)
		skkeleton#config({
		globalJisyo: expand($'{$SKK_JISYO_DIR}SKK-JISYO.L'),
			userJisyo: expand($'{$SKK_JISYO_DIR}.skkeleton'),
		})
	endif
	skkeleton#config({
		eggLikeNewline: true,
		keepState: true,
		showCandidatesCount: 1,
	})
	map! <C-j> <Plug>(skkeleton-toggle)
endif
#}}}

# textobj-user {{{
CmdEach onoremap,xnoremap ab <Plug>(textobj-multiblock-a)
CmdEach onoremap,xnoremap ib <Plug>(textobj-multiblock-i)
g:textobj_multiblock_blocks = [
	[ "(", ")" ],
	[ "[", "]" ],
	[ "{", "}" ],
	[ '<', '>' ],
	[ '"', '"', 1 ],
	[ "'", "'", 1 ],
	[ ">", "<", 1 ],
	[ "ã€Œ", "ã€", 1 ],
]
call textobj#user#plugin('nonwhitespace', {
  '-': { 'pattern': '\S\+', 'select': ['a<Space>', 'i<Space>'], }
})
#}}}

# è£œå®Œ {{{
CmdEach inoremap,snoremap <expr> JJ    vsnip#expandable() ? '<Plug>(vsnip-expand)' : 'JJ'
CmdEach inoremap,snoremap <expr> <C-l> vsnip#available(1) ? '<Plug>(vsnip-expand-or-jump)' : '<C-l>'
def RegisterAsyncompSource(name: string, white: list<string>, black: list<string>)
	execute printf("asyncomplete#register_source(asyncomplete#sources#%s#get_source_options({ name: '%s', whitelist: %s, blacklist: %s, completor: asyncomplete#sources#%s#completor }))", name, name, white, black, name)
enddef
RegisterAsyncompSource('omni', ['*'], ['c', 'cpp', 'html'])
RegisterAsyncompSource('buffer', ['*'], ['go'])
#}}}

# ãã®ä»– {{{
Enable g:rainbow_active
Enable  g:ctrlp_use_caching
Disable g:ctrlp_clear_cache_on_exit
g:ctrlp_match_func = {'match': 'ctrlp_matchfuzzy#matcher'}
g:ctrlp_cmd = 'CtrlPMixed'
g:auto_cursorline_wait_ms = &updatetime
g:hlairs = { delay: 250 }
g:loaded_matchparen = 1
au vimrc VimEnter * silent! NoMatchParen
Each w,b,e,ge nnoremap {} <Plug>(smartword-{})
nnoremap % <ScriptCmd>call hlpairs#Jump()<CR>
nnoremap [c <Plug>(GitGutterPrevHunk)
nnoremap ]c <Plug>(GitGutterNextHunk)
nnoremap <Space>t <ScriptCmd>tabpopupmenu#popup()<CR>
nnoremap <Space>T <ScriptCmd>tablist#Show()<CR>
CmdEach nnoremap,xnoremap <Space>c <Plug>(caw:hatpos:toggle)
CmdEach nnoremap,tnoremap <silent> <C-w><C-s> <Plug>(shrink-height)<C-w>w
CmdEach nnoremap,tnoremap <silent> <C-w><C-h> <Plug>(shrink-width)<C-w>w
# EasyMotionã¨ã©ã£ã¡ã‚’ä½¿ã†ã‹æ§˜å­è¦‹ä¸­
noremap <Space>s <Plug>(jumpcursor-jump)
#}}}

# é–‹ç™ºç”¨ {{{
const localplugins = expand($'{rtproot}/pack/local/opt/*')
if localplugins !=# ''
	&runtimepath = $'{substitute(localplugins, '\n', ',', 'g')},{&runtimepath}'
endif
g:vimhelpgenerator_version = ''
g:vimhelpgenerator_author = 'Author  : utubo'
g:vimhelpgenerator_defaultlanguage = 'en'
g:vimhelpgenerator_uri = 'https://github.com/utubo/'
#}}}

filetype plugin indent on
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ã‚³ãƒ”ãƒšå¯„ã›é›†ã‚è‰²ã€… {{{
au vimrc InsertLeave * set nopaste
au vimrc BufReadPost *.log* normal! G
xnoremap * "vy/\V<C-r>=substitute(escape(@v,'\/'),"\n",'\\n','g')<CR><CR>
inoremap jk <Esc>`^
inoremap <CR> <CR><C-g>u
# https://github.com/astrorobot110/myvimrc/blob/master/vimrc
set matchpairs+=ï¼ˆ:ï¼‰,ã€Œ:ã€,ã€:ã€,ã€:ã€‘,ï¼»:ï¼½,ï¼œ:ï¼
# https://github.com/Omochice/dotfiles
Each i,a,A nnoremap <expr> {} !empty(getline('.')) ? '{}' : '"_cc'
# ã™ã”ã„
# https://zenn.dev/mattn/articles/83c2d4c7645faa
Each +,-,>,< CmdEach nmap,tmap <C-w>{} <C-w>{}<SID>ws
Each +,-,>,< CmdEach nnoremap,tnoremap <script> <SID>ws{} <C-w>{}<SID>ws
CmdEach nmap,tmap <SID>ws <Nop>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ã‚¿ãƒ–å¹…ã‚„ã‚¿ãƒ–å±•é–‹ã‚’è‡ªå‹•è¨­å®š {{{
def SetupTabstop()
	const limit = 100
	const org = getpos('.')
	cursor(1, 1)
	if !!search('^\t', 'nc', limit)
		setlocal noexpandtab
		setlocal tabstop=3 # æ„å¤–ã¨ã‚ã‚Šãªæ°—ãŒã—ã¦ããŸâ€¦
	elseif !!search('^  \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=2
	elseif !!search('^    \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=4
	endif
	setpos('.', org)
enddef
au vimrc BufReadPost * SetupTabstop()
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# vimgrep {{{
command! -nargs=+ -complete=dir VimGrep myutil#VimGrep(<f-args>)
au vimrc WinEnter * if winnr('$') ==# 1 && &buftype ==# 'quickfix' | q | endif
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# diff {{{
set splitright
set fillchars+=diff:\ # å‰Šé™¤è¡Œã¯ç©ºç™½æ–‡å­—ã§åŸ‹ã‚ã‚‹
# diffãƒ¢ãƒ¼ãƒ‰ã‚’è‡ªå‹•ã§off https://hail2u.net/blog/software/vim-turn-off-diff-mode-automatically.html
au vimrc WinEnter * if (winnr('$') ==# 1) && !!getbufvar(winbufnr(0), '&diff') | diffoff | endif
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# æ—¥ä»˜é–¢ä¿‚ {{{
g:reformatdate_extend_names = [{
	a: ['æ—¥', 'æœˆ', 'ç«', 'æ°´', 'æœ¨', 'é‡‘', 'åœŸ'],
	A: ['æ—¥æ›œæ—¥', 'æœˆæ›œæ—¥', 'ç«æ›œæ—¥', 'æ°´æ›œæ—¥', 'æœ¨æ›œæ—¥', 'é‡‘æ›œæ—¥', 'åœŸæ›œæ—¥'],
}]
g:reformatdate_extend_formats = ['%m/%d(%a)']
reformatdate#init()
inoremap <expr> <F5> strftime('%Y/%m/%d')
cnoremap <expr> <F5> strftime('%Y%m%d')
nnoremap <F5> <ScriptCmd>reformatdate#reformat(localtime())<CR>
nnoremap <C-a> <ScriptCmd>reformatdate#inc(v:count)<CR>
nnoremap <C-x> <ScriptCmd>reformatdate#dec(v:count)<CR>
nnoremap <Space><F5> /\d\{4\}\/\d\d\/\d\d<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ã‚¹ãƒãƒ›ç”¨ {{{
# - ã‚­ãƒ¼ãŒå°ã•ã„ã®ã§æŠ¼ã—ã«ãã„ã‚‚ã®ã¯Spaceã¸ãƒãƒƒãƒ”ãƒ³ã‚°
# - ã‚¹ãƒãƒ›ã§ã®ã‚³ãƒ¼ãƒ‡ã‚£ãƒ³ã‚°ã¯åŸºæœ¬çš„ã«ãƒã‚°å–ã‚Š
# ã‚¹ã‚¿ãƒƒã‚¯ãƒˆãƒ¬ãƒ¼ã‚¹ã‹ã‚‰yankã—ã¦ã‚½ãƒ¼ã‚¹ã®è©²å½“ç®‡æ‰€ã‚’æ¢ã™ã®ã‚’è£œåŠ©
nnoremap <Space>e G?\cErr\\|Exception<CR>
nnoremap <expr> <Space>f $'{(getreg('"') =~ '^\d\+$' ? ':' : '/')}{getreg('"')}<CR>'
# ã‚¹ãƒãƒ›ã ã¨:ã¨/ã¨ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ãŒé ã„ã®ã§â€¦
nmap <Space>. :
nmap <Space>, /
nmap g<Space> g;
for i in range(1, 10)
	execute $'nmap <Space>{i % 10} <F{i}>'
endfor
nmap <Space><Space>1 <F11>
nmap <Space><Space>2 <F12>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# æŠ˜ã‚Šç•³ã¿ {{{
# ã“ã‚“ãªã‹ã‚“ã˜ã§ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã«åˆã‚ã›ã¦è¡¨ç¤ºğŸ“ {{{
def! g:MyFoldText(): string
	const src = getline(v:foldstart)
	const indent = repeat(' ', indent(v:foldstart))
	const text = &foldmethod ==# 'indent' ? '' : src->substitute(matchstr(&foldmarker, '^[^,]*'), '', '')->trim()
	return $'{indent}{text} ğŸ“'
enddef
set foldtext=g:MyFoldText()
set fillchars+=fold:\ # æŠ˜ã‚Šç•³ã¿æ™‚ã®ã€Œ-ã€ã¯åŠè§’ç©ºç™½
au vimrc ColorScheme * {
	hi! link Folded Delimiter
	hi! link ALEVirtualTextWarning ALEWarningSign
	hi! link ALEVirtualTextError ALEErrorSign
}
#}}}
# ãã®ä»–æŠ˜ã‚ŠãŸãŸã¿é–¢ä¿‚ {{{
set foldmethod=marker
au vimrc FileType markdown,yaml setlocal foldlevelstart=99 foldmethod=indent
au vimrc BufReadPost * :silent! normal! zO
nnoremap <expr> h (col('.') ==# 1 && 0 < foldlevel('.') ? 'zc' : 'h')
nnoremap Z<Tab> <Cmd>set foldmethod=indent<CR>
nnoremap Z{ <Cmd>set foldmethod=marker<CR>
nnoremap Zy <Cmd>set foldmethod=syntax<CR>
xnoremap zf <ScriptCmd>myutil#Zf()<CR>
nnoremap zd <ScriptCmd>myutil#Zd()<CR>
#}}}
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# Tabline {{{
# ä¾‹: `current.txt|âœsub.txt|ğŸ™>`(3ã¤ã‚ä»¥é™ã¯çœç•¥)
g:tabline_mod_sign = "\uf040" # é‰›ç­†ã‚¢ã‚¤ã‚³ãƒ³(Cicaã®çµµæ–‡å­—ã ã¨åŠè§’å¹…ã«ãªã£ã¦ã—ã¾ã†)
g:tabline_git_sign = 'ğŸ™'
g:tabline_dir_sign = 'ğŸ“‚'
g:tabline_term_sign = "\uf489" # `>_`ã¿ãŸã„ãªã‚¢ã‚¤ã‚³ãƒ³
g:tabline_labelsep = '|'
g:tabline_maxlen = 20

def MyTablabelSign(bufs: list<number>, overflow: bool = false): string
	var mod = ''
	var git = ''
	for b in bufs
		const bt = getbufvar(b, '&buftype')
		if bt ==# ''
			if !mod && getbufvar(b, '&modified')
				mod = g:tabline_mod_sign
			endif
			if !git
				var g = false
				silent! g = len(getbufvar(b, 'gitgutter', {'hunks': []}).hunks) !=# 0
				if g
					git = g:tabline_git_sign
				endif
			endif
		endif
		if overflow
			continue
		endif
		if bt ==# 'terminal'
			return g:tabline_term_sign
		endif
		const ft = getbufvar(b, '&filetype')
		if ft ==# 'netrw' || ft ==# 'fern'
			return g:tabline_dir_sign
		endif
	endfor
	return mod .. git
enddef

def! g:MyTablabel(tab: number = 0): string
	var label = ''
	var bufs = tabpagebuflist(tab)
	const win = tabpagewinnr(tab) - 1
	bufs = remove(bufs, win, win) + bufs
	var names = []
	var i = -1
	for b in bufs
		i += 1
		if len(names) ==# 2
			names += [(MyTablabelSign(bufs[i : ], true) .. '>')]
			break
		endif
		var name = bufname(b)
		if !name
			name = '[No Name]'
		elseif getbufvar(b, '&buftype') ==# 'terminal'
			name = term_getline(b, '.')->trim()
		endif
		name = name->pathshorten()
		if g:tabline_maxlen < len(name)
			name = '<' .. name->matchstr(repeat('.', g:tabline_maxlen - 1) .. '$')
		endif
		if names->index(name) ==# -1
			names += [MyTablabelSign([b]) .. name]
		endif
	endfor
	label ..= names->join(g:tabline_labelsep)
	return label
enddef

def! g:MyTabline(): string
	# å·¦ç«¯ã‚’ãƒãƒƒãƒ•ã‚¡ã®è¡¨ç¤ºã«åˆã‚ã›ã‚‹(ãšã‚Œã¦ã‚‹ã¨ãªã‚“ã‹æ°—æŒã¡æ‚ªã„ã®ã§)
	var line = '%#TabLineFill#'
	line ..= repeat(' ', getwininfo(win_getid(1))[0].textoff)
	# ã‚¿ãƒ–ä¸€è¦§
	const curtab = tabpagenr()
	for tab in range(1, tabpagenr('$'))
		line ..= tab ==# curtab ? '%#TabLineSel#' : '%#TabLine#'
		line ..= ' '
		line ..= g:MyTablabel(tab)
		line ..= ' '
	endfor
	line ..= '%#TabLineFill#%T'
	return line
enddef

set tabline=%!g:MyTabline()
set guitablabel=%{g:MyTablabel()}
#}}}

# ----------------------------------------------------------
# ãƒ“ã‚¸ãƒ¥ã‚¢ãƒ«ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
xnoremap u <ScriptCmd>undo\|normal! gv<CR>
xnoremap <C-R> <ScriptCmd>redo\|normal! gv<CR>
xnoremap <Tab> <ScriptCmd>StayCurPos('normal! >gv')<CR>
xnoremap <S-Tab> <ScriptCmd>StayCurPos('normal! <gv')<CR>
#}}}

# ----------------------------------------------------------
# ã‚³ãƒãƒ³ãƒ‰ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
CmdEach nnoremap,xnoremap / <Cmd>noh<CR>/
CmdEach nnoremap,xnoremap ? <Cmd>noh<CR>?
CmdEach nmap,xmap ; :
nnoremap <Space>; ;
nnoremap <Space>: :
cnoremap <C-h> <Left>
cnoremap <C-l> <Right>
cnoremap <C-n> <Down>
cnoremap <C-p> <Up>
cnoremap <expr> <C-r><C-r> trim(@")->substitute('\n', ' \| ', 'g')
cnoremap <expr> <C-r><C-e> escape(@", '~^$.*?/\[]')->substitute('\n', '\\n', 'g')
cnoreabbrev cs colorscheme
# ã€Œjjã€ã§<CR>
# ãŸã ã—ä¿å­˜ã¯ç‰‡æ‰‹ã§ã€Œ;jjã€ã§ã‚‚OK(ã€Œ;wjjã€ã˜ã‚ƒãªãã¦ã„ã„)
cnoremap <expr> jj (empty(getcmdline()) && getcmdtype() ==# ':' ? 'update<CR>' : '<CR>')
inoremap ;jj <Esc>`^<Cmd>update<CR>
# `/`ã‚’è£œå®Œ {{{
def CmdlineAutoPair(c: string): string
	if getcmdtype() !=# ':'
		return c
	endif
	const cl = getcmdline()
	if getcmdpos() !=# cl->len() + 1 || cl =~# '\s'
		return c
	endif
	const e = cl[-1]
	if e ==# 's'
		return $"{c}{c}{c}g\<Left>\<Left>\<Left>"
	endif
	if e ==# 'g' && c ==# '!'
		return "!//\<Left>"
	endif
	if e ==# 'g' || e ==# 'v'
		return $"{c}{c}\<Left>"
	endif
	return c
enddef
# `/`ä»¥å¤–ã‚‚ä½¿ã†ã‹ã‚‚`%s#foo/bar#buz#g`ã¿ãŸã„ãªã‹ã‚“ã˜
Each /,#,! cnoremap <script> <expr> {} CmdlineAutoPair('{}')
#}}}
#}}}

# ----------------------------------------------------------
# terminalã¨ã‹ {{{
if has('win32')
	command! Powershell :bo terminal ++close pwsh
	nnoremap SH <Cmd>Powershell<CR>
	nnoremap <S-F1> <Cmd>silent !start explorer %:p:h<CR>
else
	nnoremap SH <Cmd>bo terminal<CR>
endif
tnoremap <C-w>; <C-w>:
tnoremap <C-w><C-w> <C-w>w
tnoremap <C-w><C-q> exit<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒãƒƒãƒ•ã‚¡ã®æƒ…å ±ã‚’è‰²ä»˜ãã§è¡¨ç¤º {{{
def ShowBufInfo(event: string = '')
	if &ft ==# 'qf'
		return
	endif

	var isReadPost = event ==# 'BufReadPost'
	if isReadPost && !filereadable(expand('%'))
		# ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã¨ã‹ãŒä¸€æ™‚çš„ãªbufnameã‚’ä»˜ä¸ã—ã¦é–‹ã„ãŸå ´åˆã¯ç„¡è¦–ã™ã‚‹
		return
	endif

	var msg = []
	add(msg, ['Title', $'"{bufname()}"'])
	add(msg, ['Normal', ' '])
	if &modified
		add(msg, ['Delimiter', '[+]'])
		add(msg, ['Normal', ' '])
	endif
	if !isReadPost && !filereadable(expand('%'))
		add(msg, ['Tag', '[New]'])
		add(msg, ['Normal', ' '])
	endif
	if &readonly
		add(msg, ['WarningMsg', '[RO]'])
		add(msg, ['Normal', ' '])
	endif
	const w = wordcount()
	if isReadPost || w.bytes !=# 0
		add(msg, ['Constant', printf('%dL, %dB', w.bytes ==# 0 ? 0 : line('$'), w.bytes)])
		add(msg, ['Normal', ' '])
	endif
	add(msg, ['MoreMsg', &ff])
	add(msg, ['Normal', ' '])
	const enc = empty(&fenc) ? &encoding : &fenc
	add(msg, [enc ==# 'utf-8' ? 'MoreMsg' : 'WarningMsg', enc])
	add(msg, ['Normal', ' '])
	add(msg, ['MoreMsg', &ft])
	var msglen = 0
	const maxlen = &columns - 2
	for i in reverse(range(0, len(msg) - 1))
		var s = msg[i][1]
		var d = strdisplaywidth(s)
		msglen += d
		if maxlen < msglen
			const l = maxlen - msglen + d
			while !empty(s) && l < strdisplaywidth(s)
				s = s[1 :]
			endwhile
			msg[i][1] = s
			msg = msg[i : ]
			insert(msg, ['SpecialKey', '<'], 0)
			break
		endif
	endfor
	redraw
	echo ''
	for m in msg
		execute 'echohl' m[0]
		echon m[1]
	endfor
	echohl Normal
enddef

# Zenãƒ¢ãƒ¼ãƒ‰ã§ä½ç½®ãŒåˆ†ã‹ã‚‰ãªããªã‚‹ã®ã§POPUPã§ç¾åœ¨ä½ç½®ã‚’è¡¨ç¤º
def PopupCursorPos()
	popup_create($' {line(".")}:{col(".")} ', {
		pos: 'botleft',
		line: 'cursor-1',
		col: 'cursor+1',
		moved: 'any',
		padding: [1, 1, 1, 1],
	})
enddef

nnoremap <script> <C-g> <ScriptCmd>ShowBufInfo()<CR><scriptCmd>PopupCursorPos()<CR>
au vimrc BufNewFile,BufReadPost,BufWritePost * ShowBufInfo('BufNewFile')
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# é–‰ã˜ã‚‹ {{{
def QuitWin(expr: string)
	if winnr() ==# winnr(expr)
		return
	endif
	execute 'wincmd' expr
	if mode() ==# 't'
		quit!
	else
		confirm quit
	endif
enddef
Each h,j,k,l nnoremap q{} <ScriptCmd>QuitWin('{}')<CR>
nnoremap q <Nop>
nnoremap Q q
nnoremap qq <Cmd>confirm q<CR>
nnoremap qa <Cmd>confirm qa<CR>
nnoremap qn <Cmd>confirm tabclose +<CR>
nnoremap qp <Cmd>confirm tabclose -<CR>
nnoremap q# <Cmd>confirm tabclose #<CR>
nnoremap qo <Cmd>confirm tabonly<CR>
nnoremap q: q:
nnoremap q/ q/
nnoremap q? q?
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ç§»å‹•ã—ã¦ä¿å­˜ {{{
command! -nargs=1 -complete=file MoveFile myutil#MoveFile(<f-args>)
cnoreabbrev mv MoveFile
#}}}

# ----------------------------------------------------------
# vimrcã€pluginã€colorschemeä½œæˆç”¨ {{{
# ã‚«ãƒ¼ã‚½ãƒ«è¡Œã‚’å®Ÿè¡Œã™ã‚‹ã‚„ã¤
cnoremap <script> <expr> <SID>(exec_line) $'{getline('.')->substitute('^[ \t"#:]\+', '', '')}<CR>'
nnoremap <script> g: :<C-u><SID>(exec_line)
nnoremap <script> g9 :<C-u>vim9cmd <SID>(exec_line)
xnoremap g: "vy:<C-u><C-r>=@v<CR><CR>
xnoremap g9 "vy:<C-u>vim9cmd <C-r>=@v<CR><CR>
# ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã®ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’ç¢ºèªã™ã‚‹ã‚„ã¤
nnoremap <expr> <Space>gh $'<Cmd>hi {synID(line('.'), col('.'), 1)->synIDattr('name')->substitute('^$', 'Normal', '')}<CR>'
# ä»–ã®å®šç¾©ã¯.vim/after/ftplugin/vim.vim
#}}}

# ----------------------------------------------------------
# ãã®ä»–ç´°ã€…ã—ãŸã® {{{
if has('clipboard')
	au vimrc FocusGained * @" = @+
	au vimrc FocusLost   * @+ = @"
endif

nnoremap <F11> <ScriptCmd>myutil#ToggleNumber()<CR>
nnoremap <F12> <Cmd>set wrap!<CR>

nnoremap gs :<C-u>%s///g<Left><Left><Left>
nnoremap gS :<C-u>%s/<C-r>=escape(expand('<cword>'), '^$.*?/\[]')<CR>//g<Left><Left>
xnoremap gs :s///g<Left><Left><Left>
xnoremap gS "vy:<C-u>%s/<C-r>=substitute(escape(@v,'^$.*?/\[]'),"\n",'\\n','g')<CR>//g<Left><Left>

nnoremap Y y$
nnoremap <Space>p $p
nnoremap <Space>P ^P
nnoremap <expr> j (getline('.')->match('\S') + 1 ==# col('.')) ? '+' : 'j'
nnoremap <expr> k (getline('.')->match('\S') + 1 ==# col('.')) ? '-' : 'k'

# `T`å¤šå°‘æ½°ã—ã¦ã‚‚å¤§ä¸ˆå¤«ã ã‚ã†â€¦
nnoremap TE :<C-u>tabe<Space>
nnoremap TN <Cmd>tabnew<CR>
nnoremap TD <Cmd>tabe ./<CR>
nnoremap TT <Cmd>tabnext #<CR>

onoremap <expr> } $"\<Esc>m`0{v:count1}{v:operator}\}"
onoremap <expr> { $"\<Esc>m`V{v:count1}\{{v:operator}"

xnoremap <expr> h mode() ==# 'V' ? '<Esc>h' : 'h'
xnoremap <expr> l mode() ==# 'V' ? '<Esc>l' : 'l'
xnoremap J j
xnoremap K k

inoremap ï½‹ï½Š <Esc>`^
inoremap ã€Œ ã€Œã€<C-g>U<Left>
inoremap ã€Œã€ ã€Œã€<C-g>U<Left>
inoremap ï¼ˆ ()<C-g>U<Left>
inoremap ï¼ˆï¼‰ ()<C-g>U<Left>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# æ§˜å­è¦‹ä¸­ {{{
# ä½¿ã‚ãªãã†ãªã‚‰å‰Šé™¤ã™ã‚‹
nnoremap <Space>a A
xnoremap <expr> p $'"_s<C-R>{v:register}<ESC>'
xnoremap P p
nnoremap <Space>h ^
nnoremap <Space>l $
nnoremap <Space>n <Cmd>nohlsearch<CR>
au vimrc CursorHold * feedkeys(' n') # nohã¯auã§å‹•ã‹ãªã„(:help noh)

# ç§»å‹•ã—ãŸå¾ŒæŠ˜ã‚Šç•³ã¿ã‚’å±•é–‹
nnoremap <silent> g; g;zO

# è¾æ›¸ãƒ•ã‚¡ã‚¤ãƒ«æ›¸ãã¨ãã«ä¾¿åˆ©ã ã‘ã©ã‚‚ã—ã‹ã—ã¦<CR>ã£ã¦ãƒ—ãƒ¬ãƒ•ã‚£ãƒƒã‚¯ã‚¹ã«ãªã‚Šãˆã‚‹ï¼Ÿ
nnoremap <CR> j0

# â†“ã‚­ãƒ¼ãƒœãƒ¼ãƒ‰ã®è¨­å®šã§<CR>ã‚’<Space>ã«åˆ‡ã‚Šæ›¿ãˆã‚‰ã‚Œã‚‹ã‚ˆã†ã«ã—ãŸã®ã§ã‚‚ã†ä¸è¦ã‹ãªâ€¦
## åˆ†å‰²ã‚­ãƒ¼ãƒœãƒ¼ãƒ‰ã§å³æ‰‹è¦ªæŒ‡ãŒ<CR>ã«ãªã£ãŸã®ã§
#nmap <CR> <Space>

# <Tab>ã§ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆ
#nnoremap <Tab> <ScriptCmd>StayCurPos('normal! >>')<CR>
#nnoremap <S-Tab> <ScriptCmd>StayCurPos('normal! <<')<CR>

# <Tab>ã§tsvã¨ã‹csvã¨ã‹htmlã®æ¬¡ã®é …ç›®
nnoremap <Tab> <Cmd>call search('\(^\\|\t\\|, *\)\S\?', 'e')<CR>
nnoremap <S-Tab> <Cmd>call search('\(^\\|\t\\|, *\)\S\?', 'be')<CR>
au vimrc FileType html,xml,svg {
	nnoremap <buffer> <silent> <Tab> <Cmd>call search('>')<CR><Cmd>call search('\S')<CR>
	nnoremap <buffer> <silent> <S-Tab> <Cmd>call search('>', 'b')<CR><Cmd>call search('>', 'b')<CR><Cmd>call search('\S')<CR>
}

# CSVã¨ã‹ã®ãƒ˜ãƒƒãƒ€ã‚’å›ºå®šè¡¨ç¤ºã™ã‚‹ã€‚ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ã˜ã‚ƒãªãã¦ã‚³ãƒãƒ³ãƒ‰å®šç¾©ã™ã‚Œã°ã„ã„ã‹ãªâ€¦
nnoremap <silent> <F10> <ESC>1<C-w>s:1<CR><C-w>w
xnoremap <F10> <ESC>1<C-w>s<C-w>w

# USâ†’ã€Œ"ã€æŠ¼ã—ã«ãã„ã€JISâ†’ã€Œ'ã€æŠ¼ã—ã«ãã„
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®Mã¯ã‚ã¾ã‚Šä½¿ã‚ãªã„ã‹ãªãâ€¦
nnoremap ' "
nnoremap m '
nnoremap M m

# ã“ã“ã¾ã§èª­ã‚“ã 
nnoremap <F9> my
nnoremap <S-F9> 'y

# æ‚ªããªã„ã‘ã©ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰ã§jjã‚’èª¤çˆ†ã™ã‚‹
inoremap jj <C-o>
inoremap jje <C-o>e<C-o>a
inoremap jj; <C-o>$;<CR>
inoremap jj<Space> <C-o>$<CR>
inoremap jjk ã€Œã€<C-g>U<Left>
inoremap jj<Tab> <ScriptCmd>StayCurPos('normal! >>')<CR>
inoremap jj<S-Tab> <ScriptCmd>StayCurPos('normal! <<')<CR>
# ã“ã‚Œã¯ã¡ã‚‡ã£ã¨æŠ¼ã—ã«ãã„(è‡ªåˆ†ã®ã‚­ãƒ¼ãƒœãƒ¼ãƒ‰ã ã¨)
inoremap <M-x> <ScriptCmd>ToggleCheckBox()<CR>
# è‹±å˜èªã¯`q`ã®ã‚ã¨ã¯å¿…ãš`u`ã ã‹ã‚‰`q`ã‚’ãƒ—ãƒ¬ãƒ•ã‚£ãƒƒã‚¯ã‚¹ã«ã™ã‚‹æ‰‹ã‚‚ã‚ã‚Šã‹ï¼Ÿ
# ãã‚‚ãã‚‚`q`ãŒæŠ¼ã—ã«ãã„ã‹â€¦
cnoremap qj <Down>
cnoremap qk <Up>

# syntaxå›ºæœ‰ã®è¿½åŠ å¼·èª¿
def ClearMySyntax()
	for id in get(w:, 'my_syntax', [])
		matchdelete(id)
	endfor
	w:my_syntax = []
enddef
def AddMySyntax(group: string, pattern: string)
	w:my_syntax->add(matchadd(group, pattern))
enddef
au vimrc Syntax * ClearMySyntax()
# ã€Œ==#ã€ã¨ã‹ã®å­˜åœ¨ã‚’å¿˜ã‚Œãªã„ã‚ˆã†ã«
au vimrc Syntax javascript,vim AddMySyntax('SpellRare', '\s[=!]=\s')
# åŸºæœ¬çš„ã«normalã¯å†ãƒãƒƒãƒ”ãƒ³ã‚°ã•ã›ãªã„ã‚ˆã†ã«ã€Œ!ã€ã‚’ä»˜ã‘ã‚‹
au vimrc Syntax vim AddMySyntax('SpellRare', '\<normal!\@!')

# é¸æŠä¸­ã®æ–‡å­—æ•°ã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—
def PopupVisualLength()
	normal! "vy
	var text = @v->substitute('\n', '', 'g')
	popup_create($'{strlen(text)}chars', {
		pos: 'botleft',
		line: 'cursor-1',
		col: 'cursor+1',
		moved: 'any',
		padding: [1, 1, 1, 1],
	})
enddef
xnoremap <C-g> <ScriptCmd>PopupVisualLength()<CR>

# ã“ã‚Œã¯èª¤çˆ†ã—ãã†â€¦ä¾‹ãˆã°`all`ã¨ã‹`call`ã¨ã‹
def SkipParen(): string
	const c = matchstr(getline('.'), '.', col('.') - 1)
	if !c || stridx(')]}"''`ã€', c) ==# -1
		return 'll'
	endif
	# èª¤çˆ†é˜²æ­¢
	const a = matchstr(getline('.'), '.', col('.') - 2)
	if stridx('ae', a) !=# -1
		return 'll'
	endif
	return  "\<C-o>a"
enddef
inoremap <expr> ll SkipParen()

# `:%g!/re/d` ã®çµæœã‚’æ–°è¦ã‚¦ã‚¤ãƒ³ãƒ‰ã‚¦ã«è¡¨ç¤º
# (Buffer Regular Expression Print)
command! -nargs=1 Brep myutil#Brep(<q-args>, <q-mods>)

# cmdlineã§ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰ã¿ãŸã„ã«ã™ã‚‹ã‚„ã¤
def CmdToNormal(): string
	cnoremap jk <C-c>
	cnoremap h <Left>
	cnoremap l <Right>
	cnoremap b <S-Left>
	cnoremap w <S-Right>
	cnoremap $ <End><Left>
	cnoremap ^ <Home>
	cnoremap x <Delete>
	cnoremap <script> <expr> i CmdToInsert('i')
	cnoremap <script> <expr> a CmdToInsert('a')
	cmap A $a
	return ""
enddef
def CmdToInsert(c: string = 'i'): string
	Each h,l,b,w,^,$,x,i,a,A silent! cunmap {}
	cnoremap <script> <expr> jk CmdToNormal()
	return c ==# 'i' ? '' : "\<Right>"
enddef
au vimrc ModeChanged *:c CmdToInsert()
# â†“ã“ã‚Œã¯ç„¡ã—ã«ã—ã¦ã¿ã‚‹
#cnoremap jk <C-c>

# ã‚‚ã—ã‹ã—ã¦cmdwinã‚’1è¡Œã«ã™ã‚Œã°åŒã˜ã‚ˆã†ãªä½¿ã„å¿ƒåœ°ã«ãªã‚‹ã‹ã‚‚ï¼Ÿ
set cmdwinheight=1
def ExpandCmdwin()
	if winheight(0) ==# 1
		resize 7
		normal! ggG
	else
		normal! k
	endif
enddef
au vimrc CmdwinEnter * {
	nnoremap <buffer> k <ScriptCmd>ExpandCmdwin()<CR>
	normal! i
}

#noremap <F1> <Cmd>smile<CR>
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# â€  ã‚ã¨ã‚‚ã†1å›ã€Œã“ã‚Œä½¿ã£ã¦ãªã„ãªâ€¦ã€ã¨æ€ã£ãŸã¨ãã«æ¶ˆã™ {{{

# å­˜åœ¨ã‚’å¿˜ã‚Œã‚‹
# ã©ã£ã¡ã‚‚<C-w>wã€‚å·¦æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã¨å³æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã®ãƒãƒƒãƒ”ãƒ³ã‚°
nnoremap <Space>w <C-w>w
nnoremap <Space>o <C-w>w
nnoremap <Space><Space>p o<Esc>P
nnoremap <Space><Space>P O<Esc>p
nnoremap <Space>d "_d

# ä½¿ç”¨é »åº¦ãŒä½ã„ã†ãˆã«ã‚¹ãƒˆãƒ­ãƒ¼ã‚¯ã®å·®ãŒ1ã¤ã—ã‹ãªã„(ã‚¹ãƒãƒ›ã§ä½¿ã†ã‹ã‚‚ï¼Ÿ)
nnoremap <Space>y yiw

# sandwich
nmap S^ v^S

#}}} -------------------------------------------------------

# ----------------------------------------------------------
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆãƒãƒƒãƒ”ãƒ³ã‚°ãƒ‡ãƒ¼ {{{
if strftime('%d') ==# '01'
	au vimrc VimEnter * {
		notification#show("âœ¨ Today, Let's enjoy the default key mapping ! âœ¨")
		mapclear
		imapclear
		xmapclear
		cmapclear
		omapclear
		tmapclear
		# CursorHoldã§feedkyesã—ã¦ã„ã‚‹ã®ã§â€¦
		nnoremap <Space>n <Nop>
	}
endif
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# è‰² {{{
nnoremap <expr> ZB $"<Cmd>set background={&background ==# 'dark' ? 'light' : 'dark'}<CR>"
def DefaultColors()
	# (ãƒ¡ãƒ¢)autocmdã®{}ã¯è¡Œæœ«ãŒ`,`ã ã¨ã‚¨ãƒ©ãƒ¼ã«ãªã‚‹
	g:rcsv_colorpairs = [
		['105', '#9999ee'], ['117', '#99ccee'], ['120', '#99ee99'],
		['228', '#eeee99'], ['212', '#ee99cc'], ['177', '#cc99ee']
	]
enddef
au vimrc ColorSchemePre * DefaultColors()
au vimrc ColorScheme * {
	hi! link CmdHeight0Horiz TabLineFill
	hi! link ALEVirtualTextWarning ALEStyleWarningSign
	hi! link ALEVirtualTextError ALEStyleErrorSign
	hi! link CmdHeight0Horiz MoreMsg
}

# å¥½ã¿ã§ãƒã‚¤ãƒ©ã‚¤ãƒˆ
# vimrcå†èª­ã¿è¾¼ã¿ã§ã‚¯ãƒªã‚¢&å†è¨­å®šã•ã‚Œãªã„ã‘ã©é¢å€’ã ã‹ã‚‰ãƒ¨ã‚·
def MyMatches()
	if exists('w:my_matches') && !empty(getmatches())
		return
	endif
	w:my_matches = 1
	matchadd('String', 'ã€Œ[^ã€]*ã€')
	matchadd('Label', '^\s*â– .*$')
	matchadd('Delimiter', 'WARN\|æ³¨æ„\|æ³¨:\|[â˜…â€»][^\s()ï¼ˆï¼‰]*')
	matchadd('Todo', 'TODO')
	matchadd('Error', 'ERROR')
	matchadd('Delimiter', '- \[ \]')
	matchadd('SpellRare', '[ï½-ï½šï¼¡-ï¼ºï¼-ï¼™ï¼ˆï¼‰ï½›ï½]')
	# å…¨è§’ç©ºç™½ã¨åŠè§’å¹…ã®å††è¨˜å·
	matchadd('SpellBad', '[ã€€Â¥]')
	# ç¨€ã«ã‚ˆãtypoã™ã‚‹å˜èª(æ°—ã¥ã„ãŸã‚‰è¿½åŠ ã™ã‚‹)
	matchadd('SpellBad', 'stlye')
enddef
au vimrc VimEnter,WinEnter * MyMatches()

# æ–‡æœ«ç©ºç™½(&listãŒæœ‰åŠ¹ã®ã¨ãã ã‘SpellBadã§ç›®ç«‹ãŸã›ã‚‹)
def HiTail()
	if &list && !exists('w:hi_tail')
		w:hi_tail = matchadd('SpellBad', '\s\+$')
	elseif !&list && exists('w:hi_tail')
		# calendar.vimç­‰ã§è¦‹ã¥ã‚‰ããªã‚‹ã®ã§ãã®å¯¾å¿œ
		matchdelete(w:hi_tail)
		unlet w:hi_tail
	endif
enddef
au vimrc OptionSet list silent! HiTail()
# matchaddã¯ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦å˜ä½ã ãŒã€`setlocal list`ã‚’è€ƒæ…®ã—ã¦Buf...ã‚¤ãƒ™ãƒ³ãƒˆã§å®Ÿè¡Œã™ã‚‹
au vimrc BufNew,BufReadPost * silent! HiTail()

set t_Co=256
syntax on
set background=dark
silent! colorscheme girly
#}}} -------------------------------------------------------

# ----------------------------------------------------------
# çµ‚ã‚ã‚Šã« {{{
if '~/.vimrc_local'->expand()->filereadable()
	source ~/.vimrc_local
endif

def OpenLastfile()
	var lastfile = get(v:oldfiles, 0, '')->expand()
	if lastfile->filereadable()
		execute 'edit' lastfile
	endif
enddef
au vimrc VimEnter * ++nested if !BufIsSmth() | OpenLastfile() | endif
#}}}

# ----------------------------------------------------------
# ãƒ¡ãƒ¢ {{{
# <F1> fern <S-F1>ã§ãƒ•ã‚©ãƒ«ãƒ€ã‚’é–‹ã(win32)
# <F2> MRU
# <F3>
# <F4>
# <F5> æ—¥ä»˜é–¢ä¿‚
# <F6>
# <F7>
# <F8>
# <F9> ã“ã“ã¾ã§ã‚ˆã‚“ã 
# <F10> ãƒ˜ãƒƒãƒ€è¡Œã‚’è¡¨ç¤º(ã‚ã‚“ã¾ã‚Šä½¿ã‚ãªã„)
# <F11> è¡Œç•ªå·è¡¨ç¤ºåˆ‡æ›¿
# <F12> æŠ˜ã‚Šè¿”ã—è¡¨ç¤ºåˆ‡æ›¿
#}}} -------------------------------------------------------

