vim9script

#
# ãƒœãƒ„ã£ãŸã‘ã©å¾©æ´»ã•ã›ãŸããªã‚‹ã‹ã‚‚ã—ã‚Œãªã„è¨­å®šãŸã¡ã®å¢“å ´
#

# å¾®å¦™ãªã‚­ãƒ¼ãƒãƒƒãƒ”ãƒ³ã‚°é” {{{

# æœ€å¾Œã®é¸æŠç¯„å›²ã‚’ç¾åœ¨è¡Œã®ä¸‹ã«ç§»å‹•ã™ã‚‹
nnoremap <expr> <Space>m $'<Cmd>{getpos("'<")[1]},{getpos("'>")[1]}move {getpos('.')[1]}<CR>'

# }}}

# ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ {{{
def GetVisualSelectionLines(): list<string>
	var [ay, ax] = getpos('v')[1 : 2]
	var [by, bx] = getpos('.')[1 : 2]
	if by < ay
		[ax, bx] = [bx, ax]
		[ay, by] = [by, ay]
	endif
	var lines = getline(ay, by)
	if mode() ==# 'V'
		# nop
	elseif mode() ==# 'v' && ay !=# by
		lines[-1] = lines[-1][0 : bx - 1]
		lines[0] = lines[0][ax - 1 : ]
	else
		var [s, e] = [ax - 1, bx - 1]->sort('n')
		for i in range(0, by - ay)
			lines[i] = lines[i][s : e]
		endfor
	endif
	return lines
enddef

# ã“ã‚“ãªæ„Ÿã˜
# CmdEach nmap,xmap xxx yyy<if-nmap>NNN<if-xmap>VVV<endif>zzz
# â†“
# nmap xxx yyyNNNzzz | xmap xxx yyyVVVzzz
def CmdEach(qargs: string)
	const [cmds, args] = qargs->split('^\S*\zs')
	for cmd in cmds->split(',')
		const a = args
			->substitute($'<if-{cmd}>', '<endif>', 'g')
			->substitute('<if-[^>]\+>.\{-1,}\(<endif>\|$\)', '', 'g')
			->substitute('<endif>', '', 'g')
		execute cmd a
	endfor
enddef
command! -nargs=* CmdEach CmdEach(<q-args>)
suite.TestCmdEach = () => {
	CmdEach nmap,vmap xxx yyy<if-nmap>NNN<if-vmap>VVV<endif>zzz
	assert.equals(execute('nmap xxx'), "\n\nn  xxx           yyyNNNzzz")
	assert.equals(execute('vmap xxx'), "\n\nv  xxx           yyyVVVzzz")
	nunmap xxx
	vunmap xxx
}

#}}}

# 'itchyny/vim-cursorword'ã®ç°¡æ˜“CursorHoldç‰ˆ {{{
def HiCursorWord()
	var cword = expand('<cword>')
	if cword !=# '' && cword !=# get(w:, 'cword_match', '')
		if exists('w:cword_match_id')
			silent! matchdelete(w:cword_match_id)
			unlet w:cword_match_id
		endif
		if cword !~ '^[[-` -/:-@{-~]'
			w:cword_match_id = matchadd('CWordMatch', cword, 0)
			w:cword_match = cword
		endif
	endif
enddef
au vimrc CursorHold * HiCursorWord()
au vimrc ColorScheme * hi CWordMatch cterm=underline gui=underline
#}}}

# Insertãƒ¢ãƒ¼ãƒ‰ã®ãƒãƒƒãƒ”ãƒ³ã‚° {{{
inoremap jjh <C-o>^
inoremap jjl <C-o>$
#}}}

# ãƒ†ã‚¹ãƒˆç”¨ãƒ¡ã‚½ãƒƒãƒ‰ {{{
def! g:EchoErrors()
	# v:errorsè¦‹ã¥ã‚‰ã„â€¦
	for msg in v:errors
		var m = matchlist(msg, '\(line \d\+\:.*\): Expected \(.*\) but got \(.*\)')
		if len(m) == 0
			echo msg
		else
			echo m[1]
			echo '  Expected: ' .. m[2]
			echo '    Actual: ' .. m[3]
		endif
	endfor
enddef

var progress = 0
const progress_char = 'ğŸ•ğŸ•‘ğŸ•’ğŸ•“ğŸ•”ğŸ••ğŸ•–ğŸ•—ğŸ•˜ğŸ•™ğŸ•šğŸ•›'
def ShowProgress()
	progress += 1
	echon progress_char[progress % 12] .. progress
	redraw
enddef

var allTest = []
def GetAllTest(A: any = 0, L: any = 0, P: any = 0): list<string>
	if !empty(allTest)
		return allTest
	endif
	for i in range(line('$'))
		var m = getline(i)->matchlist('^def \(Test.*\)()')
		if !empty(m)
			allTest->add(m[1])
		endif
	endfor
	return allTest
enddef

def RunTestAtCursor()
	var m = getline('.')->matchlist('^def \(Test.*\)()')
	if !empty(m)
		echo 'Run' m[1]
		RunTest(m[1])
	endif
enddef

def RunTest(qargs: string = '')
	v:errors = []
	progress = 0
	var targets = empty(qargs) ? GetAllTest() : qargs->split(' ')
	for target in targets
		execute target .. '()'
	endfor
	g:EchoErrors()
	if empty(v:errors)
		echo 'Success!'
	endif
enddef
command! -nargs=* -complete=customlist,GetAllTest RunTest RunTest(<q-args>)
nnoremap <buffer> <Leader>T <Cmd>call <SID>RunTest()<CR>
nnoremap <buffer> <Leader>t <Cmd>call <SID>RunTestAtCursor()<CR>
#}}}

# vsnip ã‚¿ãƒ–ã§é¸æŠ {{{
# ã‚¿ãƒ–åŒºåˆ‡ã‚Šã®ãƒ†ã‚­ã‚¹ãƒˆ(ãƒ¦ãƒ¼ã‚¶ãƒ¼è¾æ›¸ãƒ•ã‚¡ã‚¤ãƒ«)ã‚’ç·¨é›†ã™ã‚‹ã®ã«ç…©ã‚ã—ã‹ã£ãŸ
for cmd in ['inoremap', 'snoremap']
	execute cmd "<expr> <Tab>   vsnip#jumpable(1)  ? '<Plug>(vsnip-jump-next)' : pumvisible() ? '<C-n>' : '<Tab>'"
	execute cmd "<expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)' : pumvisible() ? '<C-p>' : '<S-Tab>'"
endfor
#}}}

# vim-eft ã‹ãªã‚Šæƒœã—ã„â€¦ãƒã‚¤ãƒ©ã‚¤ãƒˆå¥½ã {{{
Jetpack 'hrsh7th/vim-eft' # fã¨tã‚’å˜èªå¢ƒç•Œã«ã™ã‚‹ã‚„ã¤

# eft {{{
nmap ; <Plug>(eft-repeat)
xmap ; <Plug>(eft-repeat)
omap ; <Plug>(eft-repeat)

nmap f <Plug>(eft-f)
xmap f <Plug>(eft-f)
omap f <Plug>(eft-f)
nmap F <Plug>(eft-F)
xmap F <Plug>(eft-F)
omap F <Plug>(eft-F)

nmap t <Plug>(eft-t)
xmap t <Plug>(eft-t)
omap t <Plug>(eft-t)
nmap T <Plug>(eft-T)
xmap T <Plug>(eft-T)
omap T <Plug>(eft-T)
#}}}

# }}}

# ã‚‚ã—ã‹ã—ã¦cmdwinã‚’1è¡Œã«ã™ã‚Œã°åŒã˜ã‚ˆã†ãªä½¿ã„å¿ƒåœ°ã«ãªã‚‹ã‹ã‚‚ï¼Ÿ {{{
set cmdwinheight=1
def ExpandCmdwin()
	if winheight(0) ==# 1
		resize 7
		normal! ggG
	else
		normal! k
	endif
enddef
au vimrc CmdwinEnter * {
	nnoremap <buffer> k <ScriptCmd>ExpandCmdwin()<CR>
	normal! i
}
#}}}

# yankã—ãŸæ–‡å­—ã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ— {{{
def PopupYankText()
	const text = ('ğŸ“‹ ' .. @"[0 : winwidth(0)])
		->substitute('\t', 'â€º', 'g')
		->substitute('\n', 'â†µ', 'g')
	const truncated = text->TruncToDisplayWidth(winwidth(0) - 10)
	const winid = popup_create(truncated, {
		line: 'cursor+1',
		col: 'cursor+1',
		pos: 'topleft',
		padding: [0, 1, 0, 1],
		fixed: true,
		moved: 'any',
		time: 2000,
	})
	win_execute(winid, 'syntax match PmenuExtra /[â€ºâ†µ]\|.\@<=>$/')
enddef
au vimrc TextYankPost * PopupYankText()
#}}}

# TruncToDisplayWidthã®ãƒ†ã‚¹ãƒˆ {{{
suite.TestTruncToDisplayWidth = () => {
	# minifyã—ãŸã‹ã‚‰ãƒ†ã‚¹ãƒˆã—ã¥ã‚‰ã„ï¼ã¡ãã—ã‚‡ã†èª°ãŒã“ã‚“ãªã“ã¨ã‚’â€¦
	#var F = function($'<SNR>{vimrc_sid}_TruncToDisplayWidth')
	const F = function($'<SNR>{vimrc_sid}_E')
	assert.equals(F('123',  3), '123')
	assert.equals(F('1234', 3), '12>')
	assert.equals(F('ã‚ã„ã†',  6), 'ã‚ã„ã†')
	assert.equals(F('ã‚ã„ã†1', 6), 'ã‚ã„>')
	assert.equals(F('ã‚ã„ã†',  5), 'ã‚ã„>')
	assert.equals(F('', -1), '')
	assert.equals(F('', 0), '')
	assert.equals(F('', 1), '')
	assert.equals(F('>', 1), '>')
	assert.equals(F('ã‚', 1), '>')
}
# }}}

# è‰²é–¢ä¿‚ {{{
# vim-cmdheight0ã®è¨­å®š
hi! link CmdHeight0Horiz MoreMsg

# ãƒãƒ¼ã‚¿ãƒ«ã¯æ°´è‰²ã¨ã‚ªãƒ¬ãƒ³ã‚¸ã«ã—ãŸã„â€¦
hi Portal_blue ctermbg=45 guibg=#00d7ff
hi Portal_orange ctermbg=214 guibg=#ffaf00

# defaultã‚‚æ‚ªããªã„
au vimrc ColorScheme default {
	hi MatchParen ctermbg=7 ctermfg=13 cterm=bold
	hi Search ctermbg=12 ctermfg=7
	hi TODO ctermbg=7 ctermfg=14
	hi String ctermbg=7
	hi SignColumn ctermbg=7
	hi FoldColumn ctermbg=7
	hi WildMenu ctermbg=7
	hi DiffText ctermbg=227
}
# }}}

# CursorHoldã§nohlsearchã™ã‚‹ {{{
# nohlsearchã¯autocmdã§ã¯å‹•ã‹ãªã„(:help noh)
# èª°ã‹ãŒautocmd CursorHoldã—ã¦ã‚Œã°å®šæœŸçš„ã«<CursorHold>ã‚­ãƒ¼ãŒã‚¹ãƒˆãƒ­ãƒ¼ã‚¯ã•ã‚Œã‚‹
nnoremap <CursorHold> <Cmd>nohlsearch<CR>
# }}}
