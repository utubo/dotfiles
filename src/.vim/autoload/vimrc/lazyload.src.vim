vim9script

# ------------------------------------------------------
# ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ {{{

# ã“ã‚“ãªæ„Ÿã˜
#   Each nmap,xmap j gj
#   â†’ nmap j gj | xmap j gj
# å…ˆé ­ä»¥å¤–ã«å·®ã—è¾¼ã‚“ã ã‚Šãƒã‚¹ãƒˆã—ãŸã„å ´åˆã¯ã“ã†
#   Each X=n,x Each Y=j,k Xmap Y gY
#   â†’ nmap j gj | nmap k gk | xmap j gj | xmap k gk
# â€»ã“ã‚Œä½¿ã†ã‚ˆã‚Šã¹ãŸã§æ›¸ã„ãŸã»ã†ãŒèµ·å‹•ã¯é€Ÿã„
def Each(qargs: string)
	var [items, cmd] = qargs->split('^\S*\zs')
	# Each foo,bar buz
	if items->stridx('=') ==# -1
		for v in items->split(',')
			execute $'{v} {cmd}'
		endfor
		return
	endif
	# Each X=foo,bar buz X
	const kv = items->split('=')
	const keys = kv[0]->split(',')
	const values = kv[1]->split(',')
	var i = 0
	while i < len(values)
		var c = cmd
		for k in keys
			c = c->substitute(k, values[i], 'g')
			i += 1
		endfor
		execute c
	endwhile
enddef
command! -nargs=* Each Each(<q-args>)

# ã‚ˆãã‚ã‚‹ã‚„ã¤
command! -nargs=1 -complete=var Enable  <args> = 1
command! -nargs=1 -complete=var Disable <args> = 0

# Windowsã§çª“ã‚’è¡¨ç¤ºã•ã›ãªã„system()
def g:System(cmd: string): string
	if !has('win32')
		return system(cmd)
	endif
	return g:SystemList(cmd)->join("\n")
enddef

def g:SystemList(cmd: string): list<string>
	if !has('win32')
		return systemlist(cmd)
	endif
	var result = []
	var job = job_start(cmd, {
		out_cb: (j, s) => {
			result->add(s)
		}
	})
	while job_status(job) ==# 'run'
		sleep 10m
	endwhile
	return result
enddef

# >>ã¨ã‹ã—ãŸã¨ãã«ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã‚’ã‚­ãƒ¼ãƒ—
def KeepCurpos(expr: string)
	const len = getline('.')->len()
	var cur = getcurpos()
	execute $'normal! {expr}'
	cur[2] += getline('.')->len() - len
	setpos('.', cur)
enddef

# SubMode
# e.g.)
# SubMode winsize nmap W j <C-w>+
# -> You can type Wjjj... to expand the window.
def SubMode(name: string, mapcmd: string, switch: string, lhs: string, ...rhs: list<string>)
	# <Space> prevents ghoast char.
	const s = $'<SID>sub{name}<Space>'
	const norcmd = mapcmd->substitute('map', 'noremap', '')
	execute $'{mapcmd} <script> {s} <Nop>'
	execute $'{mapcmd} <script> {s}<CR> <Nop>'
	execute $'{mapcmd} <script> {s}<Esc> <Nop>'
	execute $'{norcmd} <script> {s}{lhs} {rhs->join(' ')}{s}'
	execute $'{mapcmd} <script> {switch}{lhs} {s}{lhs}'
enddef
command! -nargs=* SubMode SubMode(<f-args>)
# }}}

# ------------------------------------------------------
# ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ {{{

# ã“ã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆå†…ã§å¿…è¦ã¨ãªã‚‹ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ {{{
packadd vim-reformatdate
packadd vim-textobj-user
packadd vim-headtail
packadd vim-popselect
# }}}

# zenmode {{{
au vimrc User Vim9skkModeChanged zenmode#Invalidate()
# }}}

# vim9skkp {{{
g:vim9skkp = {
	keymap: {
		commit: ["\<CR>", 'l'],
		cancel: ["\<C-g>", "\<C-e>"],
	},
	showmode: 'cursor',
	keep_midasi_mode: true,
	roman_table: {
		'ds': 'ã§ã™',
		'ms': 'ã¾ã™',
		'sr': 'ã™ã‚‹',
		'st': 'ã—ã¦',
		'ks': 'ãã ã•ã„',
		'g,': 'ãŒã€',
		'dg': 'ã§ã™ãŒã€',
		'mg': 'ã¾ã™ãŒã€',
		# é »å‡ºå˜èª
		'bg': 'ãƒã‚°',
		'fx': 'ä¿®æ­£',
		'dl': 'å‰Šé™¤',
		'tk': 'è¿½åŠ ', # 'a'ã¯'ã‚'ãªã®ã§'ad'ã«ã¯ã§ããªã„â€¦
		'cn': 'è¨­å®š',
		'wn': 'ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦',
		'ky': 'ã‚­ãƒ¼',
		'sk': 'SKK',
		# è¨˜å·
		'z*': 'â€»',
		'v.': 'ï¸™',
		# Vimç”¨èª
		'vb': 'ãƒãƒƒãƒ•ã‚¡',
		'vm': 'ãƒãƒƒãƒ”ãƒ³ã‚°',
		'vk': 'ã‚­ãƒ¼ãƒãƒƒãƒ”ãƒ³ã‚°',
		'vp': 'ãƒ—ãƒ©ã‚°ã‚¤ãƒ³',
	},
}
inoremap <LocalLeader>j <Plug>(vim9skkp-toggle)
cnoremap <LocalLeader>j <Plug>(vim9skkp-toggle)
nnoremap <LocalLeader>j a<Plug>(vim9skkp-enable)
nnoremap <LocalLeader><LocalLeader>j i<Plug>(vim9skkp-enable)
# }}}

# headtail {{{
noremap <Leader>ga ga
HeadTailMap g G
Each nmap,xmap g% gi%
Each nmap,xmap G% Gi%
# }}}

# textobj-user {{{
Each X=a,i Each onoremap,xnoremap Xb <Plug>(textobj-multiblock-X)
g:textobj_multiblock_blocks = [
	[ "(", ")" ],
	[ "[", "]" ],
	[ "{", "}" ],
	[ '<', '>' ],
	[ '"', '"', 1 ],
	[ "'", "'", 1 ],
	[ ">", "<", 1 ],
	[ "ã€Œ", "ã€", 1 ],
]
call textobj#user#plugin('nonwhitespace', {
	'-': { 'pattern': '\S\+', 'select': ['a<Space>', 'i<Space>'], }
})
# }}}

# Git {{{
command! -nargs=* GitAdd vimrc#git#Add(<q-args>)
command! -nargs=1 -complete=customlist,vimrc#git#ConventionalCommits GitCommit vimrc#git#Commit(<q-args>)
command! -nargs=1 -complete=customlist,vimrc#git#ConventionalCommits GitAmend vimrc#git#Amend(<q-args>)
command! -nargs=* GitPush vimrc#git#Push(<q-args>)
command! -nargs=1 GitTagPush vimrc#git#TagPush(<q-args>)
nnoremap <Space>ga <Cmd>GitAdd -A<CR>
nnoremap <Space>gs <Cmd>Git status -sb<CR>
nnoremap <Space>gv <Cmd>Gvdiffsplit<CR>
nnoremap <Space>gd <Cmd>Gdiffsplit<CR>
nnoremap <Space>gu <Cmd>Git pull<CR>
nnoremap <Space>gl <Cmd>Git log<CR>
# cmdlineã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—ã•ã›ã‚‹ãŸã‚ã«nnoremapã§ã¯ãªãã¦nmapã«ã—ã¦ãŠã
nmap <Space>gp :<C-u>GitPush<End>
nmap <Space>gt :<C-u>GitTagPush<Space>
nmap <Space>gC :<C-u>Git checkout %
nmap <Space>gc :<C-u>GitCommit<Space><Tab>
nmap <Space>gA :<C-u><Cmd>call setcmdline($'GitAmend {vimrc#git#GetLastCommitMessage()}')<CR>
def PullDotfiles()
	const vimrcpath = has('win32') ? '~/_vimrc' : '~/.vimrc'
	const dotfilespath = vimrcpath->expand()->resolve()->fnamemodify(':h')
	const cwd = getcwd()
	chdir(dotfilespath)
	echo g:System($'git pull')
	chdir(cwd)
	execute $'source {has('win32') ? '~/vimfiles' : '~/.vim'}/autoload/vimrc/ezpack.vim'
	EzpackInstall
enddef
nnoremap <Space>GU <ScriptCmd>PullDotfiles()<CR>
au CmdlineEnter * ++once silent! cunmap <C-r><C-g>
# }}}

# gh {{{
nnoremap <Space>GH <Cmd>e gh://utubo/repos<CR>
nnoremap <Space>gi <ScriptCmd>vimrc#gh#OpenCurrentIssues()<CR>
au vimrc FileType gh-repos vimrc#gh#ReposKeymap()
au vimrc FileType gh-issues vimrc#gh#IssuesKeymap()
au vimrc FileType gh-issue-comments vimrc#gh#IssueCommentsKeymap()
# }}}

# popselect {{{
g:popselect = {
	# è§’ãŒ`+`ã ã¨åœ§ãŒå¼·ã„ã®ã§â€¦
	borderchars: ['-', '|', '-', '|', '.', '.', "'", "'"],
	# filterã§ã‚‚æ•°å­—ã‚­ãƒ¼ã§é–‹ã
	filter_focused: true,
	want_number: false,
	# ä¸€æ™‚ãƒ•ã‚¡ã‚¤ãƒ«ã‚„ãƒ˜ãƒ«ãƒ—ãƒ•ã‚¡ã‚¤ãƒ«ã‚’é™¤å¤–
	files_ignore_regexp: '^/var/tmp\|/vim/vim91/doc/',
	# è¦–ç‚¹ç§»å‹•ã‚’å°‘ãªã
	pos: 'topleft',
	col: 'cursor',
	line: 'cursor+1',
}
nnoremap <F1> <ScriptCmd>popselect#dir#Popup()<CR>
nnoremap <F2> <ScriptCmd>popselect#mru#Popup()<CR>
nnoremap <F3> <ScriptCmd>popselect#buffers#Popup()<CR>
nnoremap <F4> <ScriptCmd>popselect#tabpages#Popup()<CR>
nnoremap <expr> <C-p> yankround#is_active() ? "\<Plug>(yankround-prev)" : "\<ScriptCmd>popselect#projectfiles#PopupWithMRU({ filter_focused: true })\<CR>"

# ã‚¿ãƒ–ç§»å‹•ã—ãŸã¨ãã‚‚ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—ã™ã‚‹
def PopselectTabpages()
	if !vimrc#tabpanel#IsVisible()
		popselect#tabpages#Popup()
	endif
enddef
Each X=t,T nnoremap gX gX<ScriptCmd>PopselectTabpages()<CR>

# gnã¨gpã§ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—ã—ãªãŒã‚‰ãƒãƒƒãƒ•ã‚¡ç§»å‹•
def ShowBuf(a: string)
	const b = bufnr()
	while true
		execute $'b{a}'
		# terminalã«ç§»å‹•ã™ã‚‹ã¨æ··ä¹±ã™ã‚‹ã®ã§ã‚¹ã‚­ãƒƒãƒ—ã™ã‚‹ || ç„¡é™ãƒ«ãƒ¼ãƒ—é˜²æ­¢
		if &buftype !=# 'terminal' || bufnr() ==# b
			break
		endif
	endwhile
	if !vimrc#tabpanel#IsVisible()
		popselect#buffers#Popup()
	endif
enddef
Each X=n,p nnoremap gX <ScriptCmd>ShowBuf('X')<CR>

# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®grã¯ä½¿ã‚ãªã„ã‹ãªâ€¦
nnoremap gr <C-^>
# }}}

# Portal {{{
nnoremap <Leader>a <Cmd>PortalAim<CR>
nnoremap <Leader>b <Cmd>PortalAim blue<CR>
nnoremap <Leader>o <Cmd>PortalAim orange<CR>
nnoremap <Leader>r <Cmd>PortalReset<CR>
# }}}

# yankround {{{
nmap p <Plug>(yankround-p)
xmap p <Plug>(yankround-p)
nmap P <Plug>(yankround-P)
nmap <C-n> <Plug>(yankround-next)
# gpã¯ä»–ã®ã“ã¨ã«ä½¿ã„ãŸã„â€¦
# nmap gp <ScriptCmd>vimrc#yankround#Paste('gp')<CR>
# xmap gp <ScriptCmd>vimrc#yankround#Paste('gp')<CR>
# nmap gP <ScriptCmd>vimrc#yankround#Paste('gP')<CR>
# }}}

# è£œå®Œ {{{
export def SkipParen(): string
	const c = matchstr(getline('.'), '.', col('.') - 1)
	# é–‰ã˜æ‹¬å¼§ã®é–“ã«TABæ–‡å­—ã‚’å…¥ã‚Œã‚‹ã“ã¨ã¯ãªã„ã ã‚ã†â€¦
	if !c || stridx(')]}>\''`ã€', c) ==# -1
		return "\<Tab>"
	else
		return  "\<C-o>a"
	endif
enddef
Each imap,smap <expr> <Tab> vsnip#jumpable(1) ? '<Plug>(vsnip-jump-next)' : pumvisible() ? '<C-n>' : vimrc#lazyload#SkipParen()
Each imap,smap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)' : pumvisible() ? '<C-p>' : '<S-Tab>'
# Copilotã¯æ§˜å­è¦‹
#g:copilot_no_tab_map = true
#imap <silent> <script> <expr> ;c copilot#Accept("\<CR>")
#au vimrc VimEnter * Copilot disable
# }}}

# ğŸ¶ğŸš {{{
g:registerslite_delay = 0.4
g:registerslite_hide_dupricate = 0
Enable g:skipslash_autocomplete
Each X=s,h Each nnoremap,tnoremap <silent> <C-w><C-X> <Plug>(shrink-height)<C-w>w
onoremap A <Plug>(textobj-twochars-a)
onoremap I <Plug>(textobj-twochars-i)
command! UpdateVim packadd vim-update|call vimupdate#Update()
# }}}

# è¨­å®šãŒè†¨ã‚‰ã‚“ã§ããŸã®ã§åˆ¥ãƒ•ã‚¡ã‚¤ãƒ«ã§å®šç¾© {{{
vimrc#lexima#LazyLoad()
vimrc#lsp#LazyLoad()
# }}}

# é…å»¶èª­ã¿è¾¼ã¿ã‚‚ã® {{{
Each nmap,xmap S <ScriptCmd>vimrc#sandwich#LazyLoad()<CR>S
nmap s <ScriptCmd>vimrc#easymotion#LazyLoad()<CR>s
Each key=<Leader>j,<Leader>k map key <ScriptCmd>vimrc#easymotion#LazyLoad()<CR>key
# }}}

# built-in {{{
packadd nohlsearch
packadd hlyank
nmap <Space>c <Cmd>packadd comment<CR>gcc
xmap <Space>c <Cmd>packadd comment<CR>gc
nnoremap <LocalLeader>h <Cmd>packadd helptoc<CR><Cmd>HelpToc<CR>
g:helptoc = { popup_borderchars: [] }
# }}}

# ãã®ä»– {{{
Enable g:rainbow_active
g:auto_cursorline_wait_ms = &updatetime
Each X=w,b,e,ge nnoremap X <Plug>(smartword-X)
nnoremap [c <Plug>(GitGutterPrevHunk)
nnoremap ]c <Plug>(GitGutterNextHunk)
# må°‘ã—æ½°ã—ã¦ã‚‚ã„ã„ã‹â€¦
vmap mj <Plug>MoveBlockDown
vmap mk <Plug>MoveBlockUp
nnoremap <C-o> :Back<CR>
nnoremap <C-i> :Forward<CR>
nnoremap <Leader><C-o> <C-o>
nnoremap <Leader><C-i> <C-i>
# }}}

# é–‹ç™ºç”¨ {{{
g:vimhelpgenerator_version = ''
g:vimhelpgenerator_author = 'Author  : utubo'
g:vimhelpgenerator_defaultlanguage = 'en'
g:vimhelpgenerator_uri = 'https://github.com/utubo/'
# }}}
# }}}

# ------------------------------------------------------
# ã‚³ãƒ”ãƒšå¯„ã›é›†ã‚è‰²ã€… {{{
au vimrc InsertLeave * set nopaste
au vimrc FileReadPost *.log* normal! G
# https://github.com/astrorobot110/myvimrc/blob/master/vimrc
set matchpairs+=ï¼ˆ:ï¼‰,ã€Œ:ã€,ã€:ã€,ã€:ã€‘,ï¼»:ï¼½,ï¼œ:ï¼
# https://github.com/Omochice/dotfiles
Each X=i,a,A nnoremap <expr> X !empty(getline('.')) ? 'X' : '"_cc'
# ã™ã”ã„
# https://zenn.dev/mattn/articles/83c2d4c7645faa
Each X=+,-,>,<lt> Each Y=nmap,tmap SubMode winsize Y <C-w> X <C-w>X
Each X=+,-,>,<lt> Each Y=nmap,tmap SubMode winsize Y <C-w> X <C-w>X
# æ„Ÿè¬
# https://zenn.dev/vim_jp/articles/43d021f461f3a4
nnoremap <A-J> <Cmd>copy.<CR>
nnoremap <A-K> <Cmd>copy-1<CR>
xnoremap <A-J> :copy'<-1<CR>gv
xnoremap <A-K> :copy'>+0<CR>gv
# https://zenn.dev/vim_jp/articles/2024-10-07-vim-insert-uppercase
def ToupperPrevWord(): string
	const col = getpos('.')[2]
	const substring = getline('.')[0 : col - 1]
	const word = matchstr(substring, '\v<(\k(<)@!)*$')
	return toupper(word)
enddef
inoremap <expr> ;l $"<C-w>{ToupperPrevWord()}"
# https://blog.atusy.net/2025/06/03/vim-contextful-mark/
au vimrc TextYankPost * execute $'au SafeState * ++once execute "normal! m{v:event.operator}"'
# https://blog.atusy.net/2023/12/17/vim-easy-to-remember-regnames/
au vimrc TextYankPost * {
	if !v:event.regname
		setreg(v:event.operator, getreg())
	endif
}
# from vim-textobj-entrie and vim-jp slack
Each onoremap,xnoremap ae :<C-u>keepjumps normal! G$vgo<CR>
# }}}

# ------------------------------------------------------
# <LocalLeader>ç³» {{{
g:maplocalleader = ';'
nnoremap <Space><LocalLeader> ;
noremap  <Space><LocalLeader> ;
# ;nã§æ±ºå®šã€;mã§ã‚­ãƒ£ãƒ³ã‚»ãƒ«
# ã‚³ãƒãƒ³ãƒ‰ãƒ¢ãƒ¼ãƒ‰ã®å®šç¾©ã¯cmdmode.src.vim
Each map,imap,cmap <LocalLeader>n <LocalLeader>(ok)
Each map,imap,cmap <LocalLeader>m <LocalLeader>(cancel)
Each nnoremap,inoremap <LocalLeader>(ok) <Esc><Cmd>Sav<CR>
noremap  <LocalLeader>(cancel) <Esc>
inoremap <LocalLeader>(cancel) <Esc>`^
# CTRLã®ä»£ã‚Š
nmap <LocalLeader>w <C-w>
nnoremap <LocalLeader>v <C-v>
nnoremap <LocalLeader>a <C-a>
nnoremap <LocalLeader>x <C-a>
# `;ttt...`ã€`;ddd...`ã§ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆèª¿æ•´
SubMode indent imap <LocalLeader> t <C-t>
SubMode indent imap <LocalLeader> d <C-d>
SubMode indent nmap <LocalLeader> t >>
SubMode indent nmap <LocalLeader> d <lt><lt>
SubMode indent xmap <LocalLeader> t >gv
SubMode indent xmap <LocalLeader> d <lt>gv
# ãã®ä»–
imap <LocalLeader><Space> <CR>
inoremap <LocalLeader>w <C-o>e<C-o>a
inoremap <LocalLeader>k ã€Œã€<C-g>U<Left>
inoremap <LocalLeader>u <Esc>u
nnoremap <LocalLeader>r "
nnoremap <LocalLeader>rr "0p
SubMode bs map! <LocalLeader> b <BS>
SubMode movecursor map! <LocalLeader> h <Left>
SubMode movecursor map! <LocalLeader> l <Right>
# }}}

# ------------------------------------------------------
# ã‚¹ãƒãƒ›ç”¨ {{{
# - ã‚­ãƒ¼ãŒå°ã•ã„ã®ã§æŠ¼ã—ã«ãã„ã‚‚ã®ã¯Spaceã¸ãƒãƒƒãƒ”ãƒ³ã‚°
# - ã‚¹ãƒãƒ›ã§ã®ã‚³ãƒ¼ãƒ‡ã‚£ãƒ³ã‚°ã¯åŸºæœ¬çš„ã«ãƒã‚°å–ã‚Š
# ã‚¹ã‚¿ãƒƒã‚¯ãƒˆãƒ¬ãƒ¼ã‚¹ã‹ã‚‰yankã—ã¦ã‚½ãƒ¼ã‚¹ã®è©²å½“ç®‡æ‰€ã‚’æ¢ã™ã®ã‚’è£œåŠ©
nnoremap <Space>e G?\cErr\\|Exception<CR>
nnoremap <expr> <Space>f $'{(getreg('"') =~ '^\d\+$' ? ':' : '/')}{getreg('"')}<CR>'
# ã‚¹ãƒãƒ›ã ã¨:ã¨/ã¨ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ãŒé ã„ã®ã§â€¦
nmap <Space>. :
nmap <Space>; :
nmap <Space>, /
nmap g<Space> g;
for i in range(1, 10)
	execute $'nmap <Space>{i % 10} <F{i}>'
endfor
nmap <Space><Space>1 <F11>
nmap <Space><Space>2 <F12>
# ãã®ä»–
nnoremap <Space>a A
nnoremap <Space>h ^
nnoremap <Space>l $
nnoremap <Space>y yiw
# }}}

# ------------------------------------------------------
# ãƒ“ã‚¸ãƒ¥ã‚¢ãƒ«ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
xnoremap u <ScriptCmd>undo\|normal! gv<CR>
xnoremap <C-R> <ScriptCmd>redo\|normal! gv<CR>
xnoremap <Tab> <ScriptCmd>KeepCurpos('>gv')<CR>
xnoremap <S-Tab> <ScriptCmd>KeepCurpos('<gv')<CR>
const vmode = ['v', 'V', "\<C-v>", "\<ESC>"] # minviml:fixed=vmode
xnoremap <script> <expr> v vmode[vmode->index(mode()) + 1]
# }}}

# ------------------------------------------------------
# ã‚³ãƒãƒ³ãƒ‰ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
# è€ƒãˆä¸­
Each nmap,xmap <LocalLeader>c :
Each nmap,xmap <LocalLeader>s /
Each nmap,xmap + :
Each nmap,xmap , :
Each nmap,xmap <Space><Space>, ,
# ãã®ä»–ã®è¨­å®š
au vimrc CmdlineEnter * ++once vimrc#cmdmode#ApplySettings()
# cmdlineã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—ã™ã‚‹
Each :=:,/,? Each nnoremap,vnoremap <script>: <ScriptCmd>vimrc#cmdmode#PopupMapping()<CR>:
# å¿µã®ãŸã‚å…ƒã®:ã‚’ãƒãƒƒã‚¯ã‚¢ãƒƒãƒ—ã—ã¦ãŠã
Each :=:,/,? Each nnoremap,vnoremap <Leader>: :
# }}}

# ------------------------------------------------------
# terminalã¨ã‹ {{{
# `SH`ã§é–‹ã
if has('win32')
	command! Powershell :bo terminal ++close pwsh
	nnoremap SH <Cmd>Powershell<CR>
	nnoremap <S-F1> <Cmd>silent !start explorer %:p:h<CR>
else
	nnoremap SH <Cmd>bo terminal<CR>
endif
# `drop`ã‚³ãƒãƒ³ãƒ‰ã§terminalã‹ã‚‰vimã§é–‹ã
def g:Tapi_drop(bufnr: number, arglist: list<string>)
	vimrc#terminal#Tapi_drop(bufnr, arglist)
enddef
# ãã®ä»–ã®è¨­å®š
au vimrc TerminalOpen * ++once vimrc#terminal#ApplySettings()

# }}}

# ------------------------------------------------------
# diff {{{
set splitright
set fillchars+=diff:\ # å‰Šé™¤è¡Œã¯ç©ºç™½æ–‡å­—ã§åŸ‹ã‚ã‚‹
# diffãƒ¢ãƒ¼ãƒ‰ã‚’è‡ªå‹•ã§off https://hail2u.net/blog/software/vim-turn-off-diff-mode-automatically.html
au vimrc WinEnter * if (winnr('$') ==# 1) && !!getbufvar(winbufnr(0), '&diff') | diffoff | endif
# }}}

# ------------------------------------------------------
# æ—¥ä»˜é–¢ä¿‚ {{{
g:reformatdate_extend_names = [{
	a: ['æ—¥', 'æœˆ', 'ç«', 'æ°´', 'æœ¨', 'é‡‘', 'åœŸ'],
	A: ['æ—¥æ›œæ—¥', 'æœˆæ›œæ—¥', 'ç«æ›œæ—¥', 'æ°´æ›œæ—¥', 'æœ¨æ›œæ—¥', 'é‡‘æ›œæ—¥', 'åœŸæ›œæ—¥'],
}]
g:reformatdate_extend_formats = ['%m/%d(%a)']
reformatdate#init()
inoremap <expr> <F5> strftime('%Y/%m/%d')
cnoremap <expr> <F5> strftime('%Y%m%d')
nnoremap <F5> <ScriptCmd>reformatdate#reformat(localtime())<CR>
nnoremap <C-a> <ScriptCmd>reformatdate#inc(v:count)<CR>
nnoremap <C-x> <ScriptCmd>reformatdate#dec(v:count)<CR>
nnoremap <Space><F5> /\d\{4\}\/\d\d\/\d\d<CR>
# }}}

# ------------------------------------------------------
# ã‚¿ãƒ–å¹…ã‚„ã‚¿ãƒ–å±•é–‹ã‚’è‡ªå‹•è¨­å®š {{{
def SetupTabstop()
	setlocal shiftwidth=0
	setlocal softtabstop=0
	if &ft ==# 'help'
		return
	endif
	const limit = 100
	const org = getpos('.')
	cursor(1, 1)
	if !!search('^\t', 'nc', limit)
		setlocal noexpandtab
		setlocal tabstop=3 # æ„å¤–ã¨ã‚ã‚Šãªæ°—ãŒã—ã¦ããŸâ€¦
	elseif !!search('^  \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=2
	elseif !!search('^    \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=4
	endif
	setpos('.', org)
enddef
# filetype indent on ãŒçµ‚ã‚ã£ã¦ã‹ã‚‰åˆ¤å®šã™ã‚‹
def SetupTabstopLazy()
	au vimrc SafeState * ++once SetupTabstop()
enddef
# ft ==# ''ã§ã‚‚å®Ÿè¡Œã—ãŸã„ã®ã§FileTypeã§ã¯ãªãBufReadPost
au vimrc BufReadPost * SetupTabstopLazy()
SetupTabstopLazy()
# }}}

# ------------------------------------------------------
# <C-g>ã§æœ¬æ¥ã®æƒ…å ±ï¼‹ã‚¢ãƒ«ãƒ•ã‚¡ã‚’è¡¨ç¤º {{{
nnoremap <script> <C-g> <ScriptCmd>vimrc#myutil#ShowBufInfo()<CR><ScriptCmd>vimrc#myutil#PopupCursorPos()<CR>
xnoremap <C-g> <ScriptCmd>vimrc#myutil#PopupVisualLength()<CR>
# }}}

# ------------------------------------------------------
# ãƒ•ã‚¡ã‚¤ãƒ«åã‚’å‹æ‰‹ã«ã¤ã‘ã¦ä¿å­˜ {{{
def Sav()
	if !!bufname()
		update
		return
	endif
	const dt = strftime('%Y%m%d')
	var title = getline(1)
		->matchlist('^.\{0,10\}')[0]
		->substitute("[ \t\n*?[{`$\\%#'\"|!<>]", '_', 'g')
	var ext = &ft
	if getline(1) =~# '^vim9script\>.*'
		ext = 'vim'
		title = ''
	elseif &ft ==# 'markdown' || search('^ *- \[.\] ', 'cn')
		title = getline(1)
			->substitute('- \[.\]', '', 'g')
			->substitute('^[ -#]*', '', 'g')
		ext = 'md'
	elseif &ft ==# 'javascript'
		ext = 'js'
	elseif &ft ==# 'python'
		ext = 'py'
	elseif &ft ==# 'ruby'
		ext = 'rb'
	elseif &ft ==# 'typescript'
		ext = 'ts'
	elseif &ft ==# 'text' || &ft ==# 'help' || !&ft
		ext = 'txt'
	endif
	const fname = $'{dt}{!title ? '' : '_'}{title}.{ext}'
	const iname = input($"{getcwd()}\n:sav ", $'{fname}{repeat("\<Left>", len(ext) + 1)}')
	if !!iname
		execute 'sav' iname
	endif
enddef
command! Sav Sav()
# }}}

# ------------------------------------------------------
# é–‰ã˜ã‚‹ {{{
def g:QuitWin(expr: string) # TODO: minifyã™ã‚‹ã¨ã°ãã‚‹ï¼Ÿ
	if winnr() ==# winnr(expr)
		return
	endif
	execute 'wincmd' expr
	if mode() ==# 't'
		quit!
	else
		confirm quit
	endif
enddef
Each X=h,j,k,l nnoremap qX <ScriptCmd>g:QuitWin('X')<CR>
nnoremap q <Nop>
nnoremap Q q
# é–‰ã˜ã‚‹
nnoremap <expr> qq $"\<Cmd>confirm {winnr('$') ==# 1 && execute('ls')->split("\n")->len() !=# 1 ? 'bd' : 'q'}\<CR>"
# ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦
nnoremap qa <Cmd>confirm qa<CR>
nnoremap qOw <Cmd>confirm only<CR>
# ã‚¿ãƒ–
nnoremap qt <Cmd>confirm tabclose +<CR>
nnoremap qT <Cmd>confirm tabclose -<CR>
nnoremap q# <Cmd>confirm tabclose #<CR>
nnoremap qOt <Cmd>confirm tabonly<CR>
# ãƒãƒƒãƒ•ã‚¡
nnoremap qd <Cmd>confirm bd<CR>
nnoremap <expr> qo $"\<Cmd>vim9cmd confirm bd {range(1, last_buffer_nr())->filter((i, b) => b !=# bufnr() && buflisted(b))->join()}\<CR>"
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆå‹•ä½œã‚’ä¿æŒ
nnoremap q: q:
nnoremap q/ q/
nnoremap q? q?
# é–‹ããªãŠã™
nnoremap qQ <Cmd>e #<1<CR>
# }}}

# ------------------------------------------------------
# æ–°è¦ãƒãƒƒãƒ•ã‚¡ã‚’ä½•ã‚‚ã›ãšé–‰ã˜ãŸã‚‰ãƒãƒƒãƒ•ã‚¡ãƒªã‚¹ãƒˆã‹ã‚‰å‰Šé™¤ã™ã‚‹ {{{
au vimrc BufHidden * {
	const b = getbufinfo('%')[0]
	if !b.name && !b.changed
		# BufHiddenä¸­ã«è©²å½“ã®ãƒãƒƒãƒ•ã‚¡ã¯å‰Šé™¤ã§ããªã„ã®ã§timerã§ã‚„ã‚‹
		timer_start(0, (_) => execute($'silent! bdelete {b.bufnr}'))
	endif
}
# }}}

# ------------------------------------------------------
# vimrcã€pluginã€colorschemeä½œæˆç”¨ {{{
# ã‚«ãƒ¼ã‚½ãƒ«è¡Œã‚’å®Ÿè¡Œã™ã‚‹ã‚„ã¤
nnoremap g: <Cmd>.source<CR>
nnoremap g9 <Cmd>vim9cmd :.source<CR>
xnoremap g: :source<CR>
xnoremap g9 :vim9cmd source<CR>
# ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã®ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’ç¢ºèªã™ã‚‹ã‚„ã¤â†’<C-g>ã«ç§»å‹•
# nnoremap <expr> <Space>hl $'<Cmd>hi {synID(line('.'), col('.'), 1)->synIDattr('name')->substitute('^$', 'Normal', '')}<CR>'
# ä»–ã®å®šç¾©ã¯.vim/after/ftplugin/vim.vim
# }}}

# ------------------------------------------------------
# &autocompleteã¯ãã®ã¾ã¾ã ã¨è¡¨ç¤ºã—ãŸã ã‘ã§ãƒãƒƒãƒ•ã‚¡ã‚’æ›´æ–°ã—ã¦ã—ã¾ã† {{{
set noautocomplete
au vimrc InsertEnter * au TextChangedI * ++once set autocomplete
au vimrc InsertLeave * set noautocomplete
# }}}

# ------------------------------------------------------
# ãã®ä»–ç´°ã€…ã—ãŸã® {{{
if has('clipboard')
	au vimrc FocusGained * @" = @+
	au vimrc FocusLost   * @+ = @"
endif

au vimrc WinEnter * if winnr('$') ==# 1 && &buftype ==# 'quickfix' | q | endif

nnoremap <F10> <ScriptCmd>vimrc#tabpanel#Toggle()<CR>
nnoremap <F11> <Cmd>set number!<CR>
nnoremap <F12> <Cmd>set wrap!<CR>

nmap gs :<C-u>%s///g<Left><Left><Left>
nmap gS :<C-u><Cmd>call setcmdline($'%s/{expand('<cword>')->escape('^$.*?/\[]')}//g')<CR><Left><Left>
xmap gs :s///g<Left><Left><Left>
xnoremap <SID>(setup-region-to-search) <Cmd>let @/ = $'\V{getregion(getpos('v'), getpos('.'))->join("\n")->escape('\')->substitute("\n", '\n', 'g')}'<CR>
xmap gS <SID>(setup-region-to-search)<Esc>:<C-u><Cmd>call setcmdline($'%s/{@/}//g')<CR><Left><Left>
xmap * <SID>(setup-region-to-search)<Esc>/<CR>

nnoremap <CR> j0
nnoremap Y y$
nnoremap <Space>p $p
nnoremap <Space>P ^P
Each A,B=j,+,k,- nnoremap <expr> A ((getline('.')->match('\S') + 1 ==# col('.')) ? 'B' : 'A')

# `T`å¤šå°‘æ½°ã—ã¦ã‚‚å¤§ä¸ˆå¤«ã ã‚ã†â€¦
nnoremap TE :<C-u>tabe<Space>
nnoremap TN <Cmd>tabnew<CR>
nnoremap TD <Cmd>tabe ./<CR>
nnoremap TT <Cmd>tabnext #<CR>
nnoremap TB <Cmd>tabnew %<CR>g;

onoremap <expr> } $"\<Esc>m`0{v:count1}{v:operator}\}"
onoremap <expr> { $"\<Esc>m`V{v:count1}\{{v:operator}"

xnoremap <expr> h mode() ==# 'V' ? '<Esc>h' : 'h'
xnoremap <expr> l mode() ==# 'V' ? '<Esc>l' : 'l'
xnoremap J j
xnoremap K k
xnoremap p P
xnoremap P p

inoremap ï½‹ï½Š <Esc>`^
inoremap ã€Œ ã€Œã€<C-g>U<Left>
inoremap ã€Œã€ ã€Œã€<C-g>U<Left>
inoremap ï¼ˆ ()<C-g>U<Left>
inoremap ï¼ˆï¼‰ ()<C-g>U<Left>

# USé…åˆ—â†’ã€Œ"ã€æŠ¼ã—ã«ãã„ã€JISé…åˆ—â†’ã€Œ'ã€æŠ¼ã—ã«ãã„
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®Mã¯ã‚ã¾ã‚Šä½¿ã‚ãªã„ã‹ãªãâ€¦
nnoremap ' "
nnoremap m '
nnoremap M m

# f,F,t,Tã®æ™‚ã ã‘ã‚»ãƒŸã‚³ãƒ­ãƒ³ã¨ã‚«ãƒ³ãƒã‚’å¾©æ´»ã•ã›ã‚‹ {{{
def UnmapSemi(c: string): string
	nnoremap <nowait> <expr> ; UnmapSemi(';')
	nnoremap <nowait> <expr> , UnmapSemi(',')
	augroup unmap-semi
		au! CursorMoved * ++once au unmap-semi CursorMoved * ++once unmap ; | unmap ,
	augroup END
	return c
enddef
Each X=f,F,t,T nnoremap <expr> X UnmapSemi('X')
# }}}

# yiwã¨ã‹ã—ãŸã¨ãã«ã‚«ãƒ¼ã‚½ãƒ«ã‚’ç§»å‹•ã•ã›ãŸããªã„ãŒã€ã‚„ã‚Šã‹ãŸãŒå¾®å¦™â€¦
g:preOpCurpos = getcurpos()
def OpWithKeepCurpos(expr: string)
	g:preOpCurpos = getcurpos()
	au vimrc SafeState * ++once setpos('.', g:preOpCurpos)
	feedkeys(expr, 'n')
enddef
Each key=y,= nnoremap key <ScriptCmd>OpWithKeepCurpos('key')<CR>

# æ¥µã€…å€‹äººçš„ã«å¤šã„æ“ä½œ
nnoremap <Space>r :!<Up>

# typoã®é‡ãŒã‚„ã°ã™ãã‚‹ã®ã§é‡ãã¦ã‚‚ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§ã‚ªãƒ³ã«ã™ã‚‹(æˆ’ã‚)
set spell spelllang=en_us,cjk
nnoremap <F8> <Cmd>set spell! spell?<CR>

# <C-]>ã«å¯¾ã—ã¦<C-[>â†’ESCã«ãªã£ã¡ã‚ƒã†ã®ã§NGâ†’ã‚ã‹ã‚‹
# <C-t>ã«å¯¾ã—ã¦<C-]>â†’ã‚ã‹ã‚‰ãªã„
nnoremap ]t <C-]>
nnoremap [t <C-t>
# }}}

# ------------------------------------------------------
# æ§˜å­è¦‹ä¸­ {{{
# syntaxå›ºæœ‰ã®è¿½åŠ å¼·èª¿ {{{
def ClearMySyntax()
	for id in get(w:, 'my_syntax', [])
		silent! matchdelete(id)
	endfor
	w:my_syntax = []
enddef
def AddMySyntax(group: string, pattern: string)
	w:my_syntax->add(matchadd(group, pattern))
enddef
au vimrc Syntax * ClearMySyntax()
# ã‚„ã‚ŠãŒã¡ãªãƒŸã‚¹ã«ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’ä»˜ã‘ã‚‹
au vimrc Syntax javascript {
	AddMySyntax('SpellRare', '\s[=!]=\s')
}
au vimrc Syntax vim {
	AddMySyntax('SpellRare', '\s[=!]=\s')
	AddMySyntax('SpellBad', '\s[=!]==\s')
	AddMySyntax('SpellBad', '\s\~[=!][=#]\?\s')
	AddMySyntax('SpellRare', '\<normal!\@!')
}
# }}}

# `:%g!/re/d` ã®çµæœã‚’æ–°è¦ã‚¦ã‚¤ãƒ³ãƒ‰ã‚¦ã«è¡¨ç¤º {{{
# (Buffer Regular Expression Print)
command! -nargs=1 Brep vimrc#myutil#Brep(<q-args>, <q-mods>)
# }}}

# CSVã¨ã‹ã®ãƒ˜ãƒƒãƒ€ã‚’å›ºå®šè¡¨ç¤ºã™ã‚‹ã€‚ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ã˜ã‚ƒãªãã¦ã‚³ãƒãƒ³ãƒ‰å®šç¾©ã™ã‚Œã°ã„ã„ã‹ãªâ€¦
nnoremap <silent> <F9> <ESC>1<C-w>s:1<CR><C-w>w
xnoremap <F9> <ESC>1<C-w>s<C-w>w

# README.mdã‚’é–‹ã
command! -nargs=1 -complete=packadd HelpPlugins vimrc#myutil#HelpPlugins(<q-args>)

# multi line statusline {{{
def GetDiffLocStr(): string
	if !exists('w:diffloc')
		return ''
	endif
	var ln = line('.')
	var idx = w:diffloc->indexof((_, v) => v[0] <= ln && ln <= v[1]) + 1
	return $'{!idx ? '-' : idx}/{len(w:diffloc)}'
enddef

def ClearDiffLoc()
	silent! unlet w:difflines
enddef

au vimrc WinEnter,TextChanged,InsertLeave,BufWritePost * ClearDiffLoc()

def g:MyStatusLine(): string
	var stl = '%f'
	if &diff
		if !exists('w:difflines')
			w:diffloc = []
			var start = 0
			var name_bk = ''
			var added = 0
			var changed = 0
			for lnum in range(1, line('$'))
				const name = diff_hlID(lnum, 1)->synIDattr('name')
				if name ==# 'DiffAdd'
					added += 1
				elseif name ==# 'DiffChange'
					changed += 1
				endif
				if name_bk ==# name
					continue
				endif
				name_bk = name
				if !!start
					w:diffloc->add([start, lnum - 1])
				endif
				start = name ==# 'DiffAdd' || name ==# 'DiffChange' ? lnum : 0
			endfor
			if !!start
				w:diffloc->add([start, line('$')])
			endif
			w:difflines = $'Added:{added},Changed:{changed}'
			w:difflocstr = GetDiffLocStr()
		endif
		stl = $'{w:difflines}%={w:difflocstr}%@{stl}'
		au vimrc CursorMoved * w:difflocstr = GetDiffLocStr()
	endif
	return stl
enddef
def ToggleZen()
	if zenmode#Toggle()
		# statuslineè¡¨ç¤ºãªã—
		return
	elseif !exists('g:has_mulitilinestatusline') # â†.vimrc_localã§è¨­å®š
		# multi line statuslineè¡¨ç¤ºãªã—
		return
	else
		# statuslineè¡¨ç¤ºã‚ã‚Š
		set stlo=maxheight:2
		set stl=%{%g:MyStatusLine()%}
	endif
enddef
noremap ZZ <ScriptCmd>ToggleZen()<CR>
au vimrc WinResized * redrawstatus
# }}}
# }}}

# ------------------------------------------------------
# â€  ã‚ã¨ã‚‚ã†1å›ã€Œã“ã‚Œä½¿ã£ã¦ãªã„ãªâ€¦ã€ã¨æ€ã£ãŸã¨ãã«æ¶ˆã™ {{{
# ã©ã£ã¡ã‚‚<C-w>wã€‚å·¦æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã¨å³æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã®ãƒãƒƒãƒ”ãƒ³ã‚°
nnoremap <Space>w <C-w>w
nnoremap <Space>o <C-w>w
nnoremap <Space>d "_d

# <Tab>ã§tsvã¨ã‹csvã¨ã‹htmlã®æ¬¡ã®é …ç›®ã«ç§»å‹•
au vimrc FileType tsv,csv {
	nnoremap <buffer> <nowait> <Tab> <Cmd>call search('\(^\\|\t\\|, *\)\S\?', 'e')<CR>
	nnoremap <buffer> <nowait> <S-Tab> <Cmd>call search('\(^\\|\t\\|, *\)\S\?', 'be')<CR>
}

# ã»ã¨ã‚“ã©ä½¿ã‚ãªã„
nnoremap qn <Cmd>bn<CR><Cmd>confirm bd<CR>
nnoremap qp <Cmd>bp<CR><Cmd>confirm bd<CR>
# }}}

# ------------------------------------------------------
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆãƒãƒƒãƒ”ãƒ³ã‚°ãƒ‡ãƒ¼ {{{
if strftime('%d') ==# '01'
	au vimrc VimEnter * {
		echow "âœ¨ Today, Let's enjoy the default key mapping ! âœ¨"
		mapclear
		imapclear
		xmapclear
		cmapclear
		omapclear
		tmapclear
	}
endif
# }}}

# ------------------------------------------------------
# ãƒ¡ãƒ¢ {{{
# <F1> ã‚«ãƒ¬ãƒ³ãƒˆãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒª
# <F2> MRU
# <F3> BufferList
# <F4> TabList
# <F5> æ—¥ä»˜é–¢ä¿‚
# <F6>
# <F7> ã“ã“ã¾ã§ã‚ˆã‚“ã 
# <F8> Spell check
# <F9> ãƒ˜ãƒƒãƒ€è¡Œã‚’è¡¨ç¤º(ã‚ã‚“ã¾ã‚Šä½¿ã‚ãªã„)
# <F10> ã‚¿ãƒ–ãƒ‘ãƒãƒ«
# <F11> è¡Œç•ªå·è¡¨ç¤ºåˆ‡æ›¿
# <F12> æŠ˜ã‚Šè¿”ã—è¡¨ç¤ºåˆ‡æ›¿
# }}}

export def LazyLoad()
	# nop
enddef
