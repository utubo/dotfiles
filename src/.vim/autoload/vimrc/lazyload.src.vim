vim9script

# ------------------------------------------------------
# ãƒ¦ãƒ¼ãƒ†ã‚£ãƒªãƒ†ã‚£ {{{

# ã“ã‚“ãªæ„Ÿã˜
#   Each nmap,xmap j gj
#   â†’ nmap j gj | xmap j gj
# å…ˆé ­ä»¥å¤–ã«å·®ã—è¾¼ã‚“ã ã‚Šãƒã‚¹ãƒˆã—ãŸã„å ´åˆã¯ã“ã†
#   Each j,k Each nmap,xmap {1} {0} g{0}
#   â†’ nmap j gj | xmap j gj | nmap k gk | xmap k gk
# â€»ã“ã‚Œä½¿ã†ã‚ˆã‚Šã¹ãŸã§æ›¸ã„ãŸã»ã†ãŒèµ·å‹•ã¯é€Ÿã„
g:util_each_nest = 0
def! g:UtilEach(qargs: string)
	const [items, args] = qargs->split('^\S*\zs')
	g:util_each_nest += 1
	for i in items->split(',')
		var a = args->substitute('{0\?}', i, 'g')
		if a ==# args
			a = $'{i} {a}'
		endif
		execute a->substitute($"\{{g:util_each_nest}\}", '{}', 'g')
	endfor
	g:util_each_nest -= 1
enddef
command! -keepscript -nargs=* Each g:UtilEach(<q-args>)

# ãã®ä»–
command! -nargs=1 -complete=var Enable  <args> = 1
command! -nargs=1 -complete=var Disable <args> = 0

def g:IndentStr(expr: any): string
	return matchstr(getline(expr), '^\s*')
enddef

def g:StayCurPos(expr: string)
	const len = getline('.')->len()
	var cur = getcurpos()
	execute expr
	cur[2] += getline('.')->len() - len
	setpos('.', cur)
enddef

# <Cmd>ã§defã‚’å®Ÿè¡Œã—ãŸã¨ãã®ãƒ“ã‚¸ãƒ¥ã‚¢ãƒ«ãƒ¢ãƒ¼ãƒ‰ã®ç¯„å›²(è¡Œ)
def! g:VFirstLast(): list<number>
	return [line('.'), line('v')]->sort('n')
enddef

def! g:VRange(): list<number>
	const a = g:VFirstLast()
	return range(a[0], a[1])
enddef
#}}} -------------------------------------------------------

# ------------------------------------------------------
# ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ {{{

# ã“ã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆå†…ã§å¿…è¦ã¨ãªã‚‹ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ {{{
packadd lsp
packadd vim-notification
packadd vim-reformatdate
packadd vim-textobj-user
# }}}

# zenmode {{{
au vimrc User Vim9skkModeChanged zenmode#Invalidate()
#}}}

# vim9skk {{{
g:vim9skk = {
	keymap: {
		toggle: ['<C-j>', ';j'],
		midasi: [':', 'Q'],
	},
	mode_label_timeout: 500,
}
g:vim9skk_mode = '' # statuslineã§ã‚¨ãƒ©ãƒ¼ã«ãªã‚‰ãªã„ã‚ˆã†ã«å¿µã®ç‚ºè¨­å®šã—ã¦ãŠã
nnoremap ;j i<Plug>(vim9skk-enable)
# ã‚¤ãƒ³ã‚µãƒ¼ãƒˆãƒ¢ãƒ¼ãƒ‰ãŒçµ‚ã‚ã£ãŸã‚‰ã‚ªãƒ•ã«ã™ã‚‹
au vimrc ModeChanged [ic]:* au SafeState * ++once vim9skk#Disable()
# è¦‹å‡ºã—ãƒ¢ãƒ¼ãƒ‰ã§ã‚¹ã‚¿ãƒ¼ãƒˆã™ã‚‹
au vimrc User Vim9skkEnter feedkeys('Q')
# AZIKãƒ©ã‚¤ã‚¯ãªè¨­å®šã¨ã‹
au vimrc User Vim9skkInitPre vimrc#vim9skk#ApplySettings()
#}}}

# textobj-user {{{
Each onoremap,xnoremap ab <Plug>(textobj-multiblock-a)
Each onoremap,xnoremap ib <Plug>(textobj-multiblock-i)
g:textobj_multiblock_blocks = [
	[ "(", ")" ],
	[ "[", "]" ],
	[ "{", "}" ],
	[ '<', '>' ],
	[ '"', '"', 1 ],
	[ "'", "'", 1 ],
	[ ">", "<", 1 ],
	[ "ã€Œ", "ã€", 1 ],
]
call textobj#user#plugin('nonwhitespace', {
	'-': { 'pattern': '\S\+', 'select': ['a<Space>', 'i<Space>'], }
})
#}}}

# Git {{{
command! -nargs=* GitAdd vimrc#git#Add(<q-args>)
command! -nargs=1 -complete=customlist,vimrc#git#ConventionalCommits GitCommit vimrc#git#Commit(<q-args>)
command! -nargs=1 GitTagPush vimrc#git#TagPush(<q-args>)
nnoremap <Space>ga <Cmd>GitAdd -A<CR>
nnoremap <Space>gA :<C-u>Git add %
nnoremap <Space>gc :<C-u>GitCommit<Space><Tab>
nnoremap <Space>gp :<C-u>Git push<End>
nnoremap <Space>gs <Cmd>Git status -sb<CR>
nnoremap <Space>gv <Cmd>Gvdiffsplit<CR>
nnoremap <Space>gd <Cmd>Gdiffsplit<CR>
nnoremap <Space>gl <Cmd>Git pull<CR>
nnoremap <Space>gt :<C-u>GitTagPush<Space>
nnoremap <Space>gC :<C-u>Git checkout %
#}}}

# gh {{{
# ftpluginã«ã™ã‚‹ã¨å®šç¾©ãŒãƒãƒ©ãƒãƒ©ã«ãªã£ã¦è¦‹é€šã—æ‚ªããªã‚‹ã‹ãª
au vimrc FileType gh-repos {
	nnoremap <buffer> i <ScriptCmd>execute 'edit!' ['gh:/', getline('.')->matchstr('\S\+'), 'issues']->join('/')<CR>
}
au vimrc FileType gh-issues {
	nnoremap <buffer> <CR> <ScriptCmd>execute 'new' [expand('%'), getline('.')->matchstr('[0-9]\+'), 'comments']->join('/')<CR>
	nnoremap <buffer> r <ScriptCmd>execute 'edit!' expand('%:h:h') .. '/repos'<CR>
}
au vimrc FileType gh-issue-comments {
	nnoremap <buffer> <CR> <ScriptCmd>execute 'bo vsplit' [expand('%'), getline('.')->matchstr('[0-9]\+')]->join('/')<CR><Cmd>setlocal wrap<CR>
}
nnoremap <Space>gh <Cmd>tabe gh://utubo/repos<CR>
# }}}

# MRU {{{
nnoremap <F2> <Cmd>MRUToggle<CR>
g:MRU_Exclude_Files = has('win32') ? $'{$TEMP}\\.*' : '^/tmp/.*\|^/var/tmp/.*'
# MRUã«é–¢ã—ã¦ã®ãã®ä»–ã®è¨­å®šã¯.vim/after/ftplugin/mru.src.vimã§æŒ‡å®šã—ã¦ã„ã‚‹
#}}}

# Portal {{{
nnoremap <Leader>a <Cmd>PortalAim<CR>
nnoremap <Leader>b <Cmd>PortalAim blue<CR>
nnoremap <Leader>o <Cmd>PortalAim orange<CR>
nnoremap <Leader>r <Cmd>PortalReset<CR>
#}}}

# è£œå®Œ {{{
export def SkipParen(): string
	const c = matchstr(getline('.'), '.', col('.') - 1)
	# é–‰ã˜æ‹¬å¼§ã®é–“ã«TABæ–‡å­—ã‚’å…¥ã‚Œã‚‹ã“ã¨ã¯ãªã„ã ã‚ã†â€¦
	if !c || stridx(')]}>\''`ã€', c) ==# -1
		return "\<Tab>"
	else
		return  "\<C-o>a"
	endif
enddef
Each imap,smap <expr> <Tab> vsnip#jumpable(1) ? '<Plug>(vsnip-jump-next)' : pumvisible() ? '<C-n>' : vimrc#lazyload#SkipParen()
Each imap,smap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)' : pumvisible() ? '<C-p>' : '<S-Tab>'
# Copilotã¯æ§˜å­è¦‹
#g:copilot_no_tab_map = true
#imap <silent> <script> <expr> ;c copilot#Accept("\<CR>")
#au vimrc VimEnter * Copilot disable
#}}}

# ğŸ¶ğŸš {{{
g:skipslash_autocomplete = 1
nnoremap <Space>t <ScriptCmd>tabpopupmenu#popup()<CR>
nnoremap <Space>T <ScriptCmd>tablist#Show()<CR>
Each nnoremap,tnoremap <silent> <C-w><C-s> <Plug>(shrink-height)<C-w>w
Each nnoremap,tnoremap <silent> <C-w><C-h> <Plug>(shrink-width)<C-w>w
noremap <Space>s <Plug>(jumpcursor-jump)
# }}}

# è¨­å®šãŒè†¨ã‚‰ã‚“ã§ããŸã®ã§åˆ¥ãƒ•ã‚¡ã‚¤ãƒ«ã§å®šç¾© {{{
vimrc#lexima#LazyLoad()
vimrc#lsp#LazyLoad()
# }}}

# é…å»¶èª­ã¿è¾¼ã¿ã‚‚ã® {{{
Each nmap,xmap S <ScriptCmd>vimrc#sandwich#LazyLoad('S')<CR>
nmap <C-p> <ScriptCmd>vimrc#ctrlp#LazyLoad()<CR><C-p>
nmap s <ScriptCmd>vimrc#easymotion#LazyLoad()<CR>s
command! -nargs=* Fern vimrc#fern#LazyLoad(<q-args>)
nnoremap <F1> <Cmd>Fern . -reveal=% -opener=edit<CR>
# }}}

# ãã®ä»– {{{
Enable g:rainbow_active
g:auto_cursorline_wait_ms = &updatetime
Each w,b,e,ge nnoremap {0} <Plug>(smartword-{0})
nnoremap [c <Plug>(GitGutterPrevHunk)
nnoremap ]c <Plug>(GitGutterNextHunk)
Each nnoremap,xnoremap <Space>c <Plug>(caw:hatpos:toggle)
#}}}

# é–‹ç™ºç”¨ {{{
g:vimhelpgenerator_version = ''
g:vimhelpgenerator_author = 'Author  : utubo'
g:vimhelpgenerator_defaultlanguage = 'en'
g:vimhelpgenerator_uri = 'https://github.com/utubo/'
#}}}
#}}} -------------------------------------------------------

# ------------------------------------------------------
# ã‚³ãƒ”ãƒšå¯„ã›é›†ã‚è‰²ã€… {{{
au vimrc InsertLeave * set nopaste
au vimrc BufReadPost *.log* normal! G
xnoremap * "vy/\V<C-r>=substitute(escape(@v,'\/'),"\n",'\\n','g')<CR><CR>
# https://github.com/astrorobot110/myvimrc/blob/master/vimrc
set matchpairs+=ï¼ˆ:ï¼‰,ã€Œ:ã€,ã€:ã€,ã€:ã€‘,ï¼»:ï¼½,ï¼œ:ï¼
# https://github.com/Omochice/dotfiles
Each i,a,A nnoremap <expr> {0} !empty(getline('.')) ? '{0}' : '"_cc'
# ã™ã”ã„
# https://zenn.dev/mattn/articles/83c2d4c7645faa
Each +,-,>,< Each nmap,tmap <C-w>{0} <C-w>{0}<SID>ws
Each +,-,>,< Each nnoremap,tnoremap <script> <SID>ws{0} <C-w>{0}<SID>ws
Each nmap,tmap <SID>ws <Nop>
# æ„Ÿè¬
# https://zenn.dev/vim_jp/articles/43d021f461f3a4
nnoremap <A-J> <Cmd>copy.<CR>
nnoremap <A-K> <Cmd>copy-1<CR>
xnoremap <A-J> :copy'<-1<CR>gv
xnoremap <A-K> :copy'>+0<CR>gv
# https://zenn.dev/vim_jp/articles/2024-10-07-vim-insert-uppercase
def ToupperPrevWord(): string
  const col = getpos('.')[2]
  const substring = getline('.')[0 : col - 1]
  const word = matchstr(substring, '\v<(\k(<)@!)*$')
  return toupper(word)
enddef
inoremap <expr> ;l $"<C-w>{ToupperPrevWord()}"

#}}} -------------------------------------------------------

# ------------------------------------------------------
# vimgrep {{{
command! -nargs=+ -complete=dir VimGrep vimrc#myutil#VimGrep(<f-args>)
au vimrc WinEnter * if winnr('$') ==# 1 && &buftype ==# 'quickfix' | q | endif
#}}} -------------------------------------------------------

# ------------------------------------------------------
# diff {{{
set splitright
set fillchars+=diff:\ # å‰Šé™¤è¡Œã¯ç©ºç™½æ–‡å­—ã§åŸ‹ã‚ã‚‹
# diffãƒ¢ãƒ¼ãƒ‰ã‚’è‡ªå‹•ã§off https://hail2u.net/blog/software/vim-turn-off-diff-mode-automatically.html
au vimrc WinEnter * if (winnr('$') ==# 1) && !!getbufvar(winbufnr(0), '&diff') | diffoff | endif
#}}} -------------------------------------------------------

# ------------------------------------------------------
# æ—¥ä»˜é–¢ä¿‚ {{{
g:reformatdate_extend_names = [{
	a: ['æ—¥', 'æœˆ', 'ç«', 'æ°´', 'æœ¨', 'é‡‘', 'åœŸ'],
	A: ['æ—¥æ›œæ—¥', 'æœˆæ›œæ—¥', 'ç«æ›œæ—¥', 'æ°´æ›œæ—¥', 'æœ¨æ›œæ—¥', 'é‡‘æ›œæ—¥', 'åœŸæ›œæ—¥'],
}]
g:reformatdate_extend_formats = ['%m/%d(%a)']
reformatdate#init()
inoremap <expr> <F5> strftime('%Y/%m/%d')
cnoremap <expr> <F5> strftime('%Y%m%d')
nnoremap <F5> <ScriptCmd>reformatdate#reformat(localtime())<CR>
nnoremap <C-a> <ScriptCmd>reformatdate#inc(v:count)<CR>
nnoremap <C-x> <ScriptCmd>reformatdate#dec(v:count)<CR>
nnoremap <Space><F5> /\d\{4\}\/\d\d\/\d\d<CR>
#}}} -------------------------------------------------------

# ------------------------------------------------------
# ã‚¿ãƒ–å¹…ã‚„ã‚¿ãƒ–å±•é–‹ã‚’è‡ªå‹•è¨­å®š {{{
def SetupTabstop()
	if &ft ==# 'help'
		return
	endif
	const limit = 100
	const org = getpos('.')
	cursor(1, 1)
	if !!search('^\t', 'nc', limit)
		setlocal noexpandtab
		setlocal tabstop=3 # æ„å¤–ã¨ã‚ã‚Šãªæ°—ãŒã—ã¦ããŸâ€¦
	elseif !!search('^  \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=2
	elseif !!search('^    \S', 'nc', limit)
		setlocal expandtab
		setlocal tabstop=4
	endif
	&shiftwidth = &tabstop
	&softtabstop = &tabstop
	setpos('.', org)
enddef
# filetype indent on ãŒçµ‚ã‚ã£ã¦ã‹ã‚‰åˆ¤å®šã™ã‚‹
def SetupTabstopLazy()
	au vimrc SafeState * ++once SetupTabstop()
enddef
# ft ==# ''ã§ã‚‚å®Ÿè¡Œã—ãŸã„ã®ã§FileTypeã§ã¯ãªãBufReadPost
au vimrc BufReadPost * SetupTabstopLazy()
SetupTabstopLazy()
#}}} -------------------------------------------------------

# ------------------------------------------------------
# ãƒãƒƒãƒ•ã‚¡æ“ä½œ {{{
nnoremap gn <Cmd>bnext<CR>
nnoremap gp <Cmd>bprevious<CR>
g:recentBufnr = 0
au vimrc BufLeave * g:recentBufnr = bufnr()
nnoremap <expr> gr $"\<Cmd>b{g:recentBufnr}\<CR>"

# bufã®ä¸€è¦§ã‚’ç”»é¢ä¸‹éƒ¨ã«è¡¨ç¤ºã™ã‚‹
vimrc#echobuflist#Setup()
au vimrc User EchoBufListShow g:zenmode.preventEcho = true
au vimrc User EchoBufListHide g:zenmode.preventEcho = false|zenmode#RedrawNow()
#}}}

# ------------------------------------------------------
# ãƒãƒƒãƒ•ã‚¡ã®æƒ…å ±ã‚’è‰²ä»˜ãã§è¡¨ç¤º {{{
def ShowBufInfo(event: string = '')
	if &ft ==# 'qf'
		return
	endif

	var isReadPost = event ==# 'BufReadPost'
	if isReadPost && !filereadable(expand('%'))
		# ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã¨ã‹ãŒä¸€æ™‚çš„ãªbufnameã‚’ä»˜ä¸ã—ã¦é–‹ã„ãŸå ´åˆã¯ç„¡è¦–ã™ã‚‹
		return
	endif

	const ruler = $' {line(".")}:{col(".")}'

	var msg = []
	add(msg, ['Title', $'"{bufname()}"'])
	add(msg, ['Normal', ' '])
	if &modified
		add(msg, ['Delimiter', '[+]'])
		add(msg, ['Normal', ' '])
	endif
	if !isReadPost && !filereadable(expand('%'))
		add(msg, ['Tag', '[New]'])
		add(msg, ['Normal', ' '])
	endif
	if &readonly
		add(msg, ['WarningMsg', '[RO]'])
		add(msg, ['Normal', ' '])
	endif
	const w = wordcount()
	if isReadPost || w.bytes !=# 0
		add(msg, ['Constant', printf('%dL, %dB', w.bytes ==# 0 ? 0 : line('$'), w.bytes)])
		add(msg, ['Normal', ' '])
	endif
	add(msg, [&ff ==# 'unix' ? 'MoreMsg' : 'WarningMsg', &ff])
	add(msg, ['Normal', ' '])
	const enc = empty(&fenc) ? &encoding : &fenc
	add(msg, [enc ==# 'utf-8' ? 'MoreMsg' : 'WarningMsg', enc])
	add(msg, ['Normal', ' '])
	add(msg, ['MoreMsg', &ft])
	var msglen = 0
	const maxlen = &columns - len(ruler) - 2
	for i in reverse(range(0, len(msg) - 1))
		var s = msg[i][1]
		var d = strdisplaywidth(s)
		msglen += d
		if maxlen < msglen
			const l = maxlen - msglen + d
			while !empty(s) && l < strdisplaywidth(s)
				s = s[1 :]
			endwhile
			msg[i][1] = s
			msg = msg[i : ]
			insert(msg, ['SpecialKey', '<'], 0)
			break
		endif
	endfor
	add(msg, ['Normal', repeat(' ', maxlen - msglen) .. ruler])
	redraw
	echo ''
	for m in msg
		execute 'echohl' m[0]
		echon m[1]
	endfor
	echohl Normal
enddef

nnoremap <script> <C-g> <ScriptCmd>ShowBufInfo()<CR>
#}}} -------------------------------------------------------

# ------------------------------------------------------
# Tabline {{{
set tabline=%!vimrc#tabline#MyTabline()
#}}}

# ------------------------------------------------------
# ã‚¹ãƒãƒ›ç”¨ {{{
# - ã‚­ãƒ¼ãŒå°ã•ã„ã®ã§æŠ¼ã—ã«ãã„ã‚‚ã®ã¯Spaceã¸ãƒãƒƒãƒ”ãƒ³ã‚°
# - ã‚¹ãƒãƒ›ã§ã®ã‚³ãƒ¼ãƒ‡ã‚£ãƒ³ã‚°ã¯åŸºæœ¬çš„ã«ãƒã‚°å–ã‚Š
# ã‚¹ã‚¿ãƒƒã‚¯ãƒˆãƒ¬ãƒ¼ã‚¹ã‹ã‚‰yankã—ã¦ã‚½ãƒ¼ã‚¹ã®è©²å½“ç®‡æ‰€ã‚’æ¢ã™ã®ã‚’è£œåŠ©
nnoremap <Space>e G?\cErr\\|Exception<CR>
nnoremap <expr> <Space>f $'{(getreg('"') =~ '^\d\+$' ? ':' : '/')}{getreg('"')}<CR>'
# ã‚¹ãƒãƒ›ã ã¨:ã¨/ã¨ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ãŒé ã„ã®ã§â€¦
nmap <Space>. :
nmap <Space>, /
nmap g<Space> g;
for i in range(1, 10)
	execute $'nmap <Space>{i % 10} <F{i}>'
endfor
nmap <Space><Space>1 <F11>
nmap <Space><Space>2 <F12>
# ãã®ä»–
nnoremap <Space>a A
nnoremap <Space>h ^
nnoremap <Space>l $
nnoremap <Space>y yiw
#}}} -------------------------------------------------------

# ------------------------------------------------------
# ãƒ•ã‚¡ã‚¤ãƒ«åã‚’å‹æ‰‹ã«ã¤ã‘ã¦ä¿å­˜ {{{
def AutoNamingAndSave()
	if !!bufname()
		update
		return
	endif
	const dt = strftime('%Y%m%d')
	var title = getline(1)
		->matchlist('^.\{0,10\}')[0]
		->substitute("[ \t\n*?[{`$\\%#'\"|!<]", '_', 'g')
	var ext = &ft
	if getline(1) =~# '^vim9script\>.*'
		ext = 'vim'
		title = ''
	elseif &ft ==# 'markdown' || search('^ *- \[.\] ', 'cn')
		title = getline(1)
			->substitute('- \[.\]', '', 'g')
			->substitute('^[ -#]*', '', 'g')
		ext = 'md'
	elseif &ft ==# 'javascript'
		ext = 'js'
	elseif &ft ==# 'python'
		ext = 'py'
	elseif &ft ==# 'ruby'
		ext = 'rb'
	elseif &ft ==# 'typescript'
		ext = 'ts'
	elseif &ft ==# 'text' || &ft ==# 'help' || !&ft
		ext = 'txt'
	endif
	const fname = $'{dt}{!title ? '' : '_'}{title}.{ext}'
	const iname = input($"{getcwd()}\n:sav ", $'{fname}{repeat("\<Left>", len(ext) + 1)}')
	if !!iname
		execute 'sav' iname
	endif
enddef
command! AutoNamingAndSave AutoNamingAndSave()
# }}}

# ------------------------------------------------------
# ã‚»ãƒŸã‚³ãƒ­ãƒ³ {{{
# ã‚¤ãƒ³ã‚µãƒ¼ãƒˆãƒ¢ãƒ¼ãƒ‰ã§ã‚‚ä½¿ã†ãƒ—ãƒ¬ãƒ•ã‚£ãƒƒã‚¯ã‚¹
# æ°—ã¥ã„ãŸã‚‰ã‚³ãƒ­ãƒ³ã‚’ã‚»ãƒŸã‚³ãƒ­ãƒ³ã«ãƒãƒƒãƒ”ãƒ³ã‚°ã—ã¦ãªã‹ã£ãŸâ€¦
# ;nã§æ±ºå®šã€;mã§ã‚­ãƒ£ãƒ³ã‚»ãƒ«
cnoremap ;n <CR>
Each nnoremap,inoremap ;n <Esc><Cmd>AutoNamingAndSave<CR>
inoremap ;m <Esc>`^
cnoremap ;m <C-c>
noremap  ;m <Esc>
# ãã®ä»–
inoremap ;v ;<CR>
inoremap ;w <C-o>e<C-o>a
inoremap ;k ã€Œã€<C-g>U<Left>
inoremap ;u <Esc>u
nnoremap ;r "
nnoremap ;rr "0p
Each nnoremap,inoremap ;<Tab> <ScriptCmd>g:StayCurPos('normal! >>')<CR>
Each nnoremap,inoremap ;<S-Tab> <ScriptCmd>g:StayCurPos('normal! <<')<CR>
nnoremap <Space>; ;
# `;h`+`h`é€£æ‰“ã§<BS>
map! <script> <SID>bs_ <Nop>
map! <script> ;h <SID>bs_h
noremap! <script> <SID>bs_h <BS><SID>bs_
# }}}

# ------------------------------------------------------
# ãƒ“ã‚¸ãƒ¥ã‚¢ãƒ«ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
xnoremap u <ScriptCmd>undo\|normal! gv<CR>
xnoremap <C-R> <ScriptCmd>redo\|normal! gv<CR>
xnoremap <Tab> <ScriptCmd>g:StayCurPos('normal! >gv')<CR>
xnoremap <S-Tab> <ScriptCmd>g:StayCurPos('normal! <gv')<CR>
const vmode = ['v', 'V', "\<C-v>", "\<ESC>"] # minviml:fixed=vmode
xnoremap <script> <expr> v vmode[vmode->index(mode()) + 1]
#}}}

# ------------------------------------------------------
# ã‚³ãƒãƒ³ãƒ‰ãƒ¢ãƒ¼ãƒ‰ã‚ã‚Œã“ã‚Œ {{{
Each nnoremap,xnoremap / <Cmd>noh<CR>/
Each nnoremap,xnoremap ? <Cmd>noh<CR>?
# è€ƒãˆä¸­
Each nnoremap,xnoremap ;c :
Each nnoremap,xnoremap ;s <Cmd>noh<CR>/
Each nnoremap,xnoremap + :
Each nnoremap,xnoremap , :
Each nnoremap,xnoremap <Space><Space>, ,
# ãã®ä»–ã®è¨­å®š
au vimrc CmdlineEnter * ++once vimrc#cmdline#ApplySettings()
#}}}

# ------------------------------------------------------
# terminalã¨ã‹ {{{
# `SH`ã§é–‹ã
if has('win32')
	command! Powershell :bo terminal ++close pwsh
	nnoremap SH <Cmd>Powershell<CR>
	nnoremap <S-F1> <Cmd>silent !start explorer %:p:h<CR>
else
	nnoremap SH <Cmd>bo terminal<CR>
endif
# `drop`ã‚³ãƒãƒ³ãƒ‰ã§terminalã‹ã‚‰vimã§é–‹ã
def g:Tapi_drop(bufnr: number, arglist: list<string>)
	 vimrc#terminal#Tapi_drop(bufnr, arglist)
enddef
# ãã®ä»–ã®è¨­å®š
au vimrc TerminalOpen * ++once vimrc#terminal#ApplySettings()

#}}} -------------------------------------------------------

# ------------------------------------------------------
# é–‰ã˜ã‚‹ {{{
def g:QuitWin(expr: string) # TODO: minifyã™ã‚‹ã¨ã°ãã‚‹ï¼Ÿ
	if winnr() ==# winnr(expr)
		return
	endif
	execute 'wincmd' expr
	if mode() ==# 't'
		quit!
	else
		confirm quit
	endif
enddef
Each h,j,k,l nnoremap q{0} <ScriptCmd>g:QuitWin('{0}')<CR>
nnoremap q <Nop>
nnoremap Q q
# é–‰ã˜ã‚‹
nnoremap <expr> qq $"\<Cmd>confirm {winnr('$') ==# 1 && execute('ls')->split("\n")->len() !=# 1 ? 'bd' : 'q'}\<CR>"
# ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦
nnoremap qa <Cmd>confirm qa<CR>
nnoremap qOw <Cmd>confirm only<CR>
# ã‚¿ãƒ–
nnoremap qt <Cmd>confirm tabclose +<CR>
nnoremap qT <Cmd>confirm tabclose -<CR>
nnoremap q# <Cmd>confirm tabclose #<CR>
nnoremap qOt <Cmd>confirm tabonly<CR>
# ãƒãƒƒãƒ•ã‚¡
nnoremap qb <Cmd>confirm bd<CR>
nnoremap qn <Cmd>bn<CR><Cmd>confirm bd<CR>
nnoremap qp <Cmd>bp<CR><Cmd>confirm bd<CR>
nnoremap <expr> qo $"\<Cmd>vim9cmd confirm bd {range(1, last_buffer_nr())->filter((i, b) => b !=# bufnr() && buflisted(b))->join()}\<CR>"
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆå‹•ä½œã‚’ä¿æŒ
nnoremap q: q:
nnoremap q/ q/
nnoremap q? q?
# é–‹ããªãŠã™
nnoremap qQ <Cmd>e #<1<CR>
#}}} -------------------------------------------------------

# ------------------------------------------------------
# vimrcã€pluginã€colorschemeä½œæˆç”¨ {{{
# ã‚«ãƒ¼ã‚½ãƒ«è¡Œã‚’å®Ÿè¡Œã™ã‚‹ã‚„ã¤
cnoremap <script> <expr> <SID>(exec_line) $'{getline('.')->substitute('^[ \t"#:]\+', '', '')}<CR>'
nnoremap <script> g: :<C-u><SID>(exec_line)
nnoremap <script> g9 :<C-u>vim9cmd <SID>(exec_line)
xnoremap g: "vy:<C-u><C-r>=@v<CR><CR>
xnoremap g9 "vy:<C-u>vim9cmd <C-r>=@v<CR><CR>
# ã‚«ãƒ¼ã‚½ãƒ«ä½ç½®ã®ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’ç¢ºèªã™ã‚‹ã‚„ã¤
nnoremap <expr> <Space>hl $'<Cmd>hi {synID(line('.'), col('.'), 1)->synIDattr('name')->substitute('^$', 'Normal', '')}<CR>'
# ä»–ã®å®šç¾©ã¯.vim/after/ftplugin/vim.vim
#}}}

# ------------------------------------------------------
# ãã®ä»–ç´°ã€…ã—ãŸã® {{{
if has('clipboard')
	au vimrc FocusGained * @" = @+
	au vimrc FocusLost   * @+ = @"
endif

# ã‚«ãƒ¼ã‚½ãƒ«ã®å½¢
if has('vim_starting')
	&t_SI = "\e[6 q"
	&t_EI = "\e[2 q"
	&t_SR = "\e[4 q"
endif

nnoremap <F11> <ScriptCmd>vimrc#myutil#ToggleNumber()<CR>
nnoremap <F12> <Cmd>set wrap!<CR>

nnoremap gs :<C-u>%s///g<Left><Left><Left>
nnoremap gS :<C-u>%s/<C-r>=escape(expand('<cword>'), '^$.*?/\[]')<CR>//g<Left><Left>
xnoremap gs :s///g<Left><Left><Left>
xnoremap gS "vy:<C-u>%s/<C-r>=substitute(escape(@v,'^$.*?/\[]'),"\n",'\\n','g')<CR>//g<Left><Left>

nnoremap <CR> j0
nnoremap Y y$
nnoremap <Space>p $p
nnoremap <Space>P ^P
nnoremap <expr> j (getline('.')->match('\S') + 1 ==# col('.')) ? '+' : 'j'
nnoremap <expr> k (getline('.')->match('\S') + 1 ==# col('.')) ? '-' : 'k'

# `T`å¤šå°‘æ½°ã—ã¦ã‚‚å¤§ä¸ˆå¤«ã ã‚ã†â€¦
nnoremap TE :<C-u>tabe<Space>
nnoremap TN <Cmd>tabnew<CR>
nnoremap TD <Cmd>tabe ./<CR>
nnoremap TT <Cmd>tabnext #<CR>

onoremap <expr> } $"\<Esc>m`0{v:count1}{v:operator}\}"
onoremap <expr> { $"\<Esc>m`V{v:count1}\{{v:operator}"

xnoremap <expr> h mode() ==# 'V' ? '<Esc>h' : 'h'
xnoremap <expr> l mode() ==# 'V' ? '<Esc>l' : 'l'
xnoremap J j
xnoremap K k
xnoremap p P
xnoremap P p

inoremap ï½‹ï½Š <Esc>`^
inoremap ã€Œ ã€Œã€<C-g>U<Left>
inoremap ã€Œã€ ã€Œã€<C-g>U<Left>
inoremap ï¼ˆ ()<C-g>U<Left>
inoremap ï¼ˆï¼‰ ()<C-g>U<Left>

# USé…åˆ—â†’ã€Œ"ã€æŠ¼ã—ã«ãã„ã€JISé…åˆ—â†’ã€Œ'ã€æŠ¼ã—ã«ãã„
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®Mã¯ã‚ã¾ã‚Šä½¿ã‚ãªã„ã‹ãªãâ€¦
nnoremap ' "
nnoremap m '
nnoremap M m

command! -nargs=1  -complete=customlist,vimrc#myutil#HelpList Help vimrc#myutil#Help(<q-args>)
#}}} -------------------------------------------------------

# ------------------------------------------------------
# æ§˜å­è¦‹ä¸­ ä½¿ã‚ãªãã†ãªã‚‰å‰Šé™¤ã™ã‚‹ {{{
au vimrc User InputCR feedkeys("\<C-g>u", 'n')

nnoremap <Space>n <Cmd>nohlsearch<CR>
au vimrc CursorHold * feedkeys(' n') # nohã¯auã§å‹•ã‹ãªã„(:help noh)

# CSVã¨ã‹ã®ãƒ˜ãƒƒãƒ€ã‚’å›ºå®šè¡¨ç¤ºã™ã‚‹ã€‚ãƒ•ã‚¡ãƒ³ã‚¯ã‚·ãƒ§ãƒ³ã‚­ãƒ¼ã˜ã‚ƒãªãã¦ã‚³ãƒãƒ³ãƒ‰å®šç¾©ã™ã‚Œã°ã„ã„ã‹ãªâ€¦
nnoremap <silent> <F10> <ESC>1<C-w>s:1<CR><C-w>w
xnoremap <F10> <ESC>1<C-w>s<C-w>w

# ã“ã“ã¾ã§èª­(y)ã‚“ã 
nnoremap <F9> my
nnoremap <Space><F9> 'y

# syntaxå›ºæœ‰ã®è¿½åŠ å¼·èª¿ {{{
def ClearMySyntax()
	for id in get(w:, 'my_syntax', [])
		silent! matchdelete(id)
	endfor
	w:my_syntax = []
enddef
def AddMySyntax(group: string, pattern: string)
	w:my_syntax->add(matchadd(group, pattern))
enddef
au vimrc Syntax * ClearMySyntax()
# ã‚„ã‚ŠãŒã¡ãªãƒŸã‚¹ã«ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’ä»˜ã‘ã‚‹
au vimrc Syntax javascript {
	AddMySyntax('SpellRare', '\s[=!]=\s')
}
au vimrc Syntax vim {
	AddMySyntax('SpellRare', '\s[=!]=\s')
	AddMySyntax('SpellBad', '\s[=!]==\s')
	AddMySyntax('SpellBad', '\s\~[=!][=#]\?\s')
	AddMySyntax('SpellRare', '\<normal!\@!')
}
#}}}

# yankã—ãŸæ–‡å­—ã‚’echo {{{
set report=9999
# ä»–ã®ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã¨ç«¶åˆã™ã‚‹ã®ã§ã‚¿ã‚¤ãƒãƒ¼ã§é…å»¶ã•ã›ã‚‹
def g:EchoYankText(t: number)
	vimrc#echoyanktext#EchoYankText()
enddef
au vimrc TextYankPost * timer_start(1, g:EchoYankText)
#
#}}}
# é¸æŠä¸­ã®æ–‡å­—æ•°ã‚’ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ— {{{
def PopupVisualLength()
	normal! "vygv
	var text = @v->substitute('\n', '', 'g')
	popup_create($'{strlen(text)}chars', {
		pos: 'botleft',
		line: 'cursor-1',
		col: 'cursor+1',
		moved: 'any',
		padding: [1, 1, 1, 1],
	})
enddef
xnoremap <C-g> <ScriptCmd>PopupVisualLength()<CR>
#}}}

# `:%g!/re/d` ã®çµæœã‚’æ–°è¦ã‚¦ã‚¤ãƒ³ãƒ‰ã‚¦ã«è¡¨ç¤º
# (Buffer Regular Expression Print)
command! -nargs=1 Brep vimrc#myutil#Brep(<q-args>, <q-mods>)

# <C-f>ã¨<C-b>ã€CTRLãŠã—ã£ã±ãŒã¤ã‚‰ã„ã®ã§â€¦
Each f,b nmap <C-{0}> <C-{0}><SID>(hold-ctrl)
Each f,b nnoremap <script> <SID>(hold-ctrl){0} <C-{0}><SID>(hold-ctrl)
nmap <SID>(hold-ctrl) <Nop>

# ğŸ¶ğŸš
onoremap A <Plug>(textobj-twochars-a)
onoremap I <Plug>(textobj-twochars-i)

#noremap <F1> <Cmd>smile<CR>
#}}} -------------------------------------------------------

# ------------------------------------------------------
# â€  ã‚ã¨ã‚‚ã†1å›ã€Œã“ã‚Œä½¿ã£ã¦ãªã„ãªâ€¦ã€ã¨æ€ã£ãŸã¨ãã«æ¶ˆã™ {{{

# ã©ã£ã¡ã‚‚<C-w>wã€‚å·¦æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã¨å³æ‰‹ã‚ªãƒ³ãƒªãƒ¼ã®ãƒãƒƒãƒ”ãƒ³ã‚°
nnoremap <Space>w <C-w>w
nnoremap <Space>o <C-w>w
nnoremap <Space>d "_d

# <Tab>ã§tsvã¨ã‹csvã¨ã‹htmlã®æ¬¡ã®é …ç›®ã«ç§»å‹•
nnoremap <Tab> <Cmd>call search('\(^\\|\t\\|, *\)\S\?', 'e')<CR>
nnoremap <S-Tab> <Cmd>call search('\(^\\|\t\\|, *\)\S\?', 'be')<CR>
au vimrc FileType html,xml,svg {
	nnoremap <buffer> <silent> <Tab> <Cmd>call search('>')<CR><Cmd>call search('\S')<CR>
	nnoremap <buffer> <silent> <S-Tab> <Cmd>call search('>', 'b')<CR><Cmd>call search('>', 'b')<CR><Cmd>call search('\S')<CR>
}

# ã‚¿ãƒ–ã¯å’æ¥­ï¼
nnoremap <Space><Tab>u <Cmd>call vimrc#recentlytabs#ReopenRecentlyTab()<CR>
nnoremap <Space><Tab>l <Cmd>call vimrc#recentlytabs#ShowMostRecentlyClosedTabs()<CR>

#}}} -------------------------------------------------------

# ------------------------------------------------------
# ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆãƒãƒƒãƒ”ãƒ³ã‚°ãƒ‡ãƒ¼ {{{
if strftime('%d') ==# '01'
	au vimrc VimEnter * {
		notification#show("âœ¨ Today, Let's enjoy the default key mapping ! âœ¨")
		mapclear
		imapclear
		xmapclear
		cmapclear
		omapclear
		tmapclear
		# CursorHoldã§feedkyesã—ã¦ã„ã‚‹ã®ã§â€¦
		nnoremap <Space>n <Nop>
	}
endif
#}}} -------------------------------------------------------

# ------------------------------------------------------
# ãƒ¡ãƒ¢ {{{
# <F1> fern <S-F1>ã§ãƒ•ã‚©ãƒ«ãƒ€ã‚’é–‹ã(win32)
# <F2> MRU
# <F3>
# <F4>
# <F5> æ—¥ä»˜é–¢ä¿‚
# <F6>
# <F7>
# <F8>
# <F9> ã“ã“ã¾ã§ã‚ˆã‚“ã 
# <F10> ãƒ˜ãƒƒãƒ€è¡Œã‚’è¡¨ç¤º(ã‚ã‚“ã¾ã‚Šä½¿ã‚ãªã„)
# <F11> è¡Œç•ªå·è¡¨ç¤ºåˆ‡æ›¿
# <F12> æŠ˜ã‚Šè¿”ã—è¡¨ç¤ºåˆ‡æ›¿
#}}} -------------------------------------------------------

export def LazyLoad()
	# nop
enddef
